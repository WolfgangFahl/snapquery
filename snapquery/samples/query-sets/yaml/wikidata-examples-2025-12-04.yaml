domain: wikidata.org
namespace: examples
target_graph_name: wikidata
queries:
- name: Horses (showing some info about them)
  namespace: examples
  domain: wikidata.org
  query_id: horses-showing-some-info-about-them--examples@wikidata.org
  sparql: |2

    # ----
    #Illustrates optional fields, instances of subclasses, language fallback on label service, date to year conversion
    #title: <translate><!--T:745--> Horses on Wikidata</translate>
    SELECT DISTINCT ?horse ?horseLabel ?mother ?motherLabel ?father ?fatherLabel (year(?birthdate) as ?birthyear) (year(?deathdate) as ?deathyear) ?genderLabel
    WHERE
    {
      ?horse wdt:P31/wdt:P279* wd:Q726 .     # Instance of and subclasses of Q726 (horse)

      OPTIONAL{?horse wdt:P25 ?mother .}     # mother
      OPTIONAL{?horse wdt:P22 ?father .}     # father
      OPTIONAL{?horse wdt:P569 ?birthdate .} # date of birth
      OPTIONAL{?horse wdt:P570 ?deathdate .} # date of death
      OPTIONAL{?horse wdt:P21 ?gender .}     # sex or gender

      SERVICE wikibase:label { #BabelRainbow
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,fr,ar,be,bg,bn,ca,cs,da,de,el,en,es,et,fa,fi,he,hi,hu,hy,id,it,ja,jv,ko,nb,nl,eo,pa,pl,pt,ro,ru,sh,sk,sr,sv,sw,te,th,tr,uk,yue,vec,vi,zh"
      }
    }
    ORDER BY ?horse
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Horses_(showing_some_info_about_them)
  title: Horses (showing some info about them)
  description: This query looks at items whose value of  is  or any subclass of ()
    . It displays the value of , , and  and computes the birth year using  and the
    death year using . Items are ordered using the horses' qid.
- name: Cats, with pictures
  namespace: examples
  domain: wikidata.org
  query_id: cats-with-pictures--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:746--> Cats, with pictures</translate>
    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?pic WHERE {
      ?item wdt:P31 wd:Q146;
        wdt:P18 ?pic.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cats,_with_pictures
  title: Cats, with pictures
  description: This query looks at all items with value of  equals to  with a . The
    results are displayed as a gallery with the option '#defaultView:ImageGrid'.
- name: Map of hospitals
  namespace: examples
  domain: wikidata.org
  query_id: map-of-hospitals--examples@wikidata.org
  sparql: |2

    # ----
    #added 2017-08
    #defaultView:Map
    SELECT DISTINCT * WHERE {
      ?item wdt:P31/wdt:P279* wd:Q16917;
            wdt:P625 ?geo .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_hospitals
  title: Map of hospitals
  description: The query displays all items whose value of  is  or any  of  with .
    The results are displayed on a map using 'defaultView:Map'.
- name: Map of hackerspaces using country as color
  namespace: examples
  domain: wikidata.org
  query_id: map-of-hackerspaces-using-country-as-color--examples@wikidata.org
  sparql: |2

    # ----
    #defaultView:Map{"layer":"?countryLabel", "hide":["?geo"]}
    SELECT DISTINCT ?item ?itemLabel ?geo ?countryLabel WHERE {
      ?item wdt:P31 wd:Q1032372;
            wdt:P625 ?geo ;
            wdt:P17 ?country.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY ?countryLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_hackerspaces_using_country_as_color
  title: Map of hackerspaces using country as color
  description: |-
    The query displays on a map ("defaultView:Map") all items whose value of  is  and which have values for properties  and . P17 is also used as the color of the marker.


    Warnings:
    * Items of class  without any value for  are not returned in the query.
    * Items without  are not returned in the query.
    * Items which are instances of a subclass of  are not returned.
- name: Number of humans in Wikidata
  namespace: examples
  domain: wikidata.org
  query_id: number-of-humans-in-wikidata--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:747--> Number of humans in Wikidata</translate>
    SELECT (COUNT(*) AS ?count)
    WHERE {
      ?item wdt:P31 wd:Q5 .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_humans_in_Wikidata
  title: Number of humans in Wikidata
  description: "This query takes all items whose value of  is  and count the total\
    \ number of items using COUNT function.\n\n*  Items used: \n*  Properties used:"
- name: Only truthy values
  namespace: examples
  domain: wikidata.org
  query_id: only-truthy-values--examples@wikidata.org
  sparql: |2

    # ----
    #<translate><!--T:785--> Demonstrates "no value" handling</translate>
    #title: <translate><!--T:748--> Humans without children (only truthy values)</translate>
    SELECT ?human ?humanLabel
    WHERE
    {
      ?human wdt:P31 wd:Q5 .       #find humans
      ?human rdf:type wdno:P40 .   #with at least one truthy P40 (child) statement defined to be "no value"
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Only_truthy_values
  title: Only truthy values
  description: "In the simplest form (using only [[<tvar name=\"truthyLink\">:mw:Wikibase/Indexing/RDF_Dump_Format#Truthy_statements</tvar>|truthy]]\
    \ values):\n\n*  Items used: \n*  Properties used:"
- name: Including non-truthy values
  namespace: examples
  domain: wikidata.org
  query_id: including-non-truthy-values--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:749--> Humans without children (including non-truthy values)</translate>
    SELECT ?human ?humanLabel
    WHERE
    {
      ?human wdt:P31 wd:Q5 .         #find humans
      ?human p:P40 ?childStatement . #with at least one P40 (child) statement
      ?childStatement rdf:type wdno:P40 .   #where the P40 (child) statement is defined to be "no value"
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Including_non-truthy_values
  title: Including non-truthy values
  description: "A similar query which also considers non-truthy (for example, values\
    \ with deprecated rank) \"no value\" statements:\n\n*  Items used: \n*  Properties\
    \ used:"
- name: Humans born in New York City
  namespace: examples
  domain: wikidata.org
  query_id: humans-born-in-new-york-city--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:750--> Humans born in New York City</translate>
    SELECT DISTINCT ?item ?itemLabel ?itemDescription ?sitelinks
    WHERE {
        ?item wdt:P31 wd:Q5;            # Any instance of a human
              wdt:P19/wdt:P131* wd:Q60; # Who was born in any value (eg. a hospital)
    # that has the property of 'administrative area of' New York City or New York City itself.

    # Note that using wdt:P19 wd:Q60;  # Who was born in New York City.
    # Doesn't include humans with the birth place listed as a hospital
    # or an administrative area or other location of New York City.

              wikibase:sitelinks ?sitelinks.

        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY DESC(?sitelinks)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Humans_born_in_New_York_City
  title: Humans born in New York City
  description: "This example highlights the correct way to use the  property, and\
    \ by extension the  property.  is the most specific known place of birth. For\
    \ example, it is known that Donald Trump () was born in the Jamaica Hospital ()\
    \ in New York City. Therefore, he wouldn't show up in direct query for humans\
    \ born in New York City.\n\n*  Items used:  \n*  Properties used:"
- name: Humans who died on a specific date on the English Wikipedia, ordered by label
  namespace: examples
  domain: wikidata.org
  query_id: humans-who-died-on-a-specific-date-on-the-english-wikipedia-ordered-by-label--examples@wikidata.org
  sparql: |2-

    # ----
    # <translate><!--T:786--> Humans who died on August 25 2001 according to the English Wikipedia</translate>
    SELECT ?item ?articlename ?itemLabel ?itemDescription ?sl
    WHERE {
       VALUES ?dod {"+2001-08-25"^^xsd:dateTime}
        ?dod ^wdt:P570 ?item .
        ?item wikibase:sitelinks ?sl .
        ?item ^schema:about ?article .
        ?article schema:isPartOf <https://en.wikipedia.org/>;
        schema:name ?articlename .
      SERVICE wikibase:label
        {
          bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
          ?item rdfs:label ?itemLabel .
          ?item schema:description ?itemDescription .
        }
      BIND(REPLACE(?itemLabel, "^.*(?<! [Vv][ao]n
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Humans_who_died_on_a_specific_date_on_the_English_Wikipedia,_ordered_by_label
  title: Humans who died on a specific date on the English Wikipedia, ordered by label
  description: ''
- name: Items with a Wikispecies sitelink
  namespace: examples
  domain: wikidata.org
  query_id: items-with-a-wikispecies-sitelink--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:787--> illustrates sitelink selection, ";" notation</translate>
    #title: <translate><!--T:751--> Items with a Wikispecies sitelink</translate>
    SELECT ?item ?itemLabel ?article
    WITH {
      SELECT *
      WHERE {
        ?article schema:about ?item ;
          schema:isPartOf <https://species.wikimedia.org/> .
      }
      LIMIT 200
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_with_a_Wikispecies_sitelink
  title: Items with a Wikispecies sitelink
  description: ''
- name: Items about authors with a Wikispecies page
  namespace: examples
  domain: wikidata.org
  query_id: items-about-authors-with-a-wikispecies-page--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:752--> Items about authors with a Wikispecies page</translate>
    SELECT ?author ?authorLabel ?count
    WITH {
      SELECT ?author (COUNT(?paper) AS ?count)
      WHERE {
        ?article schema:about ?author ;
          schema:isPartOf <https://species.wikimedia.org/> .
        ?author wdt:P31 wd:Q5.
        ?paper wdt:P50 ?author.
      }
      GROUP BY ?author
      ORDER BY DESC(?count)
      LIMIT 200
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_about_authors_with_a_Wikispecies_page
  title: Items about authors with a Wikispecies page
  description: ''
- name: Recent events
  namespace: examples
  domain: wikidata.org
  query_id: recent-events--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:753--> Recent events</translate>
    SELECT ?event ?eventLabel ?date
    WITH {
      SELECT DISTINCT ?event ?date
      WHERE {
        # find events
        ?event wdt:P31/wdt:P279* wd:Q1190554.
        # with a point in time or start date
        OPTIONAL { ?event wdt:P585 ?date. }
        OPTIONAL { ?event wdt:P580 ?date. }
        # but at least one of those
        FILTER(BOUND(?date) && DATATYPE(?date) = xsd:dateTime).
        # not in the future, and not more than 31 days ago
        BIND(NOW() - ?date AS ?distance).
        FILTER(0 <= ?distance && ?distance < 31).
      }
      LIMIT 150
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Recent_events
  title: Recent events
  description: ''
- name: Popular eye colors among humans
  namespace: examples
  domain: wikidata.org
  query_id: popular-eye-colors-among-humans--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:754--> Popular eye colors among humans</translate>
    # <translate><!--T:788--> illustrates bubblechart view, count</translate>
    #defaultView:BubbleChart
    SELECT ?eyeColor ?eyeColorLabel ?rgb (COUNT(?human) AS ?count)
    WHERE
    {
      ?human wdt:P31 wd:Q5.
      ?human wdt:P1340 ?eyeColor.
      OPTIONAL { ?eyeColor wdt:P465 ?rgb. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?eyeColor ?eyeColorLabel ?rgb
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Popular_eye_colors_among_humans
  title: Popular eye colors among humans
  description: ''
- name: Humans whose gender we know we don't know
  namespace: examples
  domain: wikidata.org
  query_id: humans-whose-gender-we-know-we-don-t-know--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:755--> Humans whose gender we know we don't know</translate>
    # <translate><!--T:789--> Demonstrates filtering for "unknown value"</translate>
    SELECT ?human ?humanLabel
    WHERE
    {
      ?human wdt:P31 wd:Q5 . # instance of human
      ?human wdt:P21 ?gender .
      FILTER wikibase:isSomeValue(?gender)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Humans_whose_gender_we_know_we_don't_know
  title: Humans whose gender we know we don't know
  description: ''
- name: URLs of Wikipedia in all languages
  namespace: examples
  domain: wikidata.org
  query_id: urls-of-wikipedia-in-all-languages--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:756--> URLs of Wikipedia in all languages</translate>
    SELECT ?wikipedia WHERE {
      ?wikipedia wikibase:wikiGroup "wikipedia".
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#URLs_of_Wikipedia_in_all_languages
  title: URLs of Wikipedia in all languages
  description: This query provides an alternative to scraping the Wikipedia.org portal
    page and various lists/tables of Wikipedias that are out there. (The [[<tvar name=1>mw:Special:MyLanguage/API:sitematrix</tvar>|sitematrix
    API]] is another alternative.)
- name: Names of Wikipedia articles in multiple languages
  namespace: examples
  domain: wikidata.org
  query_id: names-of-wikipedia-articles-in-multiple-languages--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?lang ?name WHERE {
      ?article schema:about wd:Q5 . hint:Prior hint:runFirst true.
      ?article schema:inLanguage ?lang ;
        schema:name ?name ;
        schema:isPartOf [ wikibase:wikiGroup "wikipedia" ] .
      FILTER(?lang in ('en', 'uz', 'ru', 'ko')) .
      FILTER (!CONTAINS(?name, ':')) .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Names_of_Wikipedia_articles_in_multiple_languages
  title: Names of Wikipedia articles in multiple languages
  description: The query retrieves Wikipedia article names (in the main namespace)
    in various languages for the given Q identity. Labels are returned instead of
    real article names.
- name: All items with a property
  namespace: examples
  domain: wikidata.org
  query_id: all-items-with-a-property--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:790--> Sample to query all values of a property</translate>
    # <translate><!--T:791--> Property talk pages on Wikidata include basic queries adapted to each property</translate>
    SELECT
      ?item ?itemLabel
      ?value ?valueLabel
    # <translate><!--T:792--> valueLabel is only useful for properties with item-datatype</translate>
    WHERE
    {
      ?item wdt:P1800 ?value
      # <translate><!--T:793--> change P1800 to another property</translate>
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    # <translate><!--T:794--> remove or change limit for more results</translate>
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_items_with_a_property
  title: All items with a property
  description: ''
- name: All statements of an item containing another item (direct / first-degree connections)
  namespace: examples
  domain: wikidata.org
  query_id: all-statements-of-an-item-containing-another-item-direct-first-degree-connections--examples@wikidata.org
  sparql: |2

    # ----
    #defaultView:Graph
    #TEMPLATE={ "template": { "en": "All statements of ?item containing another item" }, "variables": { "?item": {} } }
    SELECT ?item ?itemLabel ?itemImage ?value ?valueLabel ?valueImage ?edgeLabel WHERE {
      BIND(wd:Q12345 AS ?item)
      ?item ?wdt ?value.
      ?edge a wikibase:Property;
            wikibase:propertyType wikibase:WikibaseItem; # <translate><!--T:795--> note: to show all statements, removing this is not enough, the graph view only shows entities</translate>
            wikibase:directClaim ?wdt.
      OPTIONAL { ?item wdt:P18 ?itemImage. }
      OPTIONAL { ?value wdt:P18 ?valueImage. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_statements_of_an_item_containing_another_item_(direct_/_first-degree_connections)
  title: All statements of an item containing another item (direct / first-degree
    connections)
  description: ''
- name: Wikidata items of Wikipedia articles
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-items-of-wikipedia-articles--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:796--> Returns a list of Wikidata items for a given list of Wikipedia article names</translate>
    # <translate><!--T:797--> List of Wikipedia article names (lemma) is like "WIKIPEDIA ARTICLE NAME"@LANGUAGE CODE with de for German, en for English, etc.</translate>
    # <translate><!--T:798--> Language version and project is defined in schema:isPartOF with de.wikipedia.org for German Wikipedia, es.wikivoyage for Spanish Wikivoyage, etc.</translate>
    SELECT ?lemma ?item WHERE {
      VALUES ?lemma {
        "Wikipedia"@de
        "Wikidata"@de
        "Berlin"@de
        "Technische Universität Berlin"@de
      }
      ?sitelink schema:about ?item;
        schema:isPartOf <https://de.wikipedia.org/>;
        schema:name ?lemma.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_items_of_Wikipedia_articles
  title: Wikidata items of Wikipedia articles
  description: ''
- name: Wikidata items with English ''spoken text audio''
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-items-with-english-spoken-text-audio--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:873--> Wikidata items with English spoken text audio</translate>
    SELECT DISTINCT ?item ?itemLabel WHERE {
      ?item p:P989 ?statement .# check for spoken text audio statement and exclude those without P989 value
      ?statement ps:P989 ?spokentext .
      ?statement pq:P407 ?lang . # get qualifier language
      FILTER(?lang = wd:Q1860) . # filter for EN
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_items_with_English_''spoken_text_audio''
  title: Wikidata items with English ''spoken text audio''
  description: ''
- name: Total population in the Øresund Region
  namespace: examples
  domain: wikidata.org
  query_id: total-population-in-the-øresund-region--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:757--> Total population in the Øresund Region</translate>
    # <translate><!--T:799--> Illustrates the SUM aggregate function</translate>

    SELECT DISTINCT ?area ?areaLabel (sum(?folkm_ngd) as ?total_folkm) # <translate><!--T:800--> the first two variables can be removed</translate>
                                                                       # <translate><!--T:801--> if only the number is needed</translate>
    WHERE {
      ?item wdt:P361 wd:Q297853. # <translate><!--T:802--> part of (P361) Øresund Region (Q297853)</translate>
      ?item wdt:P1082 ?folkm_ngd. # <translate><!--T:803--> population (P1082)</translate>
      ?area wdt:P31 wd:Q1907114. # <translate><!--T:804--> instance of (P31) metropolitan area (Q1907114)</translate>
      ?area wdt:P527 ?item. # <translate><!--T:805--> has part (P527)</translate>
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?area ?areaLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Total_population_in_the_Øresund_Region
  title: Total population in the Øresund Region
  description: ''
- name: Mayors that are any kind of domesticated animal
  namespace: examples
  domain: wikidata.org
  query_id: mayors-that-are-any-kind-of-domesticated-animal--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:758--> Mayors that are any kind of domesticated animal</translate>
    SELECT ?image ?speciesLabel ?mayorLabel ?placeLabel WHERE {
      ?species wdt:P279* wd:Q622852 .
      ?mayor wdt:P31 ?species .

      ?mayor p:P39 ?node .
      ?node ps:P39 wd:Q30185 .
      ?node pq:P642 ?place .
      OPTIONAL {?mayor wdt:P18 ?image}
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mayors_that_are_any_kind_of_domesticated_animal
  title: Mayors that are any kind of domesticated animal
  description: ''
- name: Mayors that are either a dog, a cat or a chicken
  namespace: examples
  domain: wikidata.org
  query_id: mayors-that-are-either-a-dog-a-cat-or-a-chicken--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:806--> Illustrates the VALUES clause.</translate>
    SELECT ?image ?speciesLabel ?mayorLabel ?placeLabel WHERE {
      VALUES ?species {wd:Q144 wd:Q146 wd:Q780}
      ?mayor wdt:P31 ?species .
      ?mayor p:P39 ?node .
      ?node ps:P39 wd:Q30185 .
      ?node pq:P642 ?place .
      OPTIONAL {?mayor wdt:P18 ?image}
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mayors_that_are_either_a_dog,_a_cat_or_a_chicken
  title: Mayors that are either a dog, a cat or a chicken
  description: ''
- name: Items in the Messier Catalog, with image
  namespace: examples
  domain: wikidata.org
  query_id: items-in-the-messier-catalog-with-image--examples@wikidata.org
  sparql: "\n# ----\n#defaultView:ImageGrid\n# <translate><!--T:807--> Items in the\
    \ Messier Catalog</translate>\nSELECT DISTINCT ?item ?itemLabel ?numero (SAMPLE(?image)\
    \ AS ?image)\nWHERE {\n  ?item p:P528 ?catalogStatement .\n  ?catalogStatement\
    \ ps:P528 ?numero .\n  ?catalogStatement pq:P972 wd:Q14530 .\n  OPTIONAL {?item\
    \ wdt:P18 ?image . }\n\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language\
    \ \"[AUTO_LANGUAGE],mul,en\" }\n}\nGROUP BY ?item ?itemLabel ?numero \nORDER BY\
    \ ?numero\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_in_the_Messier_Catalog,_with_image
  title: Items in the Messier Catalog, with image
  description: ''
- name: Brightest stars, with image
  namespace: examples
  domain: wikidata.org
  query_id: brightest-stars-with-image--examples@wikidata.org
  sparql: "\n# ----\n#defaultView:ImageGrid\n# <translate><!--T:808--> Brightest celestial\
    \ bodies</translate>\nSELECT ?star ?starLabel ?images ?apparent_magnitude\nWHERE\
    \ {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"\
    . }\n  { SELECT  ?star ?apparent_magnitude ?images\n    WHERE {   \n      ?star\
    \ wdt:P31 wd:Q523;\n            wdt:P1215 ?apparent_magnitude;\n            wdt:P18\
    \ ?images .\n      FILTER(?apparent_magnitude < 1)\n    } LIMIT 10 \n  }\n} ORDER\
    \ BY (?apparent_magnitude)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Brightest_stars,_with_image
  title: Brightest stars, with image
  description: ''
- name: Map of the world's sign languages with number of practicians
  namespace: examples
  domain: wikidata.org
  query_id: map-of-the-world-s-sign-languages-with-number-of-practicians--examples@wikidata.org
  sparql: "\n# ----\n#title:<translate><!--T:759--> Map of the world's sign languages\
    \ with number of practicians</translate>\n#defaultView:Map\nSELECT ?sign_language\
    \ ?sign_languageLabel ?indigenous_to ?coordinate_location ?practicians WHERE {\n\
    \  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"\
    . }\n  OPTIONAL { ?sign_language wdt:P1098 ?practicians. }\n  OPTIONAL { ?sign_language\
    \ wdt:P17 ?country. }\n  OPTIONAL { ?country wdt:P625 ?coordinate_location. }\n\
    \  ?sign_language wdt:P31 wd:Q34228. \n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_the_world's_sign_languages_with_number_of_practicians
  title: Map of the world's sign languages with number of practicians
  description: ''
- name: Facets of costume design
  namespace: examples
  domain: wikidata.org
  query_id: facets-of-costume-design--examples@wikidata.org
  sparql: |2

    # ----
    #title: Facets of costume design
    SELECT DISTINCT ?item ?itemLabel ?itemDescription ?sitelinks
    WHERE {
        ?item wdt:P1269 wd:Q16331597;            # Any facet of costume design
                           # Edit the q-item (id) above to query other "facets of" statements
              wikibase:sitelinks ?sitelinks.
        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY DESC(?sitelinks)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Facets_of_costume_design
  title: Facets of costume design
  description: "*  Item used:  \n*  Property used:"
- name: Descendants of L. L. Zamenhof that speak or write Esperanto
  namespace: examples
  domain: wikidata.org
  query_id: descendants-of-l-l-zamenhof-that-speak-or-write-esperanto--examples@wikidata.org
  sparql: "\n# ----\n#title: Descendants of L. L. Zamenhof that speak or write Esperanto\n\
    SELECT ?item ?itemLabel ?itemDescription WHERE {\n  wd:Q11758 wdt:P40+ ?item.\
    \ # L. L. Zamenhof (Q11758) has descendant (P40+) ?item\n  ?item wdt:P1412 wd:Q143;\
    \ # ?item speaks or writes (P1412) Esperanto (Q143)\n        wikibase:sitelinks\
    \ ?sitelinks.\n        \n  SERVICE wikibase:label { bd:serviceParam wikibase:language\
    \ \"[AUTO_LANGUAGE],mul,en,eo\". }\n}\nORDER BY DESC(?sitelinks)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Descendants_of_L._L._Zamenhof_that_speak_or_write_Esperanto
  title: Descendants of L. L. Zamenhof that speak or write Esperanto
  description: "*   Items used:  \n*   Properties used:"
- name: Senses on Swedish phrase lexemes with a synonym
  namespace: examples
  domain: wikidata.org
  query_id: senses-on-swedish-phrase-lexemes-with-a-synonym--examples@wikidata.org
  sparql: "\n# ----\n#title: <translate><!--T:760--> Senses on Swedish phrase lexemes\
    \ with a synonym</translate>\n# <translate><!--T:809--> date: 2024-04-25</translate>\n\
    # <translate><!--T:810--> author: So9q</translate>\n# <translate><!--T:811-->\
    \ note: Thanks to Nikki and Ainali for help with selecting the synonym lexeme</translate>\
    \ \nSELECT ?l ?sense ?lemma ?synonym ?synonym_lemma WHERE {\n  ?l   ontolex:sense\
    \ ?sense ; # <translate><!--T:812--> the sense</translate>\n       dct:language\
    \ wd:Q9027 ; # <translate><!--T:813--> the language</translate>\n       wikibase:lemma\
    \ ?lemma ; # <translate><!--T:814--> and the lemma</translate>\n       wikibase:lexicalCategory\
    \ ?category.\n  ?category wdt:P279/wdt:P31* wd:Q187931. # <translate><!--T:815-->\
    \ subclass of phrase</translate>\n  ?sense wdt:P5973 ?synonym_sense .\n  ?synonym\
    \ ontolex:sense ?synonym_sense . # <translate><!--T:816--> select synonym lexeme</translate>\n\
    \  ?synonym wikibase:lemma ?synonym_lemma . # <translate><!--T:817--> get the\
    \ synonym lemma</translate>\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Senses_on_Swedish_phrase_lexemes_with_a_synonym
  title: Senses on Swedish phrase lexemes with a synonym
  description: ''
- name: Senses on English lexemes with an offensive or profanity style statement
  namespace: examples
  domain: wikidata.org
  query_id: senses-on-english-lexemes-with-an-offensive-or-profanity-style-statement--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:761--> Senses on English lexemes with an offensive or profanity style statement</translate>
    # <translate><!--T:818--> date: 2024-01-25</translate>
    # <translate><!--T:819--> author: So9q</translate>
    SELECT ?l ?sense ?lemma ?swearLabel WHERE {
      ?l ontolex:sense ?sense ; # the sense
         dct:language wd:Q1860 ; # the language
         wikibase:lemma ?lemma . # and the lemma
      VALUES ?swear {
        wd:Q184439
        wd:Q545779
      }
      ?sense wdt:P6191 ?swear .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Senses_on_English_lexemes_with_an_offensive_or_profanity_style_statement
  title: Senses on English lexemes with an offensive or profanity style statement
  description: ''
- name: Swedish lexemes missing in SAOB
  namespace: examples
  domain: wikidata.org
  query_id: swedish-lexemes-missing-in-saob--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:762--> Swedish lexemes that are missing in the biggest official printed Swedish dictionary SAOB.</translate>
    # <translate><!--T:820--> date: 2021-10-12</translate>
    # <translate><!--T:821--> author:So9q</translate>
    SELECT ?lemma
    WHERE {
      ?lexemeId dct:language wd:Q9027;
                wikibase:lemma ?lemma;
                wdt:P9660 wd:Q1935308.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Swedish_lexemes_missing_in_SAOB
  title: Swedish lexemes missing in SAOB
  description: ''
- name: Forms in Swedish that have no example demonstrating them
  namespace: examples
  domain: wikidata.org
  query_id: forms-in-swedish-that-have-no-example-demonstrating-them--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:763--> Forms in Swedish that have no example demonstrating them</translate>
    # <translate><!--T:822--> 2021-08-06</translate>
    SELECT ?form ?lemma
    WHERE {
      ?lexemeId dct:language wd:Q9027;
                wikibase:lemma ?lemma;
                ontolex:lexicalForm ?form.
      MINUS {
      ?lexemeId p:P5831 ?statement.
      ?statement ps:P5831 ?example;
                 pq:P6072 [];
                 pq:P5830 ?form_with_example.
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Forms_in_Swedish_that_have_no_example_demonstrating_them
  title: Forms in Swedish that have no example demonstrating them
  description: ''
- name: Get the lemma for an inflected word in English
  namespace: examples
  domain: wikidata.org
  query_id: get-the-lemma-for-an-inflected-word-in-english--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:764--> Get the lemma for an inflected word in English</translate>
    # <translate><!--T:823--> Author: So9q</translate>
    # <translate><!--T:824--> 2021-07-25</translate>
    # <translate><!--T:825--> inspired by <tvar name="1">https://sinaahmadi.github.io/posts/10-essential-sparql-queries-for-lexicographical-data-on-wikidata.html</tvar></translate>
    SELECT DISTINCT ?l ?word ?lemma WHERE {
      VALUES ?word { "bought"@en }
      ?l dct:language wd:Q1860 ;
         wikibase:lemma ?lemma ;
         ontolex:lexicalForm ?form .
      ?form ontolex:representation ?word .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_the_lemma_for_an_inflected_word_in_English
  title: Get the lemma for an inflected word in English
  description: ''
- name: The Swedish alphabet
  namespace: examples
  domain: wikidata.org
  query_id: the-swedish-alphabet--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:765--> The Swedish alphabet</translate>
    # <translate><!--T:826--> Author: So9q</translate>
    # <translate><!--T:827--> 2021-07-22</translate>
    SELECT ?lexemeId ?lemma WHERE {
      ?lexemeId dct:language wd:Q9027;
                wikibase:lemma ?lemma.
      ?lexemeId wikibase:lexicalCategory wd:Q9788
    }
    ORDER BY ?lemma
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_Swedish_alphabet
  title: The Swedish alphabet
  description: ''
- name: Danish phrases
  namespace: examples
  domain: wikidata.org
  query_id: danish-phrases--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:828--> Danish phrases</translate>
    # <translate><!--T:829--> Author: So9q</translate>
    # <translate><!--T:830--> Date: 2024-01-03</translate>
    SELECT ?l ?lemma WHERE {
      ?l dct:language wd:Q9035; # Change language here
         wikibase:lemma ?lemma;
         wikibase:lexicalCategory ?cat.
      ?cat wdt:P279 wd:Q187931.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Danish_phrases
  title: Danish phrases
  description: ''
- name: All forms in Swedish missing a pronunciation for the form with a representation
    identical to the lemma of the lexeme
  namespace: examples
  domain: wikidata.org
  query_id: all-forms-in-swedish-missing-a-pronunciation-for-the-form-with-a-representation-identical-to-the-lemma-of-the-lexeme--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:766--> All forms in Swedish missing a pronunciation for the form with a representation identical to the lemma of the lexeme</translate>
    # So9q 13-01-2021
    SELECT ?l ?lemma ?form ?audio WHERE {
      ?l dct:language wd:Q9027;
         wikibase:lemma ?lemma;
         ontolex:lexicalForm ?form .
      ?form ontolex:representation ?lemma .
      MINUS {?form wdt:P443 ?audio.}
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_forms_in_Swedish_missing_a_pronunciation_for_the_form_with_a_representation_identical_to_the_lemma_of_the_lexeme
  title: All forms in Swedish missing a pronunciation for the form with a representation
    identical to the lemma of the lexeme
  description: ''
- name: Lexemes in English that match an expression
  namespace: examples
  domain: wikidata.org
  query_id: lexemes-in-english-that-match-an-expression--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:831--> Lexemes in English that match an expression</translate>
    SELECT ?lexemeId ?lemma WHERE {
      ?lexemeId dct:language wd:Q1860;
                wikibase:lemma ?lemma.
      # only those lemmas that begin with "pota", i.e. "potato"
      FILTER (regex(?lemma, '^pota.*'))
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexemes_in_English_that_match_an_expression
  title: Lexemes in English that match an expression
  description: ''
- name: Lexemes in Swedish with usage example that demonstrates both a form and a
    sense
  namespace: examples
  domain: wikidata.org
  query_id: lexemes-in-swedish-with-usage-example-that-demonstrates-both-a-form-and-a-sense--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:767--> Lexemes in Swedish with usage example that demonstrates both a form and a sense</translate>
    # So9q 23-12-2020
    SELECT ?lexemeId ?lemma ?example WHERE {
      ?lexemeId dct:language wd:Q9027;
                wikibase:lemma ?lemma.
      ?lexemeId p:P5831 ?statement.
      ?statement ps:P5831 ?example;
                 pq:P6072 [];
                 pq:P5830 [].
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexemes_in_Swedish_with_usage_example_that_demonstrates_both_a_form_and_a_sense
  title: Lexemes in Swedish with usage example that demonstrates both a form and a
    sense
  description: ''
- name: Pictures of noun lexemes in English (picture dictionary à la Wikidata)
  namespace: examples
  domain: wikidata.org
  query_id: pictures-of-noun-lexemes-in-english-picture-dictionary-à-la-wikidata--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:832--> Lexemes in English with picture and description fetched from the concept Q-item</translate>
    # <translate><!--T:833--> NB: when 2 or more pictures occur for the same lemma a preferred rank is missing on the Q-item.</translate>
    # <translate><!--T:834--> Please fix that if you know how.</translate>
    # <translate><!--T:835--> by So9q</translate>
    #defaultView:ImageGrid
    SELECT DISTINCT ?lexemeId ?lemma ?q_concept ?q_conceptDescription ?picture
    WHERE {
      ?lexemeId dct:language wd:Q1860;
                wikibase:lemma ?lemma;
                ontolex:sense ?sense.
      ?sense wdt:P5137 ?q_concept.
      ?q_concept wdt:P18 ?picture.
      # <translate><!--T:836--> if you wish to only browse certain items you can insert a FILTER</translate>
      # <translate><!--T:837--> regular expression here, like in the example above.</translate>
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY lcase(?lemma) # order a-z
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Pictures_of_noun_lexemes_in_English_(picture_dictionary_à_la_Wikidata)
  title: Pictures of noun lexemes in English (picture dictionary à la Wikidata)
  description: ''
- name: German picture dictionary for young children
  namespace: examples
  domain: wikidata.org
  query_id: german-picture-dictionary-for-young-children--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:838--> German picture dictionary for young children</translate>
    # <translate><!--T:839--> NB: when 2 or more pictures occur for the same lemma a preferred rank is missing on the Q-item.</translate>
    # <translate><!--T:840--> Please fix that if you know how.</translate>
    # <translate><!--T:841--> If any inapproriate terms show up it is probably because the sense is missing a correct P5137</translate>
    # <translate><!--T:842--> by So9q</translate>
    #defaultView:ImageGrid
    SELECT DISTINCT ?lexemeId ?lemma ?q_concept ?q_conceptDescription ?picture
    WHERE {
      ?lexemeId dct:language wd:Q188; # change language here
                wikibase:lemma ?lemma;
                ontolex:sense ?sense.
      ?sense wdt:P5137 ?q_concept.
      ?q_concept wdt:P18 ?picture.
      ############
      # <translate><!--T:843--> Exclusions</translate>
      ############
      # <translate><!--T:844--> Exclude out of scope concepts</translate>
      MINUS {?q_concept wdt:P31 wd:Q3624078.}. # countries

      # <translate><!--T:845--> Exclude non suitable concepts</translate>
      VALUES ?minus {
      wd:Q47092 # <translate><!--T:846--> rape</translate>
      wd:Q198   # <translate><!--T:847--> war</translate>
      wd:Q124490 # <translate><!--T:848--> violence</translate>
      wd:Q170382 # <translate><!--T:849--> revolver</translate>
      wd:Q1576   # <translate><!--T:850--> cigar</translate>
      #... <translate><!--T:851--> add yours here</translate>
      }.
      MINUS {?sense wdt:P5137 ?minus.}.

      # <translate><!--T:852--> Exclude senses not suitable for young children:</translate>
      VALUES ?filter {
      wd:Q8102
      wd:Q545779
      wd:Q1521634
      wd:Q184439}.
      FILTER NOT EXISTS {?sense wdt:P6191 ?filter.}.
      # <translate><!--T:853--> if you wish to only browse certain items you can insert a FILTER</translate>
      # <translate><!--T:854--> regular expression here, like in the example above.</translate>
      SERVICE wikibase:label { bd:serviceParam wikibase:language "de,en,mul". }
    }
    ORDER BY lcase(?lemma) # order a-z
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#German_picture_dictionary_for_young_children
  title: German picture dictionary for young children
  description: ''
- name: Lexeme languages by number of usage examples
  namespace: examples
  domain: wikidata.org
  query_id: lexeme-languages-by-number-of-usage-examples--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:768--> Lexeme languages by number of usage examples</translate>
    # <translate><!--T:855--> by Vesihiisi</translate>
    # <translate><!--T:856--> improved by So9q 23-02-2020 to only show those with both a form and a sense,</translate>
    # <translate><!--T:857--> because that's what we really want</translate>

    #defaultView:BubbleChart
    SELECT ?languageLabel (COUNT(?example) AS ?count) WHERE {
      ?l dct:language ?language;
         p:P5831 ?statement.
      ?statement ps:P5831 ?example;
                 pq:P6072 [];
                 pq:P5830 [].
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?languageLabel
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexeme_languages_by_number_of_usage_examples
  title: Lexeme languages by number of usage examples
  description: ''
- name: Lexemes describing a color
  namespace: examples
  domain: wikidata.org
  query_id: lexemes-describing-a-color--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:769--> Lexemes describing a color</translate>
    # <translate><!--T:858--> By Vesihiisi</translate>

    SELECT ?l ?lemma ?languageLabel WHERE {
      ?l dct:language ?language;
         wikibase:lemma ?lemma;
         wdt:P31 wd:Q376431.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ?languageLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexemes_describing_a_color
  title: Lexemes describing a color
  description: ''
- name: Lexemes that means water ordered by language
  namespace: examples
  domain: wikidata.org
  query_id: lexemes-that-means-water-ordered-by-language--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:859--> By So9q</translate>
    # <translate><!--T:860--> Lexemes that means (liquid) water</translate>
    SELECT ?l ?sense ?lemma ?languageLabel WHERE {
      ?l a ontolex:LexicalEntry ; # <translate><!--T:861--> get from the special LexicalEntry</translate>
           ontolex:sense ?sense ; # <translate><!--T:862--> the sense</translate>
           dct:language ?language ; # <translate><!--T:863--> the language</translate>
           wikibase:lemma ?lemma. # <translate><!--T:864--> and the lemma</translate>
      ?sense wdt:P5137 wd:Q29053744 .
                       # <translate><!--T:865--> change Q29053744 to anything you want</translate>
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    # <translate><!--T:866--> Lower case before order</translate>
    ORDER BY (LCASE(?languageLabel))
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexemes_that_means_water_ordered_by_language
  title: Lexemes that means water ordered by language
  description: ''
- name: The 100 most translated concepts in the Lexeme namespace
  namespace: examples
  domain: wikidata.org
  query_id: the-100-most-translated-concepts-in-the-lexeme-namespace--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:867--> by So9q, 2019-11-21</translate>
    #defaultView:BubbleChart
    SELECT ?meaning ?meaningLabel ?count
    WITH {
      SELECT ?meaning (count(?l) as ?count)
      WHERE {
        ?l a ontolex:LexicalEntry ;
          ontolex:sense ?sense.
        ?sense wdt:P5137 ?meaning.
      }
      GROUP BY ?meaning
      ORDER BY desc(?count)
      LIMIT 100
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY desc(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_100_most_translated_concepts_in_the_Lexeme_namespace
  title: The 100 most translated concepts in the Lexeme namespace
  description: ''
- name: Demonyms on map
  namespace: examples
  domain: wikidata.org
  query_id: demonyms-on-map--examples@wikidata.org
  sparql: |2

    # ----
    # <translate><!--T:868--> Words describing people from a certain place</translate>
    # <translate><!--T:869--> e.g. swede</translate>
    # by Vesihiisi
    #defaultView:Map
    SELECT ?l ?lemma ?demoPlaceLabel ?coords WHERE {
      ?l a ontolex:LexicalEntry ;
           dct:language ?language ;
           wikibase:lemma ?lemma .
      ?language wdt:P218 'sv'. # <translate><!--T:870--> language</translate>
      ?l ontolex:sense ?sense.
         ?sense wdt:P6271 ?demoPlace.
      ?demoPlace wdt:P625 ?coords
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Demonyms_on_map
  title: Demonyms on map
  description: ''
- name: Forms in Hebrew that are both feminine and masculine
  namespace: examples
  domain: wikidata.org
  query_id: forms-in-hebrew-that-are-both-feminine-and-masculine--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:770--> Forms of nouns in Hebrew that are both feminine and masculine</translate>
    # by Uziel302
    SELECT ?lexemeId ?lemma ?form ?word WHERE {
      ?lexemeId dct:language wd:Q9288;
                wikibase:lexicalCategory wd:Q1084;
                wikibase:lemma ?lemma;
                ontolex:lexicalForm ?form.
      ?form wikibase:grammaticalFeature wd:Q499327, wd:Q1775415;
            ontolex:representation ?word.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Forms_in_Hebrew_that_are_both_feminine_and_masculine
  title: Forms in Hebrew that are both feminine and masculine
  description: ''
- name: Lexemes by number of statements
  namespace: examples
  domain: wikidata.org
  query_id: lexemes-by-number-of-statements--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:771--> Lexemes by number of statements</translate>
    SELECT * {
      ?l dct:language [];
         wikibase:statements ?c.
    }
    ORDER BY desc(?c) LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Lexemes_by_number_of_statements
  title: Lexemes by number of statements
  description: ''
- name: Standard Mandarin Lexeme Forms missing Pinyin Transliteration
  namespace: examples
  domain: wikidata.org
  query_id: standard-mandarin-lexeme-forms-missing-pinyin-transliteration--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:772--> Standard Mandarin Lexeme Forms missing Pinyin Transliteration</translate>
    #author:0xDeadbeef 2022-07-31
    SELECT ?lexemeId ?lemma ?form ?word WHERE {
      ?lexemeId dct:language wd:Q727694;
        wikibase:lemma ?lemma;
        ontolex:lexicalForm ?form.
      ?form ontolex:representation ?word.
      FILTER(NOT EXISTS { ?form wdt:P1721 ?o. })
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Standard_Mandarin_Lexeme_Forms_missing_Pinyin_Transliteration
  title: Standard Mandarin Lexeme Forms missing Pinyin Transliteration
  description: ''
- name: List of French nouns that have no audio recording added, formatted for LinguaLibre.org
    generator
  namespace: examples
  domain: wikidata.org
  query_id: list-of-french-nouns-that-have-no-audio-recording-added-formatted-for-lingualibre-org-generator--examples@wikidata.org
  sparql: |2

    # ----
    # author: Lea Lacroix (WMDE) 2018-12-18
    #contact: Yug
    SELECT ?id ?label
    WHERE {
      ?l a ontolex:LexicalEntry ; dct:language wd:Q150 ; wikibase:lexicalCategory wd:Q1084 ; ontolex:lexicalForm ?id .
      ?id ontolex:representation ?label .
      FILTER NOT EXISTS { ?id wdt:P443 ?audio. }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_French_nouns_that_have_no_audio_recording_added,_formatted_for_LinguaLibre.org_generator
  title: List of French nouns that have no audio recording added, formatted for LinguaLibre.org
    generator
  description: ''
- name: List of Puno Quechua lexeme-forms that have no audio recording added, formatted
    for LinguaLibre.org generator
  namespace: examples
  domain: wikidata.org
  query_id: list-of-puno-quechua-lexeme-forms-that-have-no-audio-recording-added-formatted-for-lingualibre-org-generator--examples@wikidata.org
  sparql: "\n#author: Elwinlhq 2022-11-20\n#contact: Yug\nSELECT ?id ?label WHERE\
    \ {\n  ?l a ontolex:LexicalEntry ; dct:language wd:Q5218 ; wikibase:lexicalCategory\
    \ wd:Q24905 ; ontolex:lexicalForm ?id .\n  ?id ontolex:representation ?label .\n\
    \  FILTER NOT EXISTS { ?id wdt:P443 ?audio. }  \n  FILTER (LANG(?label) = \"qu-x-q7260479\"\
    ). # retrieve Puno Quechua lexemes/forms: qu-x-Q7260479 !!Lowercase\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_Puno_Quechua_lexeme-forms_that_have_no_audio_recording_added,_formatted_for_LinguaLibre.org_generator
  title: List of Puno Quechua lexeme-forms that have no audio recording added, formatted
    for LinguaLibre.org generator
  description: ''
- name: Properties grouped by their parent property
  namespace: examples
  domain: wikidata.org
  query_id: properties-grouped-by-their-parent-property--examples@wikidata.org
  sparql: |2

    # ----
    #Properties grouped by their parent property
    #TODO: should display links and numeric ids
    #defaultView:Tree
    SELECT ?property2 ?property2Label ?property1 ?property1Label WHERE {
      ?property1 rdf:type wikibase:Property. #not replaceable with wikibase:Item, wikibase:Statement, wikibase:Reference at WDQS
                                             #https://www.mediawiki.org/wiki/Wikibase/Indexing/RDF_Dump_Format#WDQS_data_differences
      ?property1 wdt:P1647 ?property2.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Properties_grouped_by_their_parent_property
  title: Properties grouped by their parent property
  description: ''
- name: Subproperties of <tvar name=p276>{{Property|P276}}</tvar>
  namespace: examples
  domain: wikidata.org
  query_id: subproperties-of-tvar-name-p276-property-p276-tvar--examples@wikidata.org
  sparql: |2

    # ----
    #Subproperties of location (P276)
    SELECT DISTINCT ?subProperties ?subPropertiesLabel WHERE {
      ?subProperties wdt:P1647* wd:P276.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Subproperties_of_<tvar_name=p276>{{Property|P276}}</tvar>
  title: Subproperties of <tvar name=p276>{{Property|P276}}</tvar>
  description: ''
- name: Properties grouped by their <tvar name="qWikibaseDatatype">{{Q|Q19798645}}</tvar>
    with number of properties
  namespace: examples
  domain: wikidata.org
  query_id: properties-grouped-by-their-tvar-name-qwikibasedatatype-q-q19798645-tvar-with-number-of-properties--examples@wikidata.org
  sparql: |2

    # ----
    #Properties grouped by their type with number of properties
    SELECT (COUNT(?property) as ?pcount ) ?wbtype WHERE {
      ?property rdf:type wikibase:Property.
      ?property wikibase:propertyType ?wbtype.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?wbtype
    ORDER BY DESC(?pcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Properties_grouped_by_their_<tvar_name="qWikibaseDatatype">{{Q|Q19798645}}</tvar>_with_number_of_properties
  title: Properties grouped by their <tvar name="qWikibaseDatatype">{{Q|Q19798645}}</tvar>
    with number of properties
  description: ''
- name: Properties used to link to instances of <tvar name="qTechnicalStandard">{{Q|Q317623}}</tvar>
  namespace: examples
  domain: wikidata.org
  query_id: properties-used-to-link-to-instances-of-tvar-name-qtechnicalstandard-q-q317623-tvar--examples@wikidata.org
  sparql: |2

    # ----
    #properties used to link to instances of technical standard
    SELECT DISTINCT ?propertyRel ?propertyItem ?propertyItemLabel WHERE
    {
       ?item ?propertyRel ?standard.
       ?standard wdt:P31 wd:Q317623.
       ?propertyItem wikibase:directClaim ?propertyRel
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Properties_used_to_link_to_instances_of_<tvar_name="qTechnicalStandard">{{Q|Q317623}}</tvar>
  title: Properties used to link to instances of <tvar name="qTechnicalStandard">{{Q|Q317623}}</tvar>
  description: ''
- name: All properties with descriptions and aliases and types
  namespace: examples
  domain: wikidata.org
  query_id: all-properties-with-descriptions-and-aliases-and-types--examples@wikidata.org
  sparql: |
    SELECT ?property ?propertyType ?propertyLabel ?propertyDescription ?propertyAltLabel WHERE {
      ?property wikibase:propertyType ?propertyType .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ASC(xsd:integer(STRAFTER(STR(?property), 'P')))
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_properties_with_descriptions_and_aliases_and_types
  title: All properties with descriptions and aliases and types
  description: ''
- name: Properties connecting items of type <tvar name="qZoo">{{Q|Q43501}}</tvar>
    with items of type <tvar name="qAnimal">{{Q|Q729}}</tvar>
  namespace: examples
  domain: wikidata.org
  query_id: properties-connecting-items-of-type-tvar-name-qzoo-q-q43501-tvar-with-items-of-type-tvar-name-qanimal-q-q729-tvar--examples@wikidata.org
  sparql: |
    SELECT ?p ?pLabel (count (*) as ?count) {
      ?s ?pd ?o .
      ?p wikibase:directClaim ?pd .
      ?s wdt:P31/wdt:P279* wd:Q729 .
      ?o wdt:P31/wdt:P279* wd:Q43501 .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } GROUP BY ?p ?pLabel ORDER BY desc(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Properties_connecting_items_of_type_<tvar_name="qZoo">{{Q|Q43501}}</tvar>_with_items_of_type_<tvar_name="qAnimal">{{Q|Q729}}</tvar>
  title: Properties connecting items of type <tvar name="qZoo">{{Q|Q43501}}</tvar>
    with items of type <tvar name="qAnimal">{{Q|Q729}}</tvar>
  description: ''
- name: Properties likely missing type constraints
  namespace: examples
  domain: wikidata.org
  query_id: properties-likely-missing-type-constraints--examples@wikidata.org
  sparql: "\n# ----\n#title: <translate><!--T:881--> Most Used Properties for Hospitals\
    \ in Wikidata</translate>\n#defaultView:BubbleChart\n# This query retrieves the\
    \ most frequently used properties for items that are instances of a hospital (Q16917).\n\
    # It counts how many times each property is used and displays the results in descending\
    \ order.\n# The visualization is set to a Bubble Chart to provide a visual representation\
    \ of property usage.\n\nSELECT ?property ?propertyLabel ?count\nWHERE {\n  # Subquery\
    \ to calculate the count of each property used by hospital items\n  {\n    SELECT\
    \ \n      (IRI(REPLACE(STR(?prop), \"http://www.wikidata.org/prop/direct/\", \"\
    http://www.wikidata.org/entity/\")) AS ?property)  # Convert direct property to\
    \ property entity\n      (COUNT(*) AS ?count)  # Count the number of occurrences\
    \ for each property\n    WHERE {\n      ?item wdt:P31 wd:Q16917 .  # Select items\
    \ that are instances of a hospital\n      ?item ?prop ?value .        # Retrieve\
    \ all properties and their values for these items\n      FILTER(STRSTARTS(STR(?prop),\
    \ STR(wdt:))) .  # Ensure only direct properties (wdt:) are considered\n    }\n\
    \    GROUP BY ?prop  # Group results by property to aggregate counts\n    ORDER\
    \ BY DESC(?count)  # Order properties by count in descending order\n  }\n  \n\
    \  # Retrieve human-readable labels for the properties\n  SERVICE wikibase:label\
    \ { \n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".  # Fetch labels\
    \ in the user's language or English\n  }\n}\nORDER BY DESC(?count)  # Final ordering\
    \ of the results by count\nLIMIT 1000\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Properties_likely_missing_type_constraints
  title: Properties likely missing type constraints
  description: ''
- name: References
  namespace: examples
  domain: wikidata.org
  query_id: references--examples@wikidata.org
  sparql: |2

    # ----
    # See also the SPARQL manual
    # https://en.wikibooks.org/wiki/SPARQL/WIKIDATA_Qualifiers,_References_and_Ranks
    # In this example we look for statements which assign a specific value (Q51955019)
    # to the property P1343 and then look for references of that property, specifically
    # the string associated to P958 for the reference. May actually bring up references
    # for other P1343 statements; uses the shortened expression syntax with brackets.
    SELECT ?item ?reference
    WHERE {
      ?item wdt:P1343 wd:Q51955019 .
      ?item p:P1343 [ prov:wasDerivedFrom [ pr:P958 ?reference ] ] .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#References
  title: References
  description: ''
- name: Content of a reference for a specific statement
  namespace: examples
  domain: wikidata.org
  query_id: content-of-a-reference-for-a-specific-statement--examples@wikidata.org
  sparql: |2

    # ----
    # See also the SPARQL manual
    # https://en.wikibooks.org/wiki/SPARQL/WIKIDATA_Qualifiers,_References_and_Ranks
    # In this example we look for statements which assign a specific value (Q51955019)
    # to the property P1343 and then look for references of that property, specifically
    # the string associated to P958 for the reference. May actually bring up references
    # for other P1343 statements; uses the shortened expression syntax with brackets.
    SELECT ?item ?reference
    WHERE {
      ?item wdt:P1343 wd:Q51955019 .
      ?item p:P1343 [ prov:wasDerivedFrom [ pr:P958 ?reference ] ] .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Content_of_a_reference_for_a_specific_statement
  title: Content of a reference for a specific statement
  description: ''
- name: Subclasses
  namespace: examples
  domain: wikidata.org
  query_id: subclasses--examples@wikidata.org
  sparql: |2

    # ----
    # This gives a list of classes whose super-classes include both "physical entity" and "non-physical entity".
    # One of these is, presumably, in error.
    # Due to timeouts, it is necessary to explicitly limit the depth.
    SELECT DISTINCT ?item ?itemLabel WHERE {
      {
        SELECT ?item WHERE { ?item (wdt:P279/wdt:P279/(wdt:P279?)/(wdt:P279?)/(wdt:P279?)) wd:Q7048977, wd:Q112276019. }
        LIMIT 300
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,es,fr,de,mul". }
    }
    ORDER BY (?itemLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Subclasses
  title: Subclasses
  description: ''
- name: Some classes with both physical and non-physical super-classes
  namespace: examples
  domain: wikidata.org
  query_id: some-classes-with-both-physical-and-non-physical-super-classes--examples@wikidata.org
  sparql: |2

    # ----
    # This gives a list of classes whose super-classes include both "physical entity" and "non-physical entity".
    # One of these is, presumably, in error.
    # Due to timeouts, it is necessary to explicitly limit the depth.
    SELECT DISTINCT ?item ?itemLabel WHERE {
      {
        SELECT ?item WHERE { ?item (wdt:P279/wdt:P279/(wdt:P279?)/(wdt:P279?)/(wdt:P279?)) wd:Q7048977, wd:Q112276019. }
        LIMIT 300
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,es,fr,de,mul". }
    }
    ORDER BY (?itemLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Some_classes_with_both_physical_and_non-physical_super-classes
  title: Some classes with both physical and non-physical super-classes
  description: ''
- name: Countries that have sitelinks to en.wiki
  namespace: examples
  domain: wikidata.org
  query_id: countries-that-have-sitelinks-to-en-wiki--examples@wikidata.org
  sparql: |
    SELECT ?country ?countryLabel ?article WHERE {

        ?country wdt:P31 wd:Q3624078 . # sovereign state
        ?article schema:about ?country .
        ?article schema:isPartOf <https://en.wikipedia.org/>.

        SERVICE wikibase:label {
           bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en"
        }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Countries_that_have_sitelinks_to_en.wiki
  title: Countries that have sitelinks to en.wiki
  description: ''
- name: Canadian subjects with no English article in Wikipedia
  namespace: examples
  domain: wikidata.org
  query_id: canadian-subjects-with-no-english-article-in-wikipedia--examples@wikidata.org
  sparql: |2-

    # --
    #added before 2019-02

    SELECT ?item ?itemLabel ?cnt WHERE {
    {
      SELECT ?item (COUNT(?sitelink) AS ?cnt) WHERE {
      ?item wdt:P27
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Canadian_subjects_with_no_English_article_in_Wikipedia
  title: Canadian subjects with no English article in Wikipedia
  description: ''
- name: Countries that have a Featured Article on Russian Wikipedia
  namespace: examples
  domain: wikidata.org
  query_id: countries-that-have-a-featured-article-on-russian-wikipedia--examples@wikidata.org
  sparql: |
    SELECT ?sitelink ?itemLabel WHERE {
      ?item wdt:P31 wd:Q6256.
      ?sitelink schema:isPartOf <https://ru.wikipedia.org/>;
         schema:about ?item;
         wikibase:badge wd:Q17437796 . # Sitelink is badged as a Featured Article
        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" } .
    } ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Countries_that_have_a_Featured_Article_on_Russian_Wikipedia
  title: Countries that have a Featured Article on Russian Wikipedia
  description: ''
- name: Numbers of Wikipedia sitelinks for items with {{P|1367}} for each language
  namespace: examples
  domain: wikidata.org
  query_id: numbers-of-wikipedia-sitelinks-for-items-with-p-1367-for-each-language--examples@wikidata.org
  sparql: |
    SELECT ?lang (COUNT(DISTINCT ?article) AS ?count) WHERE {
      hint:Query hint:optimizer "None".
      ?item wdt:P1367 ?yp_id .
      ?article schema:about ?item . # find articles about things with an Art UK artist identifier
      ?article schema:isPartOf / wikibase:wikiGroup "wikipedia" . # only Wikipedia articles
      hint:Prior hint:gearing "forward" .
      # This hint says to search the property chain above from left to right ("forward"),
      # i.e. it is checked if each previously found value for ?article is linked to a Wikipedia.
      # The default ("reverse") would be to search it from right to left, i.e. find all Wikipedia
      # articles first, and then select the intersection with the previously found values of ?article.
      ?article schema:inLanguage ?lang .
    } GROUP BY ?lang
    ORDER BY DESC (?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Numbers_of_Wikipedia_sitelinks_for_items_with_{{P|1367}}_for_each_language
  title: Numbers of Wikipedia sitelinks for items with {{P|1367}} for each language
  description: ''
- name: Titles of articles about Ukrainian villages on Romanian Wikipedia
  namespace: examples
  domain: wikidata.org
  query_id: titles-of-articles-about-ukrainian-villages-on-romanian-wikipedia--examples@wikidata.org
  sparql: |2

    # ----
    #Show titles of articles about Ukrainian villages on Romanian Wikipedia, plus English and Ukrainian labels in Wikidata items
    #added in 2017-05
    SELECT DISTINCT ?item ?LabelEN ?LabelUK ?page_titleRO WHERE {
      # item: is a - village
      ?item wdt:P31 wd:Q532 .
      # item: country - Ukraine
      ?item wdt:P17 wd:Q212 .
      # exists article in item that is ro.wiki
      ?article schema:about ?item ; schema:isPartOf <https://ro.wikipedia.org/> ; schema:name ?page_titleRO .
      # wd labels
      ?item rdfs:label ?LabelEN FILTER (lang(?LabelEN) = "en") .
      ?item rdfs:label ?LabelUK FILTER (lang(?LabelUK) = "uk") .
    }
    LIMIT 300
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Titles_of_articles_about_Ukrainian_villages_on_Romanian_Wikipedia
  title: Titles of articles about Ukrainian villages on Romanian Wikipedia
  description: ''
- name: Wikisource pages for authors of scientific articles
  namespace: examples
  domain: wikidata.org
  query_id: wikisource-pages-for-authors-of-scientific-articles--examples@wikidata.org
  sparql: "\n# ----\n# NOTE - This scholarly query is written for use in WDQS-Main\
    \ UI only  (query.wikidata.org).\n\n#Wikisource pages for authors of scientific\
    \ articles, ordered by Wikisource language\n#added in 2017-09; updated 2025-08\n\
    SELECT DISTINCT ?author ?wikisourceSitelink ?wikisourceLanguage WHERE {\n  hint:Query\
    \ hint:optimizer \"None\" .      \n  {\n    SELECT DISTINCT ?author ?wikisourceSitelink\
    \ ?wikisourceLanguage WHERE {\n      ?wikisourceSitelink schema:isPartOf [ wikibase:wikiGroup\
    \ \"wikisource\" ];\n                      schema:inLanguage ?wikisourceLanguage;\n\
    \                      schema:about ?author.\n    }\n    LIMIT 20000\n  }\n SERVICE\
    \ wdsubgraph:scholarly_articles {\n         SELECT ?author \n           WHERE\
    \ {\n              ?object wdt:P31 wd:Q13442814 ;\n                      wdt:P50\
    \ ?author .     \n         }\n  }\n}\nORDER BY ?wikisourceLanguage\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikisource_pages_for_authors_of_scientific_articles
  title: Wikisource pages for authors of scientific articles
  description: ''
- name: Items with a GTAA id and their articles on the Dutch and English Wikipedia
  namespace: examples
  domain: wikidata.org
  query_id: items-with-a-gtaa-id-and-their-articles-on-the-dutch-and-english-wikipedia--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?gtaa ?_articleEN ?_articleNL where {
      ?item wdt:P1741 ?gtaa. # GTAA id
      OPTIONAL {
        ?_articleEN schema:about ?item.
        ?_articleNL schema:about ?item.
        ?_articleEN schema:isPartOf <https://en.wikipedia.org/>.
        ?_articleNL schema:isPartOf <https://nl.wikipedia.org/>.
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_with_a_GTAA_id_and_their_articles_on_the_Dutch_and_English_Wikipedia
  title: Items with a GTAA id and their articles on the Dutch and English Wikipedia
  description: ''
- name: People deceased in 2018 ordered by the number of sitelinks
  namespace: examples
  domain: wikidata.org
  query_id: people-deceased-in-2018-ordered-by-the-number-of-sitelinks--examples@wikidata.org
  sparql: |2

    SELECT ?person ?personLabel ?died ?sitelinks
    WITH {
      SELECT *
      WHERE {
        ?person wdt:P31 wd:Q5;
          wdt:P570 ?died.
        FILTER (?died >= "2018-01-01T00:00:00Z"^^xsd:dateTime && ?died < "2019-01-01T00:00:00Z"^^xsd:dateTime)
        ?person wikibase:sitelinks ?sitelinks.
      }
      ORDER BY desc(?sitelinks)
      LIMIT 100
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY desc(?sitelinks)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_deceased_in_2018_ordered_by_the_number_of_sitelinks
  title: People deceased in 2018 ordered by the number of sitelinks
  description: ''
- name: List of small monuments and other similar sites with link to Commons category
    (sitelink or P373)
  namespace: examples
  domain: wikidata.org
  query_id: list-of-small-monuments-and-other-similar-sites-with-link-to-commons-category-sitelink-or-p373--examples@wikidata.org
  sparql: |
    SELECT ?item ?comm ?p373 ?cat WHERE {
      ?item wdt:P131* wd:Q894107. # ... municipality ...

      OPTIONAL {?comm schema:about ?item ; schema:isPartOf <https://commons.wikimedia.org/> .}
      BIND(replace(wikibase:decodeUri(SUBSTR(STR(?comm), 45)),"_"," ") AS ?comm_decode)
       OPTIONAL {?item wdt:P373 ?p373 .}

     bind(COALESCE(?comm_decode, ?p373) as ?cat) .
     VALUES ?trida { wd:Q1746392 wd:Q108325 wd:Q4989906 wd:Q10861631 wd:Q15077340 wd:Q1516537 wd:Q47008262} # chapel, small monument, memorial, belltower, memorial monument, technical monument, group of small monuments
     ?item wdt:P31/wdt:P279* ?trida. # small monument
      }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_small_monuments_and_other_similar_sites_with_link_to_Commons_category_(sitelink_or_P373)
  title: List of small monuments and other similar sites with link to Commons category
    (sitelink or P373)
  description: ''
- name: Most famous child of a librarian
  namespace: examples
  domain: wikidata.org
  query_id: most-famous-child-of-a-librarian--examples@wikidata.org
  sparql: "\n# ----\n#Children of librarians with the most number of sitelinks (as\
    \ a proxy for fame)\nSELECT ?person ?personLabel ?parentLabel ?linkcount WHERE\
    \ {\n    ?parent wdt:P106 wd:Q182436 .\n    ?parent wdt:P40 ?person .\n    ?person\
    \ wikibase:sitelinks ?linkcount .\n  #Remove comment below to only see humans\
    \ (as in no fictional humans)\n  #FILTER EXISTS {?person wdt:P31 wd:Q5} \n  SERVICE\
    \ wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en,de,es,ar,fr\"\
    \ }\n}\nGROUP BY ?linkcount ?person ?personLabel ?parent ?parentLabel\nORDER BY\
    \ DESC(?linkcount)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_famous_child_of_a_librarian
  title: Most famous child of a librarian
  description: ''
- name: All languages with a Wikimedia language code (P424)
  namespace: examples
  domain: wikidata.org
  query_id: all-languages-with-a-wikimedia-language-code-p424--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:773--> All languages with a Wikimedia language code (P424)</translate>
    # Date: 2021-09-24
    SELECT DISTINCT ?lang_code ?itemLabel ?item
    WHERE
    {
      # ?lang is one of these options
      VALUES ?lang {
        wd:Q34770   # language
        wd:Q436240  # ancient language
        wd:Q1288568 # modern language
        wd:Q33215   # constructed language
      }
      ?item wdt:P31 ?lang ;
        # get the language code
        wdt:P424 ?lang_code .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY ?lang_code
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_languages_with_a_Wikimedia_language_code_(P424)
  title: All languages with a Wikimedia language code (P424)
  description: ''
- name: All Wikipedia sites
  namespace: examples
  domain: wikidata.org
  query_id: all-wikipedia-sites--examples@wikidata.org
  sparql: |2

    # ----
    # Get all Wikipedia sites
    SELECT ?item ?itemLabel ?website
    WHERE
    {
      #?item wdt:P31 wd:Q10876391.
      ?item wdt:P856 ?website.
      ?website wikibase:wikiGroup "wikipedia".
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_Wikipedia_sites
  title: All Wikipedia sites
  description: ''
- name: Featured articles of all Wikimedia projects
  namespace: examples
  domain: wikidata.org
  query_id: featured-articles-of-all-wikimedia-projects--examples@wikidata.org
  sparql: |2

    # ----
    # Get all Featured Articles (Q17437796)
    SELECT ?lang ?name ?itemLabel ?sitelink ?linkcount ?item WHERE {
      ?item wikibase:sitelinks ?linkcount.
      ?sitelink schema:name ?name;
        schema:inLanguage ?lang;
        schema:about ?item;
        # Sitelink is badged as a Featured Article
        wikibase:badge wd:Q17437796.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Featured_articles_of_all_Wikimedia_projects
  title: Featured articles of all Wikimedia projects
  description: ''
- name: People born in Lisbon without articles on ptwiki but with articles on other
    Wikipedias
  namespace: examples
  domain: wikidata.org
  query_id: people-born-in-lisbon-without-articles-on-ptwiki-but-with-articles-on-other-wikipedias--examples@wikidata.org
  sparql: |2-

    # ----
    # People born in Lisbon that don't have an article on Portuguese Wikipedia
    # but do have an article on other Wikipedias.
    SELECT DISTINCT ?person ?personLabel WHERE
    {
      ?person wdt:P31 wd:Q5. # Instance of human
      ?person (wdt:P19
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_born_in_Lisbon_without_articles_on_ptwiki_but_with_articles_on_other_Wikipedias
  title: People born in Lisbon without articles on ptwiki but with articles on other
    Wikipedias
  description: ''
- name: List of French communes in Finistère, formatter for Lingualibre.org list generator
  namespace: examples
  domain: wikidata.org
  query_id: list-of-french-communes-in-finistère-formatter-for-lingualibre-org-list-generator--examples@wikidata.org
  sparql: |2

    # ----
    # author: Yug 2024-12-11
    # https://w.wiki/CNrc
    SELECT ?id ?label
    WHERE {
      ?id wdt:P31 wd:Q484170;  # item is `commune de france` (Q484170)
          wdt:P131 wd:Q3389.   # item is `located in administrative entity` (P131) of `finistere` (Q3389)
      ?id rdfs:label ?label.   # fetch labels
      FILTER(lang(?label) = "fr") # keep French ones
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_French_communes_in_Finistère,_formatter_for_Lingualibre.org_list_generator
  title: List of French communes in Finistère, formatter for Lingualibre.org list
    generator
  description: ''
- name: List of present-day countries and capital(s)
  namespace: examples
  domain: wikidata.org
  query_id: list-of-present-day-countries-and-capital-s--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?country ?countryLabel ?capital ?capitalLabel
    WHERE
    {
      ?country wdt:P31 wd:Q3624078 .
      #not a former country
      FILTER NOT EXISTS {?country wdt:P31 wd:Q3024240}
      #and no an ancient civilisation (needed to exclude ancient Egypt)
      FILTER NOT EXISTS {?country wdt:P31 wd:Q28171280}
      OPTIONAL { ?country wdt:P36 ?capital } .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY ?countryLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_present-day_countries_and_capital(s)
  title: List of present-day countries and capital(s)
  description: ''
- name: UN member states
  namespace: examples
  domain: wikidata.org
  query_id: un-member-states--examples@wikidata.org
  sparql: "\n# ----\n#title: <translate>UN member states</translate>\n\nSELECT DISTINCT\
    \ ?state ?stateLabel WHERE {\n  ?state wdt:P31/wdt:P279* wd:Q3624078;\n      \
    \   p:P463 ?memberOfStatement.\n  ?memberOfStatement a wikibase:BestRank;\n  \
    \                   ps:P463 wd:Q1065.\n                     \n  FILTER( ?state\
    \ != wd:Q124653007)  # Exclude \"realm of United Kingdom\" (duplicates \"United\
    \ Kingdom\")\n  MINUS { ?memberOfStatement pq:P582 ?endTime. }\n  MINUS { ?state\
    \ wdt:P576"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#UN_member_states
  title: UN member states
  description: ''
- name: Largest cities per country
  namespace: examples
  domain: wikidata.org
  query_id: largest-cities-per-country--examples@wikidata.org
  sparql: |2

    # ----
    #Largest cities per country
    SELECT DISTINCT ?city ?cityLabel ?population ?country ?countryLabel ?loc WHERE {
      {
        SELECT (MAX(?population_) AS ?population) ?country WHERE {
          ?city wdt:P31/wdt:P279* wd:Q515 .
          ?city wdt:P1082 ?population_ .
          ?city wdt:P17 ?country .
        }
        GROUP BY ?country
        ORDER BY DESC(?population)
      }
      ?city wdt:P31/wdt:P279* wd:Q515 .
      ?city wdt:P1082 ?population .
      ?city wdt:P17 ?country .
      ?city wdt:P625 ?loc .
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
      }
    }
    ORDER BY DESC(?population)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Largest_cities_per_country
  title: Largest cities per country
  description: "* Items used: \n* Properties used:"
- name: Wikidata people per million inhabitants for all EU countries
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-people-per-million-inhabitants-for-all-eu-countries--examples@wikidata.org
  sparql: |2

    # ----
    #Interesting maintenance query that shows the relative prominence of a country's current (living) population on Wikidata. The query tends to time out when using all countries at once, but it might be possible to get the figures for each individual country by using uncommenting the line as indicated below
    SELECT
      ?country ?countryLabel
      ?wikiPersons
      ?population
      (ROUND(?wikiPersons/?population*1000000) AS ?wikiPersonsPerM)
    WHERE
    {
      { SELECT ?country (count(*) as ?wikiPersons) WHERE {
        {SELECT DISTINCT ?person ?country WHERE {
          wd:Q458 wdt:P150 ?country . # European Union contains administrative territorial entity
          # BIND( wd:Q30 AS ?country ) # use instead of previous line to check individual countries
          ?person wdt:P31 wd:Q5 .
          ?person wdt:P27 ?country .
          FILTER NOT EXISTS{ ?person wdt:P570 ?date } # only count living people
        } }
      } GROUP BY ?country }
      ?country wdt:P1082 ?population
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY DESC(?wikiPersonsPerM)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_people_per_million_inhabitants_for_all_EU_countries
  title: Wikidata people per million inhabitants for all EU countries
  description: ''
- name: Papers about Wikidata
  namespace: examples
  domain: wikidata.org
  query_id: papers-about-wikidata--examples@wikidata.org
  sparql: |2

    #papers about Wikidata
    SELECT ?item ?itemLabel
    WHERE {
      ?item wdt:P921 wd:Q2013. hint:Prior hint:runFirst true.
      ?item wdt:P31/wdt:P279* wd:Q191067.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Papers_about_Wikidata
  title: Papers about Wikidata
  description: "* Items used:  \n* Properties used:"
- name: Countries sorted by population
  namespace: examples
  domain: wikidata.org
  query_id: countries-sorted-by-population--examples@wikidata.org
  sparql: |
    # defaultView:BubbleChart
    SELECT DISTINCT ?countryLabel ?population
    {
      ?country wdt:P31 wd:Q6256 ;
               wdt:P1082 ?population .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    GROUP BY ?population ?countryLabel
    ORDER BY DESC(?population)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Countries_sorted_by_population
  title: Countries sorted by population
  description: "* Items used: \n* Properties used:"
- name: Country populations together with total city populations
  namespace: examples
  domain: wikidata.org
  query_id: country-populations-together-with-total-city-populations--examples@wikidata.org
  sparql: |2

    SELECT ?country ?countryLabel ?population ?totalCityPopulation (?population / ?totalCityPopulation AS ?ratio) {
      ?country wdt:P1082 ?population .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
      {
        SELECT ?country (SUM(?cityPopulation) AS ?totalCityPopulation) WHERE {
          ?city wdt:P31 wd:Q515 .
          ?city wdt:P17 ?country .
          ?city wdt:P1082 ?cityPopulation .
        } GROUP BY ?country
      }
    } ORDER BY ?ratio
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Country_populations_together_with_total_city_populations
  title: Country populations together with total city populations
  description: |-
    Lists for each country the population and the total of the populations of all cities of this country.
    If data were complete and populations were measured at the same time for each country and the respective cities then the ratio would be 1.
- name: Names of African countries in all their official languages and English
  namespace: examples
  domain: wikidata.org
  query_id: names-of-african-countries-in-all-their-official-languages-and-english--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?item ?itemLabel_ol ?official_language ?itemLabel_en WHERE {
      ?item wdt:P30 wd:Q15 ;
            wdt:P37 ?officiallanguage ;
            wdt:P31 wd:Q6256 .
      ?officiallanguage wdt:P424 ?langcode .
      ?item rdfs:label ?itemLabel_ol . FILTER(lang(?itemLabel_ol)=?langcode)
      ?item rdfs:label ?itemLabel_en . FILTER(lang(?itemLabel_en)='en')
      ?officiallanguage rdfs:label ?official_language . FILTER(lang(?official_language)='en')
    }
    ORDER BY ?item ?itemLabel_ol ?official_language
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Names_of_African_countries_in_all_their_official_languages_and_English
  title: Names of African countries in all their official languages and English
  description: ''
- name: Languages and dialects spoken in the Netherlands with their optional Wikipedia
    editions
  namespace: examples
  domain: wikidata.org
  query_id: languages-and-dialects-spoken-in-the-netherlands-with-their-optional-wikipedia-editions--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?language ?languageLabel ?wikipediaLabel where {
      ?language wdt:P31 ?instance;
                wdt:P17 wd:Q55.
      FILTER (?instance in (wd:Q34770, wd:Q33384)).
      OPTIONAL {
        ?wikipedia wdt:P31 wd:Q10876391;
                   wdt:P407 ?language.
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,nl,en". }
    } GROUP BY ?language ?languageLabel ?wikipediaLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Languages_and_dialects_spoken_in_the_Netherlands_with_their_optional_Wikipedia_editions
  title: Languages and dialects spoken in the Netherlands with their optional Wikipedia
    editions
  description: ''
- name: Population of cities and towns in Denmark and their OSM relation id
  namespace: examples
  domain: wikidata.org
  query_id: population-of-cities-and-towns-in-denmark-and-their-osm-relation-id--examples@wikidata.org
  sparql: |2

    # ----
    # Population of cities and towns in Denmark and their OSM relation id
    # This shows a simple OR using VALUES and a variable
    SELECT ?city ?cityLabel ?population ?OSM_relation_ID WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      VALUES ?town_or_city {
        wd:Q3957
        wd:Q515
      }
      ?city (wdt:P31/(wdt:P279*)) ?town_or_city;
        wdt:P17 wd:Q35.
      OPTIONAL { ?city wdt:P1082 ?population. }
      OPTIONAL { ?city wdt:P402 ?OSM_relation_ID. }
    }
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Population_of_cities_and_towns_in_Denmark_and_their_OSM_relation_id
  title: Population of cities and towns in Denmark and their OSM relation id
  description: ''
- name: Former capitals
  namespace: examples
  domain: wikidata.org
  query_id: former-capitals--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT DISTINCT ?country ?countryLabel ?capital ?capitalLabel ?coordinates ?ended
    WHERE
    {
      ?country p:P36 ?stat.
      ?stat ps:P36 ?capital.
      ?capital wdt:P625 ?coordinates.
      OPTIONAL {
        ?country wdt:P582{{!}}wdt:P576 ?ended.
      }
      OPTIONAL {
        ?capital wdt:P582{{!}}wdt:P576 ?ended.
      }
      OPTIONAL {
        ?stat pq:P582 ?ended.
      }
      FILTER(BOUND(?ended)).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Former_capitals
  title: Former capitals
  description: '*  Properties used:'
- name: Largest cities of the world
  namespace: examples
  domain: wikidata.org
  query_id: largest-cities-of-the-world--examples@wikidata.org
  sparql: |2

    #defaultView:BubbleChart
    SELECT ?cityLabel ?population ?gps
    WITH {
      SELECT DISTINCT *
      WHERE {
        ?city wdt:P31/wdt:P279* wd:Q515 .
        ?city wdt:P1082 ?population .
        ?city wdt:P625 ?gps .
      }
      ORDER BY DESC(?population)
      LIMIT 100
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY DESC(?population)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Largest_cities_of_the_world
  title: Largest cities of the world
  description: ''
- name: Cities as big as Eindhoven give or take 1000
  namespace: examples
  domain: wikidata.org
  query_id: cities-as-big-as-eindhoven-give-or-take-1000--examples@wikidata.org
  sparql: |2

    #defaultView:Map
    SELECT ?city ?cityLabel ?location ?populatie2 WHERE {
      { SELECT DISTINCT * WHERE {
        wd:Q9832 wdt:P1082 ?populatie .
        ?city wdt:P1082 ?populatie2 ;
          wdt:P625 ?location .
        FILTER (abs(?populatie - ?populatie2) < 1000)
      } }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,nl" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cities_as_big_as_Eindhoven_give_or_take_1000
  title: Cities as big as Eindhoven give or take 1000
  description: ''
- name: Where in the world is Antwerp
  namespace: examples
  domain: wikidata.org
  query_id: where-in-the-world-is-antwerp--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT DISTINCT ?settlement ?name ?coor
    WHERE
    {

       ?subclass_settlement wdt:P279+ wd:Q486972 .
       ?settlement wdt:P31 ?subclass_settlement ;
                   wdt:P625 ?coor ;
                    rdfs:label ?name .
       FILTER regex(?name, "Antwerp", "i")

    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Where_in_the_world_is_Antwerp
  title: Where in the world is Antwerp
  description: ''
- name: Destinations from Antwerp International airport
  namespace: examples
  domain: wikidata.org
  query_id: destinations-from-antwerp-international-airport--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT ?connectsairport ?connectsairportLabel ?place_served ?place_servedLabel ?coor
    WHERE
    {
      VALUES ?airport { wd:Q17480 } # Antwerp international airport wd:Q17480
      ?airport wdt:P81 ?connectsairport ;
               wdt:P625 ?base_airport_coor .
      ?connectsairport wdt:P931 ?place_served ;
                       wdt:P625 ?coor .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Destinations_from_Antwerp_International_airport
  title: Destinations from Antwerp International airport
  description: ''
- name: Cities connected by the European route E40
  namespace: examples
  domain: wikidata.org
  query_id: cities-connected-by-the-european-route-e40--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT ?city ?cityLabel ?coordinates
    WHERE
    {
       VALUES ?highway {wd:Q327162 }
       ?highway wdt:P2789 ?city .
        ?city wdt:P625 ?coordinates .
       SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cities_connected_by_the_European_route_E40
  title: Cities connected by the European route E40
  description: ''
- name: Cities connected by the Trans-Mongolian and Trans-Siberian Railway
  namespace: examples
  domain: wikidata.org
  query_id: cities-connected-by-the-trans-mongolian-and-trans-siberian-railway--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?city ?cityLabel ?coordinates
    WHERE
    {
       VALUES ?highway { wd:Q559037 wd:Q58767 }
       ?highway wdt:P2789 ?city .
        ?city wdt:P625 ?coordinates .
       SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cities_connected_by_the_Trans-Mongolian_and_Trans-Siberian_Railway
  title: Cities connected by the Trans-Mongolian and Trans-Siberian Railway
  description: ''
- name: Metro station of city with template
  namespace: examples
  domain: wikidata.org
  query_id: metro-station-of-city-with-template--examples@wikidata.org
  sparql: |2

    # ----
    #TEMPLATE={"template":"metro station of ?city","variables":{"?city":{"query":"SELECT DISTINCT ?cityLabel where {?city wdt:P31/wdt:P279* wd:Q1637706. SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"} }"} } }
    #defaultView:Map{"hide":["?comm1Label","?coord1", "?comm2","coord2","?layer","?str","?line", "?rgb"],"layer":"?connectingLine1Label"}
    SELECT ?comm1 ?comm1Label ?coord1 ?comm2 ?coord2 ?line ?connectingLine1 ?connectingLine1Label ?layer ?rgb WHERE {
      BIND(wd:Q8686 AS ?city)
      ?comm1 wdt:P31/wdt:P279* wd:Q928830 ;
             wdt:P131*/wdt:P279* ?city;
             wdt:P625 ?coord1 ;
             wdt:P81 ?connectingLine1;
             wdt:P197 ?comm2 .
      ?connectingLine1 wdt:P465 ?rgb.
      ?comm2 wdt:P81 ?connectingLine2;
             wdt:P625 ?coord2 .
      FILTER (?connectingLine1 = ?connectingLine2)

      ?comm1 p:P625 [# ps:P625 [];
                      psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ] .
      ?comm2 p:P625 [# ps:P625 [];
                      psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ] .

      BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .
      BIND(STRDT(?str, geo:wktLiteral) AS ?line) .
      BIND(?connectingLine1 AS ?layer)

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }

    ORDER BY (?connectingLine1Label)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Metro_station_of_city_with_template
  title: Metro station of city with template
  description: ''
- name: Cities connected to Paramaribo (Suriname) by main roads
  namespace: examples
  domain: wikidata.org
  query_id: cities-connected-to-paramaribo-suriname-by-main-roads--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT DISTINCT ?connection ?connectionLabel ?connectedWith ?connectedWithLabel ?coor
    WHERE
    {
      VALUES ?city {wd:Q3001} # wd:Q3001 = Paramaribo}
      ?connection wdt:P2789+ ?city ;
                  wdt:P2789+ ?connectedWith .
      ?connectedWith wdt:P625 ?coor .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cities_connected_to_Paramaribo_(Suriname)_by_main_roads
  title: Cities connected to Paramaribo (Suriname) by main roads
  description: ''
- name: Names of 100 cities with a population larger than 1000000 in the native languages
    of their countries
  namespace: examples
  domain: wikidata.org
  query_id: names-of-100-cities-with-a-population-larger-than-1000000-in-the-native-languages-of-their-countries--examples@wikidata.org
  sparql: |2

    # ----
    # Show the names of 100 cities with a population larger than 1000000 in the native languages of their countries
    SELECT ?city ?cityLabel ?country ?countryLabel ?lang ?langLabel ?langCode ?population
    WHERE
    {
      ?city wdt:P1082 ?population . hint:Prior hint:runFirst true.

      FILTER(?population>1000000)

      ?city wdt:P31 wd:Q515;
            wdt:P17 ?country;
            rdfs:label ?cityLabel .
      ?country wdt:P37 ?lang;
               rdfs:label ?countryLabel .
      ?lang wdt:P424 ?langCode;
            rdfs:label ?langLabel .

      FILTER(lang(?cityLabel)=?langCode)
      FILTER(lang(?countryLabel)=?langCode)
      FILTER(lang(?langLabel)=?langCode)
    } LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Names_of_100_cities_with_a_population_larger_than_1000000_in_the_native_languages_of_their_countries
  title: Names of 100 cities with a population larger than 1000000 in the native languages
    of their countries
  description: ''
- name: grouped by dist range, color-coded
  namespace: examples
  domain: wikidata.org
  query_id: grouped-by-dist-range-color-coded--examples@wikidata.org
  sparql: |2

    #defaultView:BarChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from ?to ?distGrp WHERE {
      {
        SELECT ?from ?to ?distNum ?mun ?mun2 WHERE {
          { SELECT ?mun (SAMPLE(?loc) AS ?loc)
            WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                         wdt:P131 var_area:;
                         wdt:P625 ?loc. }
            GROUP BY ?mun
          }
          OPTIONAL {
            { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
              WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                           wdt:P131 var_area:;
                           wdt:P625 ?loc. }
              GROUP BY ?mun
            }
          }
          BIND(geof:distance(?loc, ?loc2) AS ?distNum).

          SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
            ?mun rdfs:label ?from.
            ?mun2 rdfs:label ?to.
          }
        }
      }
      FILTER(CONCAT(?from,STR(?mun)) <= CONCAT(?to,STR(?mun2))).
      #BIND(IF(STR(?from) < STR(?to),CONCAT(?from," <--> ",?to),
      # CONCAT(?to," <--> ",?from)) AS ?distLabel).

      BIND(COALESCE(
        IF(?distNum >= 40, "40 - .. km", 1/0),
        IF(?distNum >= 30, "30 - 40 km", 1/0),
        IF(?distNum >= 20, "20 - 30 km", 1/0),
        IF(?distNum >= 10, "10 - 20 km", 1/0),
        IF(?distNum >= 5, "05 - 10 km", 1/0),
        IF(?distNum >= 1, "01 - 05 km", "00 - 01 km")) AS ?distGrp).
    }
    ORDER BY ?from ?distGrp
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_by_dist_range,_color-coded
  title: grouped by dist range, color-coded
  description: '='
- name: grouped per municipality on x-axis, alphabetically
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-alphabetically--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from (?distGrp1 AS ?kilometers) ?to WHERE {
      { SELECT ?mun (SAMPLE(?loc) AS ?loc)
        WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                     wdt:P131 var_area:;
                     wdt:P625 ?loc. }
        GROUP BY ?mun
      }
      OPTIONAL {
        { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
          WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                       wdt:P131 var_area:;
                       wdt:P625 ?loc. }
          GROUP BY ?mun
        }
      }
      BIND(geof:distance(?loc, ?loc2) AS ?distNum).
      BIND(STR(ROUND(?distNum)) AS ?distGrp0).
      BIND(CONCAT(SUBSTR("000",STRLEN(?distGrp0)+1),?distGrp0,".",STR(ROUND((?distNum-FLOOR(?distNum))*10))," km") AS ?distGrp1).

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?from ?kilometers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_alphabetically
  title: grouped per municipality on x-axis, alphabetically
  description: '='
- name: grouped per municipality on x-axis, by sum of dist
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-by-sum-of-dist--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from (?distNum AS ?kilometers) ?to WHERE {
      { SELECT ?mun (SAMPLE(?loc) AS ?loc)
        WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                     wdt:P131 var_area:;
                     wdt:P625 ?loc. }
        GROUP BY ?mun
      }
      OPTIONAL {
        { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
          WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                       wdt:P131 var_area:;
                       wdt:P625 ?loc. }
          GROUP BY ?mun
        }
      }
      BIND(geof:distance(?loc, ?loc2) AS ?distNum).

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?from ?kilometers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_by_sum_of_dist
  title: grouped per municipality on x-axis, by sum of dist
  description: '='
- name: grouped per municipality on x-axis, animated per municipality on x-axis
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-animated-per-municipality-on-x-axis--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from (?distNum AS ?kilometers) ?to (?from AS ?animation) WHERE {
      { SELECT ?mun (SAMPLE(?loc) AS ?loc)
        WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                     wdt:P131 var_area:;
                     wdt:P625 ?loc. }
        GROUP BY ?mun
      }
      OPTIONAL {
        { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
          WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                       wdt:P131 var_area:;
                       wdt:P625 ?loc. }
          GROUP BY ?mun
        }
      }
      BIND(geof:distance(?loc, ?loc2) AS ?distNum).

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?from ?kilometers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_animated_per_municipality_on_x-axis
  title: grouped per municipality on x-axis, animated per municipality on x-axis
  description: '='
- name: grouped per municipality on x-axis, animated per municipality on z-axis
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-animated-per-municipality-on-z-axis--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from (?distNum AS ?kilometers) ?to (?to AS ?animation) WHERE {
      { SELECT ?mun (SAMPLE(?loc) AS ?loc)
        WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                     wdt:P131 var_area:;
                     wdt:P625 ?loc. }
        GROUP BY ?mun
      }
      OPTIONAL {
        { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
          WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                       wdt:P131 var_area:;
                       wdt:P625 ?loc. }
          GROUP BY ?mun
        }
      }
      BIND(geof:distance(?loc, ?loc2) AS ?distNum).

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?from ?kilometers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_animated_per_municipality_on_z-axis
  title: grouped per municipality on x-axis, animated per municipality on z-axis
  description: '='
- name: grouped per municipality on x-axis, animated by fixed dist range groups
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-animated-by-fixed-dist-range-groups--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from (?distNum AS ?kilometers) ?to (?distGrp AS ?animation) WHERE {
      { SELECT ?mun (SAMPLE(?loc) AS ?loc)
        WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                     wdt:P131 var_area:;
                     wdt:P625 ?loc. }
        GROUP BY ?mun
      }
      OPTIONAL {
        { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
          WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                       wdt:P131 var_area:;
                       wdt:P625 ?loc. }
          GROUP BY ?mun
        }
      }
      #FILTER (STR(?mun) <= STR(?mun2)).
      BIND(geof:distance(?loc, ?loc2) AS ?distNum).
      BIND(COALESCE(
        IF(?distNum >= 40, "40 km und mehr", 1/0),
        IF(?distNum >= 30, "30 - 40 km", 1/0),
        IF(?distNum >= 20, "20 - 30 km", 1/0),
        IF(?distNum >= 10, "10 - 20 km", 1/0),
        IF(?distNum >= 5, "05 - 10 km", 1/0),
        IF(?distNum >= 1, "01 - 05 km", "00 - 01 km")) AS ?distGrp).

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?animation ?from ?kilometers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_animated_by_fixed_dist_range_groups
  title: grouped per municipality on x-axis, animated by fixed dist range groups
  description: '='
- name: grouped per municipality on x-axis, animated by ranked dist (farthest, 2nd
    farthest, ..)
  namespace: examples
  domain: wikidata.org
  query_id: grouped-per-municipality-on-x-axis-animated-by-ranked-dist-farthest-2nd-farthest--examples@wikidata.org
  sparql: |2

    #defaultView:ScatterChart
    PREFIX var_muntype: <http://www.wikidata.org/entity/Q15284>
    PREFIX var_area: <http://www.wikidata.org/entity/Q6308>
    SELECT ?from ?kilometers ?to ?rank_group
    WHERE {
      {
        SELECT (SAMPLE(?mun) AS ?mun) (SAMPLE(?mun2) AS ?mun2) (SAMPLE(?distNum) AS ?kilometers)
               (COUNT(*)-1 AS ?rg) (SUBSTR("00",1+STRLEN(STR(?rg))) AS ?rgpad)
               (IF(FLOOR((?rg-(100*FLOOR(?rg/100)))/10)=1,0,?rg-(10*FLOOR(?rg/10))) AS ?rgmod)
               (IF(?rgmod=1,"st",IF(?rgmod=2,"nd",IF(?rgmod=3,"rd","th"))) AS ?rgord)
               (CONCAT(?rgpad,STR(?rg),?rgord,"-most farthest places") AS ?rank_group)
        WHERE {
          { SELECT ?mun (SAMPLE(?loc) AS ?loc)
            WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                         wdt:P131 var_area:;
                         wdt:P625 ?loc. }
            GROUP BY ?mun
          }
          OPTIONAL {
            { SELECT (?mun AS ?mun2) (SAMPLE(?loc) AS ?loc2)
              WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                           wdt:P131 var_area:;
                           wdt:P625 ?loc. }
              GROUP BY ?mun
            }
          }
          OPTIONAL {
            { SELECT (?mun AS ?mun3) (SAMPLE(?loc) AS ?loc3)
              WHERE { ?mun wdt:P31/wdt:P279* var_muntype:;
                           wdt:P131 var_area:;
                           wdt:P625 ?loc. }
              GROUP BY ?mun
            }
          }
          BIND(geof:distance(?loc, ?loc2) AS ?distNum).
          BIND(geof:distance(?loc, ?loc3) AS ?d).
          FILTER(?distNum >= ?d).
        } GROUP BY ?mun ?mun2 ?distNum
      }

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?mun rdfs:label ?from.
        ?mun2 rdfs:label ?to.
      }
    }
    ORDER BY ?rank_group ?kilometers ?from
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#grouped_per_municipality_on_x-axis,_animated_by_ranked_dist_(farthest,_2nd_farthest,_..)
  title: grouped per municipality on x-axis, animated by ranked dist (farthest, 2nd
    farthest, ..)
  description: '='
- name: Show all Dutch municipalities that share a border with {{Q|Q213246}}, ignoring
    rank
  namespace: examples
  domain: wikidata.org
  query_id: show-all-dutch-municipalities-that-share-a-border-with-q-q213246-ignoring-rank--examples@wikidata.org
  sparql: |2

    SELECT ?muni ?muniLabel where {
      ?muni p:P31 [ps:P31 wd:Q2039348];
            wdt:P47 wd:Q213246.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Show_all_Dutch_municipalities_that_share_a_border_with_{{Q|Q213246}},_ignoring_rank
  title: Show all Dutch municipalities that share a border with {{Q|Q213246}}, ignoring
    rank
  description: ''
- name: Border cities of the world
  namespace: examples
  domain: wikidata.org
  query_id: border-cities-of-the-world--examples@wikidata.org
  sparql: |2-

    # ----
    # To filter by country add FILTER (?country = wd:Qxx)
    #defaultView:Map
    # To show the cities of several countries use FILTER (?country IN (wd:Qxx, wd:Qyy))
    # To obtain cities that are part of a particular continent or territory ?country
    # should not be optional and add "?country wdt:P361 wd:Qxx ."

    SELECT ?border_city ?border_cityLabel ?countryLabel ?coords
    WHERE {
      ?border_city wdt:P31 wd:Q902814.
      OPTIONAL { ?border_city wdt:P17 ?country. }
      OPTIONAL { ?border_city wdt:P625 ?coords. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
                             ?border_city rdfs:label ?border_cityLabel.
                             ?country rdfs:label ?countryLabel.}
    }
    GROUP BY ?border_city ?border_cityLabel ?countryLabel ?coords
    ORDER BY ?countryLabel ?border_cityLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Border_cities_of_the_world
  title: Border cities of the world
  description: ''
- name: Municipalities of the Basque Country without former municipalities
  namespace: examples
  domain: wikidata.org
  query_id: municipalities-of-the-basque-country-without-former-municipalities--examples@wikidata.org
  sparql: "\n# ----\n# This query has two different features:\n# First, it is able\
    \ to search for municipalities which are part of two different states, looking\
    \ to P527\n# Second, it doesn't show former municipalities, filtering the old\
    \ ones from the P582 statement.\nSELECT DISTINCT ?udalerri ?udalerriLabel WHERE\
    \ { \n  ?udalerri p:P31 ?status.\n  ?udalerri wdt:P131*/^wdt:P527 wd:Q47588 .\n\
    \  {?status ps:P31 wd:Q484170.} UNION {?status ps:P31 wd:Q2074737}\n  MINUS {?status\
    \ pq:P582 ?old .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language\
    \ \"eu,es,fr,en\". }\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Municipalities_of_the_Basque_Country_without_former_municipalities
  title: Municipalities of the Basque Country without former municipalities
  description: ''
- name: Longest rivers
  namespace: examples
  domain: wikidata.org
  query_id: longest-rivers--examples@wikidata.org
  sparql: |2


    #defaultView:BubbleChart
    SELECT ?item ?itemLabel ?length ?pic ?location
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q4022 .
      ?item wdt:P2043 ?length .
      ?item wdt:P18 ?pic .
      ?item wdt:P625 ?location
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    } ORDER BY DESC(?length) ?itemLabel
    LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Longest_rivers
  title: Longest rivers
  description: ''
- name: Longest river of each continent
  namespace: examples
  domain: wikidata.org
  query_id: longest-river-of-each-continent--examples@wikidata.org
  sparql: |2


    SELECT ?continent ?river ?continentLabel ?riverLabel ?maxlength
    WHERE
    {
      {
        SELECT ?continent (MAX(?length) AS ?maxlength)
        WHERE
        {
          ?river wdt:P31/wdt:P279* wd:Q355304;
                 wdt:P2043 ?length;
                 wdt:P30 ?continent.
        }
        GROUP BY ?continent
      }
      ?river wdt:P31/wdt:P279* wd:Q355304;
             wdt:P2043 ?maxlength;
             wdt:P30 ?continent.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ?continentLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Longest_river_of_each_continent
  title: Longest river of each continent
  description: ''
- name: Rivers in Antarctica
  namespace: examples
  domain: wikidata.org
  query_id: rivers-in-antarctica--examples@wikidata.org
  sparql: |2


    SELECT ?river ?riverLabel ?location
    WHERE
    {
      ?river wdt:P31/wdt:P279* wd:Q355304; # watercourse; includes a few creeks – use wd:Q4022 for rivers only
             wdt:P30 wd:Q51.
      OPTIONAL { ?river wdt:P625 ?location. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Rivers_in_Antarctica
  title: Rivers in Antarctica
  description: ''
- name: Body of water with the most watercourses ending in it
  namespace: examples
  domain: wikidata.org
  query_id: body-of-water-with-the-most-watercourses-ending-in-it--examples@wikidata.org
  sparql: |2

    # --
    # Bodies of water that most watercourses end in (transitive).
    # For example, over 4000 watercourses eventually flow into the Ob,
    # and since the Ob flows into the Kara Sea, the Kara Sea has over 4000 results as well.
    SELECT ?mouth (COUNT(?watercourse) AS ?count)
    WHERE
    {
      ?watercourse wdt:P403+ ?mouth.
    }
    GROUP BY ?mouth
    ORDER BY DESC(?count)
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Body_of_water_with_the_most_watercourses_ending_in_it
  title: Body of water with the most watercourses ending in it
  description: |-
    * Items used: None
    * Properties used:
- name: Bridges over rivers in former government district of Leipzig
  namespace: examples
  domain: wikidata.org
  query_id: bridges-over-rivers-in-former-government-district-of-leipzig--examples@wikidata.org
  sparql: |2

    #defaultView:Map{"layer":"?wcourseLabel"}
    SELECT ?br ?brLabel ?wcourse ?wcourseLabel
           (SAMPLE(?coord) AS ?loc) (SAMPLE(?img) AS ?pic)
           (CONCAT(SAMPLE(?sKey),": ",STR(YEAR(SAMPLE(?s)))) AS ?start)
           (CONCAT(SAMPLE(?eKey),": ",STR(YEAR(SAMPLE(?e)))) AS ?end)
           ?a (IF(BOUND(?a),CONCAT(?brLabel," in Wikipedia"),1/0) AS ?aLabel)
    WHERE {
      # current ?mun within former wd:Q24186
      { VALUES ?lk { wd:Q6301 wd:Q6308 wd:Q20233 }. ?lk wdt:P150 ?mun. } UNION {
        VALUES ?mun { wd:Q2079 wd:Q113503289 }. }
      ?br wdt:P31/wdt:P279* wd:Q12280; wdt:P177 ?wcourse; wdt:P131 ?mun.
      ?wcourse wdt:P31/wdt:P279* wd:Q355304.

      OPTIONAL { ?br wdt:P625 ?coord. }.
      OPTIONAL { ?br wdt:P18 ?img. }.
      OPTIONAL { ?br wdt:P571 ?s. }.
      OPTIONAL { ?br wdt:P576 ?e. }.
      OPTIONAL { ?a schema:about ?br. FILTER(
        IF(EXISTS {?a schema:inLanguage "[AUTO_LANGUAGE]".},
           SUBSTR(STR(?a), 1, 11) = SUBSTR("https://[AUTO_LANGUAGE].", 1, 11),
           IF(EXISTS {?a schema:inLanguage "en".},
              SUBSTR(STR(?a), 1, 10) = "https://en",
              SUBSTR(STR(?a), 1, 10) = "https://de"
           )
        )
      ).}

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,de".
        ?br rdfs:label ?brLabel.
        ?wcourse rdfs:label ?wcourseLabel.
        wd:P571 rdfs:label ?sKey.
        wd:P576 rdfs:label ?eKey.
      }
    }
    GROUP BY ?br ?brLabel ?wcourse ?wcourseLabel ?a
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Bridges_over_rivers_in_former_government_district_of_Leipzig
  title: Bridges over rivers in former government district of Leipzig
  description: ''
- name: Mons (mountains), with coordinates, not located on Earth
  namespace: examples
  domain: wikidata.org
  query_id: mons-mountains-with-coordinates-not-located-on-earth--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?item ?name ?coord ?lat ?lon ?globe
    {
       ?item wdt:P31 wd:Q429088 ;
             p:P625 [
               psv:P625 [
                 wikibase:geoLatitude ?lat ;
                 wikibase:geoLongitude ?lon ;
                 wikibase:geoGlobe ?globe ;
               ] ;
               ps:P625 ?coord
             ]
      FILTER ( ?globe != wd:Q2 )
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
        ?item rdfs:label ?name
       }
    }
    ORDER BY ASC (?name)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mons_(mountains),_with_coordinates,_not_located_on_Earth
  title: Mons (mountains), with coordinates, not located on Earth
  description: "* Items used:  \n* Properties used:"
- name: Highest places on Earth
  namespace: examples
  domain: wikidata.org
  query_id: highest-places-on-earth--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:774--> Elevations over 8000 meter</translate>
    SELECT ?elevation ?item ?itemLabel ?itemDescription ?coord WHERE {
      hint:Query hint:optimizer "None".
      ?st psn:P2044 ?psn .
      ?psn wikibase:quantityAmount ?elevation . FILTER(?elevation > 8000) .
      ?psn wikibase:quantityUnit wd:Q11573 .
      ?st wikibase:rank ?rank . FILTER(?rank != wikibase:DeprecatedRank) .
      ?item p:P2044 ?st .
      ?item wdt:P625 ?coord .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY DESC(?elevation)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Highest_places_on_Earth
  title: Highest places on Earth
  description: |-
    *  Items used: None
    *  Properties used:
- name: Highest mountains in the universe (with units)
  namespace: examples
  domain: wikidata.org
  query_id: highest-mountains-in-the-universe-with-units--examples@wikidata.org
  sparql: |2

    # ----
    #Elevations over 8000
    #Displays units
    #Last updated 2021-04-25

    SELECT ?elevation ?unit ?unitLabel ?item ?itemLabel ?itemDescription ?coord
    WHERE
    {
      ?psv_triples wikibase:quantityAmount ?elevation .
      filter(?elevation > 8000)
      ?psv_triples wikibase:quantityUnit ?unit .

      ?p_triples psv:P2044 ?psv_triples .
      ?p_triples a wikibase:BestRank .

      ?item p:P2044 ?p_triples .

      ?item wdt:P625 ?coord .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY DESC(?elevation)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Highest_mountains_in_the_universe_(with_units)
  title: Highest mountains in the universe (with units)
  description: |-
    *  Items used: None
    *  Properties used:
- name: Highest mountains in the universe (with units, compact form)
  namespace: examples
  domain: wikidata.org
  query_id: highest-mountains-in-the-universe-with-units-compact-form--examples@wikidata.org
  sparql: |2


    #Elevations over 8000
    #Displays units
    #Last updated 2021-04-25

    SELECT ?elevation ?unit ?unitLabel ?item ?itemLabel ?itemDescription ?coord
    WHERE
    {
      ?item wdt:P2044 ?elevation .
      # this could be dropped, but is included to speed it up
      filter(?elevation > 8000)

      ?item p:P2044 [ a wikibase:BestRank ; psv:P2044 [ wikibase:quantityAmount ?elevation ; wikibase:quantityUnit ?unit ] ] .

      ?item wdt:P625 ?coord .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY DESC(?elevation)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Highest_mountains_in_the_universe_(with_units,_compact_form)
  title: Highest mountains in the universe (with units, compact form)
  description: |-
    *  Items used: None
    *  Properties used:
- name: Mountains over 8000 meters elevation
  namespace: examples
  domain: wikidata.org
  query_id: mountains-over-8000-meters-elevation--examples@wikidata.org
  sparql: |2

    #Last updated 2021-04-25
    #defaultView:Map
    SELECT ?item ?itemLabel ?itemDescription ?coord ?elevation
    WHERE
    {
      ?item wdt:P31 wd:Q8502 .
      ?item p:P2044 [ a wikibase:BestRank; psn:P2044/wikibase:quantityAmount ?elevation ]
      FILTER(?elevation >= 8000 )
      ?item wdt:P625 ?coord.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mountains_over_8000_meters_elevation
  title: Mountains over 8000 meters elevation
  description: '* Properties used:'
- name: Italian mountains higher than 4000 meters
  namespace: examples
  domain: wikidata.org
  query_id: italian-mountains-higher-than-4000-meters--examples@wikidata.org
  sparql: |
    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?coord ?elev ?picture
    {
      ?item p:P2044/psn:P2044/wikibase:quantityAmount ?elev ; # normalized height
            wdt:P625 ?coord ;
            wdt:P17 wd:Q38 ;
            wdt:P18 ?picture
      FILTER(?elev > 4000)

      SERVICE wikibase:label { bd:serviceParam wikibase:language "it" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Italian_mountains_higher_than_4000_meters
  title: Italian mountains higher than 4000 meters
  description: "* Items used: \n* Properties used:"
- name: Places in Antarctica more than 3000km away from the South Pole
  namespace: examples
  domain: wikidata.org
  query_id: places-in-antarctica-more-than-3000km-away-from-the-south-pole--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:775--> Places in Antarctica over 3000km away from the South Pole</translate>
    #defaultView:Map
    SELECT ?place ?placeLabel ?location WHERE {
      wd:Q933 wdt:P625 ?southPole.                         # coordinates of south pole
      ?place wdt:P30 wd:Q51;                               # continent: antarctica
             wdt:P625 ?location.
      FILTER(geof:distance(?location, ?southPole) > 3000). # over 3000km away from south pole
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Places_in_Antarctica_more_than_3000km_away_from_the_South_Pole
  title: Places in Antarctica more than 3000km away from the South Pole
  description: ''
- name: Metro stations of {{Q|Q13224}} in Paris
  namespace: examples
  domain: wikidata.org
  query_id: metro-stations-of-q-q13224-in-paris--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?adjacent ?adjacentL ?coords
    {
      ?item wdt:P31/wdt:P279* wd:Q928830 ;
            wdt:P81 wd:Q13224 ;
            wdt:P625 ?coords .
      OPTIONAL {
        ?item p:P197 [ ps:P197 ?adjacent ; pq:P5051 wd:Q585752;
                                           pq:P81 wd:Q13224] .
        ?adjacent rdfs:label ?adjacentL FILTER (lang(?adjacentL) = "en")
      }
    ?item wdt:P1619 ?founded_date. #needs existence
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    } ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Metro_stations_of_{{Q|Q13224}}_in_Paris
  title: Metro stations of {{Q|Q13224}} in Paris
  description: "* Items used:   \n* Properties used:"
- name: Map of Broadway venues
  namespace: examples
  domain: wikidata.org
  query_id: map-of-broadway-venues--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    # Venues in Broadway
    SELECT DISTINCT ?venue ?venueLabel ?coords {
        ?venue wdt:P1217 ?IDBDB .
        wd:Q235065 wdt:P625 ?broadWayLoc .

        SERVICE wikibase:around {
          ?venue wdt:P625 ?coords .
          bd:serviceParam wikibase:center ?broadWayLoc .
          bd:serviceParam wikibase:radius "1.5" .
        }

      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_Broadway_venues
  title: Map of Broadway venues
  description: "* Items used: \n* Properties used:"
- name: How many states this US state borders
  namespace: examples
  domain: wikidata.org
  query_id: how-many-states-this-us-state-borders--examples@wikidata.org
  sparql: |2

    SELECT ?state ?stateLabel ?borders
    WHERE
    {
      {
        SELECT ?state (COUNT(?otherState) as ?borders)
        WHERE
        {
        ?state wdt:P31 wd:Q35657 .
        ?otherState wdt:P47 ?state .
        ?otherState wdt:P31 wd:Q35657 .
        }
        GROUP BY ?state
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
      }
    }
    ORDER BY DESC(?borders)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#How_many_states_this_US_state_borders
  title: How many states this US state borders
  description: "* Items used: \n* Properties used: ,"
- name: Places that are below 10 meters above sea level
  namespace: examples
  domain: wikidata.org
  query_id: places-that-are-below-10-meters-above-sea-level--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?place ?location ?elev ?image
    WHERE
    {
      ?place p:P2044/psv:P2044 ?placeElev.
      ?placeElev wikibase:quantityAmount ?elev.
      ?placeElev wikibase:quantityUnit ?unit.
      bind(0.01 as ?km).
      filter( (?elev < ?km*1000 && ?unit = wd:Q11573)
           {{!}}{{!}} (?elev < ?km*3281 && ?unit = wd:Q3710)
           {{!}}{{!}} (?elev < ?km      && ?unit = wd:Q828224) ).
      ?place wdt:P625 ?location.
      OPTIONAL { ?place wdt:P18 ?image }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Places_that_are_below_10_meters_above_sea_level
  title: Places that are below 10 meters above sea level
  description: "* Items used: \n* Properties used:"
- name: Globes used to represent coordinates
  namespace: examples
  domain: wikidata.org
  query_id: globes-used-to-represent-coordinates--examples@wikidata.org
  sparql: |2

     SELECT (count(?v) as ?c) ?globe
    WHERE
    {
      ?v wikibase:geoGlobe ?globe
    }
    GROUP BY ?globe
    ORDER BY DESC(?c)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Globes_used_to_represent_coordinates
  title: Globes used to represent coordinates
  description: ''
- name: Places within 1km of the Empire State Building
  namespace: examples
  domain: wikidata.org
  query_id: places-within-1km-of-the-empire-state-building--examples@wikidata.org
  sparql: |2

     SELECT ?place ?placeLabel ?location ?instanceLabel
    WHERE
    {
      wd:Q9188 wdt:P625 ?loc .
      SERVICE wikibase:around {
          ?place wdt:P625 ?location .
          bd:serviceParam wikibase:center ?loc .
          bd:serviceParam wikibase:radius "1" .
      }
      OPTIONAL { ?place wdt:P31 ?instance }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
      BIND(geof:distance(?loc, ?location) as ?dist)
    } ORDER BY ?dist
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Places_within_1km_of_the_Empire_State_Building
  title: Places within 1km of the Empire State Building
  description: "* Items used: \n* Properties used:"
- name: Select French municipalities by INSEE code (select by identifier)
  namespace: examples
  domain: wikidata.org
  query_id: select-french-municipalities-by-insee-code-select-by-identifier--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?inseeCode {
      ?item wdt:P374 ?inseeCode .
      FILTER (?inseeCode in ("75056", "69123", "13055"))
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Select_French_municipalities_by_INSEE_code_(select_by_identifier)
  title: Select French municipalities by INSEE code (select by identifier)
  description: '* Properties used:'
- name: Map and list of municipalities in The Netherlands
  namespace: examples
  domain: wikidata.org
  query_id: map-and-list-of-municipalities-in-the-netherlands--examples@wikidata.org
  sparql: |2

    # ----
    #Concise list & map of the 355 Dutch municipalities, their geo coordinates and their provinces, per 1-1-2019
    #See also:
    #https://almanak.overheid.nl/organisaties/Gemeenten/ - 355 in aantal
    #https://nl.wikipedia.org/wiki/Lijst_van_Nederlandse_gemeenten - 355 in aantal

    #defaultView:Map
    SELECT ?muni ?muniLabel ?location where {
      ?muni p:P31 ?instanceOf; # Get statement because we need this later
            wdt:P625 ?location. # And location
      ?instanceOf ps:P31 wd:Q2039348. # P31 should be 'municipality of the Netherlands'
      MINUS { ?muni wdt:P31 wd:Q7265977. } # Don't show former municipalities
      MINUS { ?instanceOf pq:P582 ?endTime. } # And don't show municipalities that have an end time
      SERVICE wikibase:label { bd:serviceParam wikibase:language "nl". } # Show names in Dutch
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_and_list_of_municipalities_in_The_Netherlands
  title: Map and list of municipalities in The Netherlands
  description: ''
- name: Emergency numbers by population using them
  namespace: examples
  domain: wikidata.org
  query_id: emergency-numbers-by-population-using-them--examples@wikidata.org
  sparql: |2

    # ----
    #Lists emergency numbers and the total amount of people which can use them
    #defaultView:BubbleChart
    SELECT ?emergencyNumber ?emergencyNumberLabel ?tel ?population ?countries
    WHERE
    {
     ?emergencyNumber wdt:P31 wd:Q694554 .
      OPTIONAL{SELECT ?emergencyNumber (COUNT(?state) as ?countries) (SUM(?pop) as ?population) WHERE {
        ?state wdt:P2852 ?emergencyNumber .
        OPTIONAL{?state wdt:P1082 ?pop} .
        ?state wdt:P31/wdt:P279* wd:Q6256
      } GROUP BY ?emergencyNumber } .
      OPTIONAL{?emergencyNumber wdt:P1329 ?tel }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY DESC(?population)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Emergency_numbers_by_population_using_them
  title: Emergency numbers by population using them
  description: "* Items used :  \n* Properties used:"
- name: German states, ordered by the number of company headquarters per million inhabitants
  namespace: examples
  domain: wikidata.org
  query_id: german-states-ordered-by-the-number-of-company-headquarters-per-million-inhabitants--examples@wikidata.org
  sparql: |2

    # ---
    # German states, ordered by the number of company headquarters per million inhabitants
    # Replace wd:Q1221156 (state of Germany) by anything else you care about, e.g., wd:Q6256 (country)
    # Nested queries for correctness (don't count companies twice) and performance (aggregate before adding more data)
    # Limits: states without population missing; company data in Wikidata is always incomplete
    # Note: This query shows some not-so-easy nesting of DISTINCT (don't count any company twice) and aggregate, in combination with arithmetic output evaluation functions. It is a hard query that may time out if modified.
    SELECT
      ?state ?stateLabel
      ?companies
      ?population
      (?companies/?population*1000000 AS ?companiesPerM)
    WHERE
    {
      { SELECT ?state (count(*) as ?companies) WHERE {
        {SELECT DISTINCT ?company ?state WHERE {
          ?state wdt:P31 wd:Q1221156 .
          ?company wdt:P31/wdt:P279* wd:Q4830453 .
          ?company wdt:P159/wdt:P131* ?state .
          FILTER NOT EXISTS{ ?company wdt:P576 ?date } # don't count dissolved companies
        } }
      } GROUP BY ?state }
        ?state wdt:P1082 ?population
      SERVICE wikibase:label { bd:serviceParam wikibase:language "de" }
    }
    ORDER BY DESC(?companiesPerM)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#German_states,_ordered_by_the_number_of_company_headquarters_per_million_inhabitants
  title: German states, ordered by the number of company headquarters per million
    inhabitants
  description: "* Items used:  \n* Properties used:"
- name: Popular names per birthplace
  namespace: examples
  domain: wikidata.org
  query_id: popular-names-per-birthplace--examples@wikidata.org
  sparql: |2


    #defaultView:BubbleChart
    SELECT ?cid ?firstname (COUNT(*) AS ?count)
    WHERE
    {
      ?pid wdt:P19 wd:Q64.
      ?pid wdt:P735 ?cid.
      OPTIONAL {
        ?cid rdfs:label ?firstname
        FILTER((LANG(?firstname)) = "en")
      }
    }
    GROUP BY ?cid ?firstname
    ORDER BY DESC(?count) ?firstname
    LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Popular_names_per_birthplace
  title: Popular names per birthplace
  description: "*  Items used: \n*  Properties used:"
- name: Map of places mentioned in travel stories with text in French accessible online
  namespace: examples
  domain: wikidata.org
  query_id: map-of-places-mentioned-in-travel-stories-with-text-in-french-accessible-online--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT DISTINCT ?item ?itemLabel ?ed ?edLabel ?auteurLabel (year(?dateCreation) as ?AnneeCreation) ?lieuLabel ?coord ?lien
    WHERE
    {
      ?item wdt:P136 wd:Q1164267 .    # genre :récit de voyage
      ?item wdt:P31 wd:Q571 .         # nature : livre
      OPTIONAL{
        ?item wdt:P50 ?auteur .       # [option] auteur
      }
      OPTIONAL{
        ?item wdt:P571 ?dateCreation. # [option] date de création
      }

      ?item wdt:P840 ?lieu .          # lieu de l'action
      MINUS {?item wdt:P840 wd:Q933}  # (bug du Pôle Sud)
      ?lieu wdt:P625 ?coord .         # coordonnées géographiques du lieu

      {
        ?item wdt:P953 ?lien .        # URL, texte intégral disponible sur
        ?item wdt:P407 wd:Q150        # langue de l'œuvre : français
      }UNION{
        ?ed wdt:P629 ?item .          # édition du livre
        ?ed wdt:P953 ?lien .          # URL, texte intégral disponible sur
        ?ed wdt:P407 wd:Q150          # langue de l'œuvre : français
      }

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr". }
    } ORDER BY ?dateCreation
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_places_mentioned_in_travel_stories_with_text_in_French_accessible_online
  title: Map of places mentioned in travel stories with text in French accessible
    online
  description: "* Items used:    \n* Properties used:"
- name: Things located where the equator meets the prime meridian
  namespace: examples
  domain: wikidata.org
  query_id: things-located-where-the-equator-meets-the-prime-meridian--examples@wikidata.org
  sparql: |2


    SELECT ?place ?placeLabel ?location
    WHERE
    {
      wd:Q24041662 wdt:P625 ?loc00 .
      SERVICE wikibase:around {
        ?place wdt:P625 ?location .
        bd:serviceParam wikibase:center ?loc00 .
        bd:serviceParam wikibase:radius "0.001" .
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    }
    ORDER BY ?placeLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Things_located_where_the_equator_meets_the_prime_meridian
  title: Things located where the equator meets the prime meridian
  description: "* Items used: \n* Properties used:"
- name: French communes with names ending in ac
  namespace: examples
  domain: wikidata.org
  query_id: french-communes-with-names-ending-in-ac--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?item ?itemLabel ?coord
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q484170;
            wdt:P17 wd:Q142;
            rdfs:label ?itemLabel;
            wdt:P625 ?coord;
      FILTER (lang(?itemLabel) = "fr").
      FILTER regex (?itemLabel, "ac$").
      FILTER not exists { ?item wdt:P131 wd:Q33788 } # excluding Koumac, New Caledonia...
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#French_communes_with_names_ending_in_ac
  title: French communes with names ending in ac
  description: ''
- name: Buildings in more than one country
  namespace: examples
  domain: wikidata.org
  query_id: buildings-in-more-than-one-country--examples@wikidata.org
  sparql: |2

    SELECT ?item ?itemLabel ?count
    WHERE
    {
      {
        SELECT ?item (COUNT(DISTINCT ?country) AS ?count) WHERE {
          ?item wdt:P31/wdt:P279* wd:Q41176 .
          ?item wdt:P17 ?country .
          FILTER NOT EXISTS { ?country wdt:P576 ?end }
        }
        GROUP BY ?item
      }
      FILTER ( ?count > 1 )
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    }
    ORDER BY DESC(?count) ?itemL
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Buildings_in_more_than_one_country
  title: Buildings in more than one country
  description: ''
- name: Streets named after a person
  namespace: examples
  domain: wikidata.org
  query_id: streets-named-after-a-person--examples@wikidata.org
  sparql: |2

    SELECT ?street ?streetLabel ?cityLabel ?personLabel
    WHERE
    {
        ?street wdt:P31 wd:Q79007 .
        ?street wdt:P17 wd:Q142 .
        ?street wdt:P131 ?city .
        ?street wdt:P138 ?person .
        ?person wdt:P31 wd:Q5
        SERVICE wikibase:label { bd:serviceParam wikibase:language "fr" }
    }
    ORDER BY ?city
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Streets_named_after_a_person
  title: Streets named after a person
  description: ''
- name: Streets in France without a city
  namespace: examples
  domain: wikidata.org
  query_id: streets-in-france-without-a-city--examples@wikidata.org
  sparql: |2

    SELECT ?street ?streetLabel
    WHERE
    {
        ?street wdt:P31/wdt:P279* wd:Q79007 .
        ?street wdt:P17 wd:Q142 .
        MINUS { ?street wdt:P131 [] } .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr" }
    }
    ORDER BY ?streetLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Streets_in_France_without_a_city
  title: Streets in France without a city
  description: ''
- name: ATMs around Munich belonging to the Bankcard-Servicenetz interbank network
    (federated query)
  namespace: examples
  domain: wikidata.org
  query_id: atms-around-munich-belonging-to-the-bankcard-servicenetz-interbank-network-federated-query--examples@wikidata.org
  sparql: |2

    # ----
    #defaultView:Map{"hide":["?atm","?geometry"], "layer": "?bankLabel"}

    PREFIX lgdo: <http://linkedgeodata.org/ontology/>
    PREFIX geom: <http://geovocab.org/geometry#>
    PREFIX bif: <bif:>

    SELECT ?atm ?geometry ?bank ?bankLabel WHERE {
      hint:Query hint:optimizer "None".

      SERVICE <http://linkedgeodata.org/sparql> {
        { ?atm a lgdo:Bank; lgdo:atm true. }
        UNION { ?atm a lgdo:Atm. }

        ?atm geom:geometry [geo:asWKT ?geometry];
             lgdo:operator ?operator.
        FILTER(bif:st_intersects(?geometry, bif:st_point(11.5746898, 48.1479876), 5)) # 5 km around Munich
      }

      BIND(STRLANG(?operator, "de") as ?bankLabel)
      ?bank rdfs:label ?bankLabel.

      # bank is part of the Bankcard service network, either via an explicit statement or implicitly due to its legal form (unless explicitly excluded)
      { ?bank wdt:P527 wd:Q806724. }
      UNION { ?bank wdt:P1454 wd:Q5349747. }
      MINUS { wd:Q806724 wdt:P3113 ?bank. }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#ATMs_around_Munich_belonging_to_the_Bankcard-Servicenetz_interbank_network_(federated_query)
  title: ATMs around Munich belonging to the Bankcard-Servicenetz interbank network
    (federated query)
  description: ''
- name: Items geographically located around the Wikimedia Foundation office, sorted
    by distance
  namespace: examples
  domain: wikidata.org
  query_id: items-geographically-located-around-the-wikimedia-foundation-office-sorted-by-distance--examples@wikidata.org
  sparql: |2

    SELECT ?place ?location ?distance ?placeLabel WHERE {
        SERVICE wikibase:around {
          ?place wdt:P625 ?location .
          bd:serviceParam wikibase:center "Point(-122.402251 37.789246)"^^geo:wktLiteral .
          bd:serviceParam wikibase:radius "1" .
          bd:serviceParam wikibase:distance ?distance .
        }
        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY ?distance LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_geographically_located_around_the_Wikimedia_Foundation_office,_sorted_by_distance
  title: Items geographically located around the Wikimedia Foundation office, sorted
    by distance
  description: ''
- name: Ten largest islands in the world
  namespace: examples
  domain: wikidata.org
  query_id: ten-largest-islands-in-the-world--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:776--> Ten largest islands in the world</translate>
    # Inspired by this infographic: https://redd.it/a6423t

    #defaultView:ImageGrid
    SELECT DISTINCT ?island ?islandLabel ?islandImage WHERE {
      ?island (wdt:P31/(wdt:P279*)) wd:Q23442.
      OPTIONAL { ?island wdt:P18 ?islandImage. }
      ?island (p:P2046/psn:P2046/wikibase:quantityAmount) ?islandArea.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY DESC (?islandArea)
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Ten_largest_islands_in_the_world
  title: Ten largest islands in the world
  description: ''
- name: City gates in the Dutch province of Zeeland
  namespace: examples
  domain: wikidata.org
  query_id: city-gates-in-the-dutch-province-of-zeeland--examples@wikidata.org
  sparql: |2

    SELECT ?town ?townLabel ?gate ?gateLabel where {
      # Municipalities within the province
      ?muni wdt:P31 wd:Q2039348;
            wdt:P131 wd:Q705.
      # Instances of city gate located within any of the municipalities
      ?gate wdt:P31 wd:Q82117;
            wdt:P131 ?muni;
      # The Location (usually Town) that the gate is in
            wdt:P276 ?town.

      SERVICE wikibase:label { bd:serviceParam wikibase:language "nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#City_gates_in_the_Dutch_province_of_Zeeland
  title: City gates in the Dutch province of Zeeland
  description: ''
- name: Glaciers map
  namespace: examples
  domain: wikidata.org
  query_id: glaciers-map--examples@wikidata.org
  sparql: |
    <!--using initially SPARQL5 but pb with untranslated TryIt-->
    # ----
    #by Jura1, 2021-05-21
    #defaultView:Map{"hide":["?coor","?area_scale_km"], "layer": "?area_scale_km"}
    SELECT ?item ?itemLabel ?itemDescription ?coor ?area_sqkm ?area_scale_km
    {
      ?item wdt:P31/wdt:P279* wd:Q35666 .
      OPTIONAL { ?item wdt:P625 ?coor }
      OPTIONAL { ?item p:P2046 [ a wikibase:BestRank; psn:P2046/wikibase:quantityAmount ?area_sqm ] .
        BIND( ROUND(?area_sqm/10000)/100 as ?area_sqkm)
        BIND( strlen(str(ROUND(?area_sqm/1000000))) as ?area_scale_km)
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Glaciers_map
  title: Glaciers map
  description: ''
- name: Birthplaces of humans named Antoine
  namespace: examples
  domain: wikidata.org
  query_id: birthplaces-of-humans-named-antoine--examples@wikidata.org
  sparql: |2

    # ----
    # Coordinates of the birth places of people named Antoine
    #defaultView:Map
    SELECT ?item ?itemLabel ?coord
    WHERE
    {
      ?item wdt:P31 wd:Q5 . # human
      ?item wdt:P735 wd:Q15235674.
      ?item wdt:P19 ?place.
      ?place wdt:P625 ?coord.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Birthplaces_of_humans_named_Antoine
  title: Birthplaces of humans named Antoine
  description: ''
- name: Average lifespan by occupation
  namespace: examples
  domain: wikidata.org
  query_id: average-lifespan-by-occupation--examples@wikidata.org
  sparql: |2

    # ----
    # Select the desired columns and get labels
    SELECT ?occ ?occLabel ?avgAge ?avgBirthYear ?count
    WHERE
    {
      {
        # Group the people by their occupation and calculate age
        SELECT
          ?occ
            (count(?p) as ?count)
            (round(avg(?birthYear)) as ?avgBirthYear)
            (avg(?deathYear - ?birthYear) as ?avgAge)
        WHERE {
          {
            # Get people with occupation + birth/death dates; combine multiple birth/death dates using avg
            SELECT
              ?p
                ?occ
                (avg(year(?birth)) as ?birthYear)
                (avg(year(?death)) as ?deathYear)
            WHERE {
               ?p wdt:P31 wd:Q5 ;
                  wdt:P106 ?occ ;
                  p:P569/psv:P569 [
                    wikibase:timePrecision "9"^^xsd:integer ; # precision of at least year
                    wikibase:timeValue ?birth ;
                  ] ;
                  p:P570/psv:P570 [
                    wikibase:timePrecision "9"^^xsd:integer ; # precision of at least year
                    wikibase:timeValue ?death ;
                  ] .
            }
            GROUP BY ?p ?occ
          }
        }
        GROUP BY ?occ
      }

      FILTER (?count > 300) # arbitrary number to weed out values with 'few' observations
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    }
    ORDER BY ASC(?avgAge)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Average_lifespan_by_occupation
  title: Average lifespan by occupation
  description: ''
- name: Items with the most birth dates
  namespace: examples
  domain: wikidata.org
  query_id: items-with-the-most-birth-dates--examples@wikidata.org
  sparql: |
    SELECT ?person (COUNT(?date) AS ?dateC) {
      ?person wdt:P569 ?date
    }
    GROUP BY ?person
    HAVING (?dateC > 2)
    ORDER BY DESC (?dateC)
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Items_with_the_most_birth_dates
  title: Items with the most birth dates
  description: ''
- name: Things/people with most children
  namespace: examples
  domain: wikidata.org
  query_id: things-people-with-most-children--examples@wikidata.org
  sparql: |2

    SELECT ?parent ?parentLabel ?count
    WHERE
    {
      {
        SELECT ?parent (COUNT(?child) AS ?count)
        WHERE
        {
          ?parent wdt:P40 ?child.
        }
        GROUP BY ?parent
        ORDER BY DESC(?count)
        LIMIT 10
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC(?count)
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Things/people_with_most_children
  title: Things/people with most children
  description: ''
- name: Population growth in Suriname from 1960 onward
  namespace: examples
  domain: wikidata.org
  query_id: population-growth-in-suriname-from-1960-onward--examples@wikidata.org
  sparql: |
    #defaultView:LineChart
    SELECT ?year ?population {
      wd:Q730 p:P1082 ?p .
      ?p pq:P585 ?year ;
         ps:P1082 ?population .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY ?year
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Population_growth_in_Suriname_from_1960_onward
  title: Population growth in Suriname from 1960 onward
  description: ''
- name: Number of dead people by month since 2000
  namespace: examples
  domain: wikidata.org
  query_id: number-of-dead-people-by-month-since-2000--examples@wikidata.org
  sparql: |
    SELECT ?yearmonth (COUNT(?person) as ?count)
    WHERE
    {
      ?person wdt:P31 wd:Q5;
              p:P570/psv:P570 [
                    wikibase:timePrecision ?precision ;
                    wikibase:timeValue ?date ;
                  ] .
      BIND(CONCAT(STR(YEAR(?date)),"-",STR(MONTH(?date))) as ?yearmonth).
      FILTER( ?date >= "2000-01-01T00:00:00"^^xsd:dateTime )
      FILTER( ?precision >= "10"^^xsd:integer ) # precision of at least month
    }
    GROUP BY ?yearmonth
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_dead_people_by_month_since_2000
  title: Number of dead people by month since 2000
  description: ''
- name: People of same year of birth by occupation
  namespace: examples
  domain: wikidata.org
  query_id: people-of-same-year-of-birth-by-occupation--examples@wikidata.org
  sparql: "\n# ----\n#title: <translate><!--T:906--> People of same year of birth\
    \ by occupation</translate>\n\n#Author: Leaf_plate  2025-08-30\n#defaultView:BubbleChart\n\
    SELECT ?occupation ?occupationLabel (COUNT(?occupation) AS ?people)\n  WHERE {\n\
    \    ?item wdt:P31 wd:Q5.\n    ?item p:P569/psv:P569 ?birth_date_node.\n    ?birth_date_node\
    \ wikibase:timePrecision \"9\"^^xsd:integer.  # Time precision = year\n    ?birth_date_node\
    \ wikibase:timeValue ?birth_date.\n    FILTER (year(?birth_date) = 1111).  # Filtered\
    \ for year 1111.\n    ?item wdt:P106 ?occupation.\n    SERVICE wikibase:label\
    \ { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }  \n}\nGROUP\
    \ BY ?occupation ?occupationLabel\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_of_same_year_of_birth_by_occupation
  title: People of same year of birth by occupation
  description: ''
- name: Candidates for the Dutch general election in 2017
  namespace: examples
  domain: wikidata.org
  query_id: candidates-for-the-dutch-general-election-in-2017--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?twitter ?LinkedIN ?politieke_partij ?politieke_partijLabel ?positie_op_lijst
    WHERE {
      ?item p:P3602 ?node .
        OPTIONAL { ?item wdt:P2002 ?twitter }
        OPTIONAL { ?item wdt:P2035 ?LinkedIN }
        ?node ps:P3602 wd:Q16061881 .
        OPTIONAL { ?node pq:P1545 ?positie_op_lijst }
        OPTIONAL { ?node pq:P1268 ?politieke_partij }
        OPTIONAL { ?node pq:P2035 ?LinkedIN }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Candidates_for_the_Dutch_general_election_in_2017
  title: Candidates for the Dutch general election in 2017
  description: ''
- name: Gender distribution in the candidates for the Dutch general election 2017
  namespace: examples
  domain: wikidata.org
  query_id: gender-distribution-in-the-candidates-for-the-dutch-general-election-2017--examples@wikidata.org
  sparql: |2

    # ----
    #Kandidaten voor de Nederlandse tk verkiezingen van 2017
    #defaultView:Dimensions
    SELECT ?positie_op_lijst ?genderLabel ?politieke_partijLabel WHERE {
      ?item p:P3602 ?node.
      ?item wdt:P21 ?gender.
      ?node ps:P3602 wd:Q16061881 .
      ?node pq:P1545 ?positie_op_lijst.
      ?node pq:P1268 ?politieke_partij.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Gender_distribution_in_the_candidates_for_the_Dutch_general_election_2017
  title: Gender distribution in the candidates for the Dutch general election 2017
  description: ''
- name: Candidates for the Dutch general election 2017 living in Antwerp, Belgium
  namespace: examples
  domain: wikidata.org
  query_id: candidates-for-the-dutch-general-election-2017-living-in-antwerp-belgium--examples@wikidata.org
  sparql: |2
     SELECT ?item ?itemLabel ?twitter ?LinkedIN ?politieke_partij ?politieke_partijLabel ?positie_op_lijst WHERE {
      ?item p:P3602 ?node.
      ?item wdt:P551 wd:Q12892.
      OPTIONAL { ?item wdt:P2002 ?twitter. }
      OPTIONAL { ?item wdt:P2035 ?LinkedIN. }
      ?node ps:P3602 wd:Q16061881.
      OPTIONAL { ?node pq:P10777 ?positie_op_lijst. }
      OPTIONAL { ?node pq:P1268 ?politieke_partij. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Candidates_for_the_Dutch_general_election_2017_living_in_Antwerp,_Belgium
  title: Candidates for the Dutch general election 2017 living in Antwerp, Belgium
  description: ''
- name: Candidates for the Dutch general election 2017 living abroad
  namespace: examples
  domain: wikidata.org
  query_id: candidates-for-the-dutch-general-election-2017-living-abroad--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT ?item ?itemLabel ?coordinates WHERE {
      ?item p:P3602 ?node.
      ?item wdt:P551 ?residence .
      ?residence wdt:P17 ?country ;
                 wdt:P625 ?coordinates .
      ?node ps:P3602 wd:Q16061881.
      FILTER (?country != wd:Q55)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Candidates_for_the_Dutch_general_election_2017_living_abroad
  title: Candidates for the Dutch general election 2017 living abroad
  description: ''
- name: Occupations of candidates of the Dutch general election 2017
  namespace: examples
  domain: wikidata.org
  query_id: occupations-of-candidates-of-the-dutch-general-election-2017--examples@wikidata.org
  sparql: |2

    # ----
    #Kandidaten voor de Nederlandse TK verkiezingen van 2017
    #defaultView:Dimensions
    SELECT ?positie_op_lijst ?genderLabel ?occupationLabel ?politieke_partijLabel WHERE {
      VALUES ?politieke_partij {wd:Q747910 wd:Q275441}
      ?item p:P3602 ?node.
      ?item wdt:P21 ?gender.
      ?item wdt:P106 ?occupation.
      ?node ps:P3602 wd:Q16061881.
      ?node pq:P10777 ?positie_op_lijst.
      ?node pq:P1268 ?politieke_partij.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Occupations_of_candidates_of_the_Dutch_general_election_2017
  title: Occupations of candidates of the Dutch general election 2017
  description: ''
- name: Gender balance of members of Irish parliament
  namespace: examples
  domain: wikidata.org
  query_id: gender-balance-of-members-of-irish-parliament--examples@wikidata.org
  sparql: |2

     SELECT ?genderlabel (COUNT(?genderlabel) as ?total)
    WHERE
    {
       ?subj wdt:P39 wd:Q654291 .
       ?subj wdt:P21 ?gender .

       ?gender rdfs:label ?genderlabel FILTER (lang(?genderlabel) = "en") .
       ?subj rdfs:label ?label FILTER (lang(?label) = "en")
    }
    GROUP BY ?genderlabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Gender_balance_of_members_of_Irish_parliament
  title: Gender balance of members of Irish parliament
  description: "* Items used: \n* Properties used:"
- name: Assemblies by number of seats
  namespace: examples
  domain: wikidata.org
  query_id: assemblies-by-number-of-seats--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?item ?title ?seats ?jurisdiction (YEAR(?inception) AS ?start) (YEAR(?dissolution) AS ?end)
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q1752346 .
      OPTIONAL { ?item wdt:P1342 ?seats . }
      OPTIONAL {
        ?item wdt:P1001 ?j .
        ?j rdfs:label ?jurisdiction FILTER (lang(?jurisdiction) = "en") .
      }
      OPTIONAL { ?item wdt:P571 ?inception . }
      OPTIONAL { ?item wdt:P576 ?dissolution . }
      OPTIONAL { ?item rdfs:label ?title FILTER (lang(?title) = "en") . }
    }
    ORDER BY DESC(?seats) ?title
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Assemblies_by_number_of_seats
  title: Assemblies by number of seats
  description: "* Items used: \n* Properties used:"
- name: List of countries by age of the head of government
  namespace: examples
  domain: wikidata.org
  query_id: list-of-countries-by-age-of-the-head-of-government--examples@wikidata.org
  sparql: |
    #added by Jura1, rev. 2016-11-08
    SELECT DISTINCT ?age ?country ?countryLabel ?hgovernment ?hgovernmentLabel
    {
      ?country wdt:P31 wd:Q3624078 .
      FILTER NOT EXISTS {?country wdt:P31 wd:Q3024240}
      ?country p:P6 ?statement .
      ?statement ps:P6 ?hgovernment .
      ?country wdt:P6 ?hgovernment .
      FILTER NOT EXISTS { ?statement pq:P582 ?x }
      ?hgovernment wdt:P569 ?dob . BIND(YEAR(now())-YEAR(?dob) as ?age)
      FILTER(?age>=65)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY DESC(?age)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_countries_by_age_of_the_head_of_government
  title: List of countries by age of the head of government
  description: "* Items used:  \n* Properties used:"
- name: Number of ministers who are themselves children of a minister, per country
  namespace: examples
  domain: wikidata.org
  query_id: number-of-ministers-who-are-themselves-children-of-a-minister-per-country--examples@wikidata.org
  sparql: |-
    SELECT ?cc (COUNT(DISTINCT ?child) AS ?number) {
      ?child wdt:P31 wd:Q5 ; # Looking for real humans and not fictional ones
             wdt:P39/wdt:P279* wd:Q83307 ;
             (wdt:P22
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_ministers_who_are_themselves_children_of_a_minister,_per_country
  title: Number of ministers who are themselves children of a minister, per country
  description: "* Items used:  \n* Properties used:"
- name: Members of the French National Assembly born out of France
  namespace: examples
  domain: wikidata.org
  query_id: members-of-the-french-national-assembly-born-out-of-france--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?item ?itemLabel ?placeLabel ?countryLabel
    WHERE
    {
      ?item wdt:P39 wd:Q3044918 .
      ?item wdt:P19 ?place .
      ?place wdt:P17 ?country .
      FILTER NOT EXISTS { ?place wdt:P17 wd:Q142 . }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr" . }
    }
    ORDER BY ?countryLabel ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Members_of_the_French_National_Assembly_born_out_of_France
  title: Members of the French National Assembly born out of France
  description: "* Items used:  \n* Properties used:"
- name: List of parliament buildings with pictures by country
  namespace: examples
  domain: wikidata.org
  query_id: list-of-parliament-buildings-with-pictures-by-country--examples@wikidata.org
  sparql: |2


    #defaultView:ImageGrid
    SELECT ?building ?buildingLabel ?country ?countryLabel ?picture
    WHERE
    {
      ?building wdt:P31 wd:Q7138926 .
      ?building wdt:P18 ?picture .
      OPTIONAL { ?building wdt:P17 ?country } . #if available
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
      }
    }
    ORDER BY ?countryLabel
    LIMIT 188
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_parliament_buildings_with_pictures_by_country
  title: List of parliament buildings with pictures by country
  description: "* Items used: \n* Properties used:"
- name: Number of jurisdictions by driving side
  namespace: examples
  domain: wikidata.org
  query_id: number-of-jurisdictions-by-driving-side--examples@wikidata.org
  sparql: |2

    SELECT ?sideLabel (COUNT(?jurisdiction) AS ?count)
    WHERE
    {
      ?jurisdiction wdt:P1622 ?side.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    GROUP BY ?sideLabel
    ORDER BY ?sideLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_jurisdictions_by_driving_side
  title: Number of jurisdictions by driving side
  description: ''
- name: Timeline of mayors of Amsterdam, the Netherlands
  namespace: examples
  domain: wikidata.org
  query_id: timeline-of-mayors-of-amsterdam-the-netherlands--examples@wikidata.org
  sparql: |
    #defaultView:Timeline
    SELECT ?mayor ?mayorLabel ?start ?end where {
      ?mayor p:P39 ?position.
      ?position ps:P39 wd:Q13423495;
                pq:P580 ?start;
                pq:P582 ?end.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Timeline_of_mayors_of_Amsterdam,_the_Netherlands
  title: Timeline of mayors of Amsterdam, the Netherlands
  description: ''
- name: Current U.S. members of the Senate with district, party and date they assumed
    office
  namespace: examples
  domain: wikidata.org
  query_id: current-u-s-members-of-the-senate-with-district-party-and-date-they-assumed-office--examples@wikidata.org
  sparql: |2

    SELECT ?senator ?senatorLabel ?districtLabel ?partyLabel ?assumedOffice (sample(?image) as ?image) where {
      # Get all senators
      ?senator p:P39 ?posheld; # With position held
               p:P102 ?partystatement. # And with a certain party

      # Get the party
      ?partystatement ps:P102 ?party.
      MINUS { ?partystatement pq:P582 ?partyEnd. } # but minus the ones the senator is no longer a member of
      MINUS { ?party wdt:P361 ?partOf. } # and the 'Minnesota Democratic–Farmer–Labor Party' and such

      # Check on the position in the senate
      ?posheld ps:P39 wd:Q4416090; # Position held is in the senate
               pq:P768 ?district;
               pq:P580 ?assumedOffice. # And should have a starttime

      MINUS { ?posheld pq:P582 ?endTime. } # But not an endtime

      # Add an image
      OPTIONAL { ?senator wdt:P18 ?image. }

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } GROUP BY ?senator ?senatorLabel ?districtLabel ?partyLabel ?assumedOffice ORDER BY ?senatorLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Current_U.S._members_of_the_Senate_with_district,_party_and_date_they_assumed_office
  title: Current U.S. members of the Senate with district, party and date they assumed
    office
  description: ''
- name: Economics and Business
  namespace: examples
  domain: wikidata.org
  query_id: economics-and-business--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT DISTINCT ?person ?name ?birthplace ?birthyear ?coord ?layer WHERE {
    {?person wdt:P106 wd:Q188094} UNION {?person wdt:P101 wd:Q8134}
    ?person wdt:P570 ?dod;
       wdt:P19 ?place .
    ?place wdt:P625 ?coord
    OPTIONAL { ?person wdt:P569 ?dob }
    BIND(YEAR(?dob) as ?birthyear)
    BIND(IF( (?birthyear < 1700), "Pre-1700", IF((?birthyear < 1751), "1700-1750", IF((?birthyear < 1801), "1751-1800", IF((?birthyear < 1851), "1801-1850", IF((?birthyear < 1901), "1851-1900", IF((?birthyear < 1951), "1901-1950", "Post-1950") ) ) ) )) AS ?layer )
    ?person rdfs:label ?name FILTER (lang(?name) = "en")
    ?place rdfs:label ?birthplace FILTER (lang(?birthplace) = "en")
    } ORDER BY ?birthyear
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Economics_and_Business
  title: Economics and Business
  description: ''
- name: Map of places of birth of dead economists, colour-coded by era
  namespace: examples
  domain: wikidata.org
  query_id: map-of-places-of-birth-of-dead-economists-colour-coded-by-era--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT DISTINCT ?person ?name ?birthplace ?birthyear ?coord ?layer WHERE {
    {?person wdt:P106 wd:Q188094} UNION {?person wdt:P101 wd:Q8134}
    ?person wdt:P570 ?dod;
       wdt:P19 ?place .
    ?place wdt:P625 ?coord
    OPTIONAL { ?person wdt:P569 ?dob }
    BIND(YEAR(?dob) as ?birthyear)
    BIND(IF( (?birthyear < 1700), "Pre-1700", IF((?birthyear < 1751), "1700-1750", IF((?birthyear < 1801), "1751-1800", IF((?birthyear < 1851), "1801-1850", IF((?birthyear < 1901), "1851-1900", IF((?birthyear < 1951), "1901-1950", "Post-1950") ) ) ) )) AS ?layer )
    ?person rdfs:label ?name FILTER (lang(?name) = "en")
    ?place rdfs:label ?birthplace FILTER (lang(?birthplace) = "en")
    } ORDER BY ?birthyear
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_places_of_birth_of_dead_economists,_colour-coded_by_era
  title: Map of places of birth of dead economists, colour-coded by era
  description: ''
- name: The number of existing translations for diseases in Wikidata
  namespace: examples
  domain: wikidata.org
  query_id: the-number-of-existing-translations-for-diseases-in-wikidata--examples@wikidata.org
  sparql: |2


    SELECT ?disease ?doid ?enLabel (count(?language) as ?languages)
    WHERE
    {
      ?disease wdt:P699 ?doid ;
                 rdfs:label ?label ;
                 rdfs:label ?enLabel .
        FILTER (lang(?enLabel) = "en")

        BIND (lang(?label) AS ?language)
    }
    GROUP BY ?disease ?doid ?enLabel
    ORDER BY desc(?languages)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_number_of_existing_translations_for_diseases_in_Wikidata
  title: The number of existing translations for diseases in Wikidata
  description: |-
    =
    Items used:
- name: 'A network of Drug-disease interactions on infectious diseases (Source: Disease
    Ontology, NDF-RT and ChEMBL)'
  namespace: examples
  domain: wikidata.org
  query_id: a-network-of-drug-disease-interactions-on-infectious-diseases-source-disease-ontology-ndf-rt-and-chembl--examples@wikidata.org
  sparql: |2

    #defaultView:Graph
    SELECT DISTINCT ?item ?itemLabel ?rgb ?link
    WHERE
    {
      VALUES ?toggle { true false }
      ?disease wdt:P699 ?doid;
               wdt:P279+ wd:Q18123741;
               wdt:P2176 ?drug.
      ?drug rdfs:label ?drugLabel.
        FILTER(LANG(?drugLabel) = "en").
      ?disease rdfs:label ?diseaseLabel.
        FILTER(LANG(?diseaseLabel) = "en").
      BIND(IF(?toggle,?disease,?drug) AS ?item).
      BIND(IF(?toggle,?diseaseLabel,?drugLabel) AS ?itemLabel).
      BIND(IF(?toggle,"FFA500","7FFF00") AS ?rgb).
      BIND(IF(?toggle,"",?disease) AS ?link).
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#A_network_of_Drug-disease_interactions_on_infectious_diseases_(Source:_Disease_Ontology,_NDF-RT_and_ChEMBL)
  title: 'A network of Drug-disease interactions on infectious diseases (Source: Disease
    Ontology, NDF-RT and ChEMBL)'
  description: '='
- name: The number of Wikidata items on Diseases and the percentage of those with
    a pointer to the Disease Ontology
  namespace: examples
  domain: wikidata.org
  query_id: the-number-of-wikidata-items-on-diseases-and-the-percentage-of-those-with-a-pointer-to-the-disease-ontology--examples@wikidata.org
  sparql: |2


    SELECT (COUNT(?disease) AS ?total) (SUM(?ref) AS ?byDO) (100*?byDO/?total AS ?percent)
    WHERE
    {
      {?disease wdt:P31 wd:Q12136 }
      UNION
      {?disease wdt:P279 wd:Q12136 .}
      OPTIONAL {
        ?disease p:P699 ?statement.
        BIND(1 AS ?ref).
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_number_of_Wikidata_items_on_Diseases_and_the_percentage_of_those_with_a_pointer_to_the_Disease_Ontology
  title: The number of Wikidata items on Diseases and the percentage of those with
    a pointer to the Disease Ontology
  description: '='
- name: Infectious diseases with their human minimum and maximum incubation time (in
    days)
  namespace: examples
  domain: wikidata.org
  query_id: infectious-diseases-with-their-human-minimum-and-maximum-incubation-time-in-days--examples@wikidata.org
  sparql: |2


    SELECT ?DiseaseLabel ((?min / 86400) AS ?Minimal_Incubation_Time) ((?max / 86400) AS ?Maximum_Incubation_Time)
    WHERE {
      ?Disease wdt:P31/wdt:P279* wd:Q18123741 .
      ?Disease p:P3488/psn:P3488/wikibase:quantityAmount ?min .
      ?Disease p:P3487/psn:P3487/wikibase:quantityAmount ?max

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
    }
    ORDER BY ?DiseaseLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Infectious_diseases_with_their_human_minimum_and_maximum_incubation_time_(in_days)
  title: Infectious diseases with their human minimum and maximum incubation time
    (in days)
  description: '='
- name: Genes
  namespace: examples
  domain: wikidata.org
  query_id: genes--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?item ?ncbi_gene ?date_modified
    WHERE
    {
      ?item wdt:P351 ?ncbi_gene ;
              wdt:P703 wd:Q15978631 ;
              schema:dateModified ?date_modified .
        BIND (now() - ?date_modified as ?date_range)
        FILTER (?date_range < 8)
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Genes
  title: Genes
  description: '='
- name: Human genes updated this week
  namespace: examples
  domain: wikidata.org
  query_id: human-genes-updated-this-week--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?item ?ncbi_gene ?date_modified
    WHERE
    {
      ?item wdt:P351 ?ncbi_gene ;
              wdt:P703 wd:Q15978631 ;
              schema:dateModified ?date_modified .
        BIND (now() - ?date_modified as ?date_range)
        FILTER (?date_range < 8)
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Human_genes_updated_this_week
  title: Human genes updated this week
  description: '='
- name: Counts of gene-variant types sourced from the CIViC database
  namespace: examples
  domain: wikidata.org
  query_id: counts-of-gene-variant-types-sourced-from-the-civic-database--examples@wikidata.org
  sparql: |2

    #defaultView:BarChart
    SELECT ?variant_typeLabel (count(?variant_typeLabel) as ?counts)
    WHERE
    {
      ?item wdt:P3329 ?civic_id ;
              wdt:P31 ?variant_type .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    GROUP BY ?variant_typeLabel
    ORDER BY ?counts
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Counts_of_gene-variant_types_sourced_from_the_CIViC_database
  title: Counts of gene-variant types sourced from the CIViC database
  description: '='
- name: Which variant of which gene predicts a positive prognosis in colorectal cancer
  namespace: examples
  domain: wikidata.org
  query_id: which-variant-of-which-gene-predicts-a-positive-prognosis-in-colorectal-cancer--examples@wikidata.org
  sparql: |2

    SELECT ?geneLabel ?variantLabel
    WHERE
    {
      VALUES ?disease {wd:Q188874}
        ?variant wdt:P3358 ?disease ; # P3358 Positive prognostic predictor
              wdt:P3433 ?gene . # P3433 biological variant of
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Which_variant_of_which_gene_predicts_a_positive_prognosis_in_colorectal_cancer
  title: Which variant of which gene predicts a positive prognosis in colorectal cancer
  description: '='
- name: Variants that are associated with renal cell carcinoma
  namespace: examples
  domain: wikidata.org
  query_id: variants-that-are-associated-with-renal-cell-carcinoma--examples@wikidata.org
  sparql: |2

    # ----
    # variants that are associated with renal cell carcinoma
    SELECT DISTINCT ?civic_id ?item ?itemLabel
    WHERE
    {
      VALUES ?property {
                          wdt:P3356 # positive diagnostic predictor
                          wdt:P3357 # negative diagnostic predictor
                          wdt:P3358 # positive prognostic predicator
                          wdt:P3359 # negative prognostic predictor
                         }
        ?item wdt:P3329 ?civic_id .
        {?item ?property wd:Q1164529.} # wd:Q1164529 = renal cell carcinoma
        UNION
        {?item p:P3354 ?o . # positive therapeutic predictor
         ?o pq:P2175 wd:Q1164529 .}
        UNION
        {?item p:P3354 ?o . # negative therapeutic predictor
         ?o pq:P2175 wd:Q1164529 .}

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Variants_that_are_associated_with_renal_cell_carcinoma
  title: Variants that are associated with renal cell carcinoma
  description: '='
- name: PubMed references in CIViCdb
  namespace: examples
  domain: wikidata.org
  query_id: pubmed-references-in-civicdb--examples@wikidata.org
  sparql: |2

    # ----
    # variants that are associated with renal cell carcinoma
    SELECT DISTINCT ?reference ?referenceLabel ?pmid
    WHERE
    {
      ?item wdt:P3329 ?civicId ;
              ?property ?object .
        ?object prov:wasDerivedFrom ?provenance .
        ?provenance pr:P248 ?reference .
        ?reference wdt:P31 wd:Q13442814 ;
                   wdt:P698 ?pmid .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#PubMed_references_in_CIViCdb
  title: PubMed references in CIViCdb
  description: '='
- name: Variant counts by predictor type
  namespace: examples
  domain: wikidata.org
  query_id: variant-counts-by-predictor-type--examples@wikidata.org
  sparql: |2

    #defaultView:BubbleChart
    SELECT ?propertyLabel (count(?prognostic_type) as ?counts)
    WHERE
    {
      VALUES ?prognostic_type {wdt:P3354 wdt:P3355 wdt:P3356 wdt:P3357 wdt:P3358 wdt:P3359}
        ?item wdt:P3329 ?civic_id ;
              ?prognostic_type ?prognostic_value .
        ?property wikibase:directClaim ?prognostic_type .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    GROUP BY ?propertyLabel ?prognostic_typeLabel
    ORDER BY ?counts
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Variant_counts_by_predictor_type
  title: Variant counts by predictor type
  description: '='
- name: Proteins
  namespace: examples
  domain: wikidata.org
  query_id: proteins--examples@wikidata.org
  sparql: |2

    SELECT ?item ?itemLabel ?uniprotid ?tax_node
    WHERE
    {
      ?item wdt:P352 ?uniprotid ;
              wdt:P703 wd:Q15978631 .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Proteins
  title: Proteins
  description: '='
- name: Get Wikidata - UniprotId mappings for homo sapiens
  namespace: examples
  domain: wikidata.org
  query_id: get-wikidata-uniprotid-mappings-for-homo-sapiens--examples@wikidata.org
  sparql: |2

    SELECT ?item ?itemLabel ?uniprotid ?tax_node
    WHERE
    {
      ?item wdt:P352 ?uniprotid ;
              wdt:P703 wd:Q15978631 .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_Wikidata_-_UniprotId_mappings_for_homo_sapiens
  title: Get Wikidata - UniprotId mappings for homo sapiens
  description: '='
- name: Metabolites and the species where they are found in
  namespace: examples
  domain: wikidata.org
  query_id: metabolites-and-the-species-where-they-are-found-in--examples@wikidata.org
  sparql: |2

    PREFIX pr: <http://www.wikidata.org/prop/reference/>
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    PREFIX prov: <http://www.w3.org/ns/prov#>

    SELECT ?compound ?compoundLabel ?speciesLabel ?sourceLabel ?doi ?wpid WHERE {
      ?compound wdt:P31 wd:Q11173.
      MINUS { ?compound wdt:P31 wd:Q8054. }
      ?compound p:P703 ?statement.
      ?statement rdf:type wikibase:BestRank.
      ?statement ps:P703 ?species.
      OPTIONAL {
        ?statement (prov:wasDerivedFrom/pr:P248) ?source.
        OPTIONAL { ?source wdt:P2410 ?wpid. }
        OPTIONAL { ?source wdt:P356 ?doi. }
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY ASC(?compound)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Metabolites_and_the_species_where_they_are_found_in
  title: Metabolites and the species where they are found in
  description: '='
- name: Metabolite-metabolite interactions (mostly conversions) and their pKa change
    (federated query)
  namespace: examples
  domain: wikidata.org
  query_id: metabolite-metabolite-interactions-mostly-conversions-and-their-pka-change-federated-query--examples@wikidata.org
  sparql: |2

    #defaultView:Dimensions
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
    PREFIX dcterms: <http://purl.org/dc/terms/>
    SELECT DISTINCT ?wpid ?metaboliteLabel ?pKa ?pKa2 ?metabolite2Label ?wpid2
    WITH {
      SELECT ?wpid ?source_pathway ?metabolite ?pKa ?pKa2 ?metabolite2 ?wpid2
      WHERE {
        # VALUES ?wpid { "WP550" }
        ?pathway wdt:P2410 ?wpid ;
                 wdt:P527 ?metabolite ;
                 wdt:P2888 ?source_pathway .
        ?metabolite wdt:P1117 ?pKa .

        SERVICE <http://sparql.wikipathways.org/sparql> {
          ?wp_mb1 wp:bdbWikidata ?metabolite .
          ?wp_mb1 dcterms:isPartOf ?interaction .
          ?interaction rdf:type wp:Interaction .
          ?wp_mb2 dcterms:isPartOf ?interaction .
          ?wp_mb2 wp:bdbWikidata ?metabolite2 .
          FILTER (?wp_mb1 != ?wp_mb2)
        }
      }
    } AS %result
    WHERE {
      INCLUDE %result
      ?metabolite2 wdt:P1117 ?pKa2 .
      ?pathway2 wdt:P2410 ?wpid2 ;
                 wdt:P527 ?metabolite2 ;
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Metabolite-metabolite_interactions_(mostly_conversions)_and_their_pKa_change_(federated_query)
  title: Metabolite-metabolite interactions (mostly conversions) and their pKa change
    (federated query)
  description: |-
    =
     Federated query using the WikiPathways SPARQL endpoint to retrieve interaction information.
     The dimensions plot show the pKa changes during metabolite-metabolite interaction.
     It must be noted here that many very basic or very acidic are reported in pathways as the uncharged structure, whereas in normal biological pathways these compounds are charged and then have quite different pKa charges.
- name: Asterophryinae parent taxon reverse graph
  namespace: examples
  domain: wikidata.org
  query_id: asterophryinae-parent-taxon-reverse-graph--examples@wikidata.org
  sparql: |2

    #defaultView:Graph
    PREFIX gas: <http://www.bigdata.com/rdf/gas#>

    SELECT ?item ?itemLabel ?pic ?linkTo
    WHERE
    {
      SERVICE gas:service {
        gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.SSSP" ;
                    gas:in wd:Q1968598;
                    gas:traversalDirection "Reverse" ;
                    gas:out ?item ;
                    gas:out1 ?depth ;
                    gas:maxIterations 3 ;
                    gas:linkType wdt:P171 .
      }
      OPTIONAL { ?item wdt:P171 ?linkTo }
      OPTIONAL { ?item wdt:P18 ?pic }
      SERVICE wikibase:label {bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Asterophryinae_parent_taxon_reverse_graph
  title: Asterophryinae parent taxon reverse graph
  description: '='
- name: Distribution map of taxon
  namespace: examples
  domain: wikidata.org
  query_id: distribution-map-of-taxon--examples@wikidata.org
  sparql: |2
     #defaultView:Map
    SELECT * WHERE {
      VALUES ?parent {
        wd:Q10908
      }
      ?taxon (wdt:P171*) ?parent;
        wdt:P8485 ?map.
      BIND(STRAFTER(STR(?taxon), "Q") AS ?idStr)
      BIND(xsd:integer(?idStr) AS ?taxonId)
      BIND(SHA1(CONCAT(STR(?taxonId))) AS ?hash)
      BIND(CONCAT(SUBSTR(?hash, 1 , 2 ), SUBSTR(?hash, 3 , 2 ), SUBSTR(?hash, 5 , 2 )) AS ?rgb)
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Distribution_map_of_taxon
  title: Distribution map of taxon
  description: '='
- name: All human pathways from Wikipathways
  namespace: examples
  domain: wikidata.org
  query_id: all-human-pathways-from-wikipathways--examples@wikidata.org
  sparql: |2

    SELECT ?pathway ?pathwayLabel ?wpid WHERE {
       ?pathway wdt:P2410 ?wpid ;
                wdt:P703 wd:Q15978631 .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_human_pathways_from_Wikipathways
  title: All human pathways from Wikipathways
  description: '='
- name: Biomarkers in Wikidata which interact with proteins in human pathways from
    Wikipathways
  namespace: examples
  domain: wikidata.org
  query_id: biomarkers-in-wikidata-which-interact-with-proteins-in-human-pathways-from-wikipathways--examples@wikidata.org
  sparql: |2

    prefix void:  <http://rdfs.org/ns/void#>
    prefix pav:   <http://purl.org/pav/>
    prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
    prefix freq:  <http://purl.org/cld/freq/>
    prefix biopax: <http://www.biopax.org/release/biopax-level3.owl#>
    prefix skos:  <http://www.w3.org/2004/02/skos/core#>
    prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
    prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    prefix gpml:  <http://vocabularies.wikipathways.org/gpml#>
    prefix wp:    <http://vocabularies.wikipathways.org/wp#>
    prefix dcterms: <http://purl.org/dc/terms/>
    prefix wprdf: <http://rdf.wikipathways.org/>
    prefix prov:  <http://www.w3.org/ns/prov#>
    prefix foaf:  <http://xmlns.com/foaf/0.1/>
    prefix dc:    <http://purl.org/dc/elements/1.1/>

    SELECT DISTINCT ?biomarkerLabel ?proteinLabel ?geneID ?WP_gene ?PathwayID ?PathwayName #results that are displayed.
    WHERE {
      VALUES ?biomarker {wd:Q420633 wd:Q27125809 wd:Q422462} #you can add more biomarkers here if needed, separated by a space.
      ?biomarker wdt:P31 wd:Q11173. #Stating that all biomarkers have to be "instance of" "chemical compound" (you could ommit this, but query will probably take longer).
      ?biomarker wdt:P638 ?pdbID . #Checking if a biomarker has a Protein Databank ID (PDB) -> meaning the metabolite can interact with a protein.
      ?protein wdt:P31 wd:Q8054 . #Stating that all proteins are "instance of" "protein"
      ?protein wdt:P638 ?pdbID . #Checking which proteins have a PDB ID, which we queried previously in relationship to the biomarkers.
      ?protein wdt:P702 ?gene . #Connecting the protein to a gene ("encoded by" relationship) -> to get an identifier we can use later in federated WikiPathways query.
      ?gene wdt:P703 wd:Q15978631 . #Now removing all genes that are not found in species "Homo sapiens". -> This info is not always available for proteins in WikiData.
      ?gene wdt:P2888 ?geneID . #Getting the "exact match" identifier for the gene, related to the protein, related to the biomarker.

      ##The IRI from Wikidata starts with http:// , where the one from WikiPathways starts with https:// , so we need to rewrite the IRI
        BIND(                      # Bind the created IRI into a new variable (called ?newIRI)
            IRI(                   # Convert the string back to an IRI
              CONCAT(              # Concatenate item 1 and 2 together as one string
                   "https",        # First item to concat (more items can be added with a comma
                  #Second item to concat:
                   SUBSTR(         # Obtain a substring
                     STR(?geneID), # Convert the geneID IRI from Wikidata to a string,
                     5)            # removing the first 5 characters (<http)
            )) AS ?newIRI          # Name for the new variable
        )

      SERVICE <http://sparql.wikipathways.org/sparql> { #Connecting to the WikiPathways SPARQL endpoint.
         ?WP_pathway a wp:Pathway . #Stating a ?WP_pathway is indeed a pathway in the WikiPathways RDF .
         ?WP_pathway wp:organismName "Homo sapiens" . #Removing all PWs not for species Homo sapiens.
         ?WP_pathway dc:identifier ?PathwayID . #Query the identifier of the pathway in WPs.
         ?WP_pathway dc:title ?PathwayName . #Obtaining the name of the pathway.

         ?WP_gene a wp:Protein . #Stating that a ?WP_gene is a Protein DataNode (you could ommit this, to also get all DataNodes modeled as GeneProducts out, but query will take longer).
         ?WP_gene wp:bdbEntrezGene ?newIRI . #Connecting the previously queried "exact match" from WikiData to the NCBI/Entrez Gene ID in WPs.
         ?WP_gene dcterms:isPartOf ?WP_pathway . #Connecting the WP_gene to the WP_pathway.

       }
     OPTIONAL {?biomarker rdfs:label ?biomarkerLabel. #Create a label (aka name) for the biomarkers in WikiData, without using the service query.
    FILTER(LANG(?biomarkerLabel) = "en").
    }
       OPTIONAL {?protein rdfs:label ?proteinLabel. #Create a label(aka name) for the proteins in WikiData, without using the service query.
    FILTER(LANG(?proteinLabel) = "en").
    }

    }
    ORDER BY DESC (?biomarkerLabel) #Order results for biomarkers
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Biomarkers_in_Wikidata_which_interact_with_proteins_in_human_pathways_from_Wikipathways
  title: Biomarkers in Wikidata which interact with proteins in human pathways from
    Wikipathways
  description: '='
- name: Ranking of most cited work in WikiPathways
  namespace: examples
  domain: wikidata.org
  query_id: ranking-of-most-cited-work-in-wikipathways--examples@wikidata.org
  sparql: |2

    SELECT ?citation ?citationLabel (count(?pathway) as ?times_cited) WHERE {
      ?pathway wdt:P2410 ?WikiPathwaysID ;
               wdt:P2860 ?citation .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
      }
    GROUP BY ?pathway ?citation ?citationLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Ranking_of_most_cited_work_in_WikiPathways
  title: Ranking of most cited work in WikiPathways
  description: '='
- name: Gene and metabolite counts per pathway
  namespace: examples
  domain: wikidata.org
  query_id: gene-and-metabolite-counts-per-pathway--examples@wikidata.org
  sparql: |2

    # ----
    #Gene and metabolite counts per path
    #defaultView:ScatterChart
    SELECT ?path ?genes ?metabolites ?pathway WHERE {
      {SELECT DISTINCT ?path (COUNT(?pwPart) AS ?genes) WHERE {
          ?path wdt:P2410 ?WikipathsID.
          ?path wdt:P527 ?pwPart.
          ?pwPart wdt:P31 wd:Q7187.
          SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
        }
        GROUP BY ?path ?genes
      }
      {SELECT DISTINCT ?path (COUNT(?pwPart) AS ?metabolites) WHERE {
          ?path wdt:P2410 ?WikipathsID.
          ?path wdt:P527 ?pwPart.
          ?pwPart wdt:P31 wd:Q11173.
          SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
        }
        GROUP BY ?path ?metabolites
      }
      OPTIONAL { ?path rdfs:label ?pathway. }
    }
    ORDER BY DESC(?genes)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Gene_and_metabolite_counts_per_pathway
  title: Gene and metabolite counts per pathway
  description: '='
- name: Biological pathways with protein structures in the PDB database
  namespace: examples
  domain: wikidata.org
  query_id: biological-pathways-with-protein-structures-in-the-pdb-database--examples@wikidata.org
  sparql: |2

    SELECT ?pathway ?pathwayLabel ?WikiPathways ?Reactome (COUNT(DISTINCT ?protein) as ?count) WHERE {
      VALUES ?pathwayType { wd:Q4915012 wd:Q2996394 }
      ?pathway wdt:P31 ?pathwayType .
      { ?pathway wdt:P527/wdt:P688 ?protein . } UNION { ?pathway wdt:P527 ?protein . }
      ?protein wdt:P638 ?PDBID .
      OPTIONAL { ?pathway wdt:P2410 ?WikiPathways }
      OPTIONAL { ?pathway wdt:P3937 ?Reactome }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } GROUP BY ?pathway ?pathwayLabel ?WikiPathways ?Reactome
      ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Biological_pathways_with_protein_structures_in_the_PDB_database
  title: Biological pathways with protein structures in the PDB database
  description: '='
- name: 'Get known variants reported in <tvar name=q1>{{Q|Q27612411}}</tvar> of genes
    reported in a Wikipathways pathway: <tvar name=q2>{{Q|Q30230812}}</tvar>'
  namespace: examples
  domain: wikidata.org
  query_id: get-known-variants-reported-in-tvar-name-q1-q-q27612411-tvar-of-genes-reported-in-a-wikipathways-pathway-tvar-name-q2-q-q30230812-tvar--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?pathway ?pathwayLabel ?pwpart ?pwpartLabel ?variant ?variantLabel ?disease?diseaseLabel WHERE {

       VALUES ?predictor {p:P3354 p:P3355 p:P3356 p:P3357 p:P3358 p:P3359}
       VALUES ?predictorQualifier {pq:P2175}
       VALUES ?wpID {"WP2828"}

       ?pathway wdt:P2410 ?wpID ; # Pathways has a Wikipathways identifier
              wdt:P527 ?pwpart . # which contains pathways parts

       ?disease wdt:P279+ wd:Q504775 . # The disease is a subclass of urinary bladder cancer
                                       # based on annotations in the Disease ontology
       ?variant wdt:P3329 ?civicID ; # a variant known in CIViC
                 ?predictor ?node ; # has a predicting relation with diseases
                                       # labeled as being a subclass of urinary bladder cancer
                 wdt:P3433 ?pwpart . # variant is biological variant of

       {?node ?predictorStatement ?drug_label ;
                  ?predictorQualifier ?disease .}
       UNION
       {
          ?node ?predictorStatement ?disease .
       }
       SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_known_variants_reported_in_<tvar_name=q1>{{Q|Q27612411}}</tvar>_of_genes_reported_in_a_Wikipathways_pathway:_<tvar_name=q2>{{Q|Q30230812}}</tvar>
  title: 'Get known variants reported in <tvar name=q1>{{Q|Q27612411}}</tvar> of genes
    reported in a Wikipathways pathway: <tvar name=q2>{{Q|Q30230812}}</tvar>'
  description: '='
- name: Known interaction types in Wikipathways for a pathway with Identifier WP716
    (federated query)
  namespace: examples
  domain: wikidata.org
  query_id: known-interaction-types-in-wikipathways-for-a-pathway-with-identifier-wp716-federated-query--examples@wikidata.org
  sparql: |
    PREFIX dcterms: <http://purl.org/dc/terms/>
    PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
    SELECT DISTINCT ?interaction_type WHERE {
      VALUES ?wpid {"WP716"}
      ?item wdt:P2410 ?wpid ;
            wdt:P2888 ?source_pathway .

      SERVICE <http://sparql.wikipathways.org/sparql> {
         ?wp_pathway dc:identifier ?source_pathway .
         ?s dcterms:isPartOf ?wp_pathway, ?interaction .
         ?interaction rdf:type wp:Interaction .
         ?interaction rdf:type ?interaction_type .
         ?interaction wp:participants ?participants .
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Known_interaction_types_in_Wikipathways_for_a_pathway_with_Identifier_WP716_(federated_query)
  title: Known interaction types in Wikipathways for a pathway with Identifier WP716
    (federated query)
  description: '='
- name: Local annotations from Wikipathways using Federated query on a Pathway with
    identifier WP716 (federated query)
  namespace: examples
  domain: wikidata.org
  query_id: local-annotations-from-wikipathways-using-federated-query-on-a-pathway-with-identifier-wp716-federated-query--examples@wikidata.org
  sparql: |
    PREFIX dcterms: <http://purl.org/dc/terms/>
    PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
    SELECT DISTINCT ?item ?pw_annotation WHERE {
      VALUES ?wpid {"WP716"}
      ?item wdt:P2410 ?wpid ;
            wdt:P2888 ?source_pathway .

      SERVICE <http://sparql.wikipathways.org/sparql> {
         ?wp_pathway dc:identifier ?source_pathway .
         ?wp_pathway wp:ontologyTag ?pw_annotation .
         # ?pw_annotation rdfs:label ?annotation_label .
       }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Local_annotations_from_Wikipathways_using_Federated_query_on_a_Pathway_with_identifier_WP716_(federated_query)
  title: Local annotations from Wikipathways using Federated query on a Pathway with
    identifier WP716 (federated query)
  description: '='
- name: Find drugs for cancers that target genes related to cell proliferation
  namespace: examples
  domain: wikidata.org
  query_id: find-drugs-for-cancers-that-target-genes-related-to-cell-proliferation--examples@wikidata.org
  sparql: |2-

    # ----
    #cases where a drug physically interacts with the product of gene known to be genetically associated a disease
    #these cases may show opportunities to repurpose a drug for a new disease
    #See http://database.oxfordjournals.org/content/2016/baw083.long and
    #http://drug-repurposing.nationwidechildrens.org/search
    #an example that was recently validated involved a new link between Metformin wd:Q19484 and cancer survival
    #https://jamia.oxfordjournals.org/content/22/1/179
    #currently set up to find drugs for cancers that target genes related to cell proliferation
    #adapt by changing constraints (e.g. to 'heart disease' Q190805) or removing them
    SELECT ?drugLabel ?geneLabel ?biological_processLabel ?diseaseLabel
    WHERE {
      ?drug wdt:P129 ?gene_product . # drug interacts with a gene_product
      ?gene wdt:P688 ?gene_product . # gene_product (usually a protein) is a product of a gene (a region of DNA)
      ?disease wdt:P2293 ?gene .     # genetic association between disease and gene
      ?disease wdt:P279* wd:Q12078 . # limit to cancers wd:Q12078 (the * operator runs up a transitive relation..)
      ?gene_product wdt:P682 ?biological_process . #add information about the GO biological processes that the gene is related to

       ?biological_process (wdt:P361
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Find_drugs_for_cancers_that_target_genes_related_to_cell_proliferation
  title: Find drugs for cancers that target genes related to cell proliferation
  description: ''
- name: Parent taxons of Blue Whale
  namespace: examples
  domain: wikidata.org
  query_id: parent-taxons-of-blue-whale--examples@wikidata.org
  sparql: |2


    #defaultView:Graph
    SELECT ?item ?itemLabel ?pic ?linkTo
    WHERE
    {
      wd:Q42196 wdt:P171* ?item
      OPTIONAL { ?item wdt:P171 ?linkTo }
      OPTIONAL { ?item wdt:P18 ?pic }
      SERVICE wikibase:label {bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Parent_taxons_of_Blue_Whale
  title: Parent taxons of Blue Whale
  description: "* Items used: \n* Properties used:"
- name: Mosquito species
  namespace: examples
  domain: wikidata.org
  query_id: mosquito-species--examples@wikidata.org
  sparql: |2

    # ----
    # Species of mosquitoes
    # added 2017-06
    SELECT ?item ?taxonname WHERE {
      ?item wdt:P31 wd:Q16521 ;
            wdt:P105 wd:Q7432 ;
            wdt:P171* wd:Q7367 ;
            wdt:P225 ?taxonname .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mosquito_species
  title: Mosquito species
  description: "* Items used:   \n* Properties used:"
- name: Taxons and what they are named after
  namespace: examples
  domain: wikidata.org
  query_id: taxons-and-what-they-are-named-after--examples@wikidata.org
  sparql: |2


    SELECT ?taxon ?eponym ?taxonName ?eponymLabel
    WHERE
    {
      ?taxon wdt:P31 wd:Q16521;
             wdt:P225 ?taxonName;
             wdt:P138 ?eponym.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY ?eponym
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Taxons_and_what_they_are_named_after
  title: Taxons and what they are named after
  description: ''
- name: Biologists with Twitter accounts
  namespace: examples
  domain: wikidata.org
  query_id: biologists-with-twitter-accounts--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?personLabel (CONCAT("https://twitter.com/",?twitterName) AS ?twitterlink) ?pic
    WHERE {
      ?person wdt:P2002 ?twitterName ;
        wdt:P106 ?occupation .
      OPTIONAL { ?person wdt:P18 ?pic . }
      ?occupation wdt:P279* wd:Q864503 . # all subclasses of biologists
       SERVICE wikibase:label {
         bd:serviceParam wikibase:language "en"
       }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Biologists_with_Twitter_accounts
  title: Biologists with Twitter accounts
  description: ''
- name: Cell lines with names that could also be URLs (Internet of Cell Lines).
  namespace: examples
  domain: wikidata.org
  query_id: cell-lines-with-names-that-could-also-be-urls-internet-of-cell-lines--examples@wikidata.org
  sparql: |
    SELECT * WHERE {
      {
        SELECT ?cell_line ?cell_line_name WHERE {
          ?cell_line wdt:P31 wd:Q21014462;
                     rdfs:label ?cell_line_name.
              FILTER(LANG(?cell_line_name) = "en").
              Filter REGEX(STR(?cell_line_name), "^[\\w\\-\\.]+\\.[A-z]+$")
        }
      }
      ?tld wdt:P31/wdt:P279* wd:Q14296;
           rdfs:label ?tld_name.
      FILTER(LANG(?tld_name) = "en").
      FILTER REGEX(STR(?cell_line_name), CONCAT(REPLACE(?tld_name, "\\.", "\\\\."), "$"), "i")
      BIND(URI(CONCAT("http://", ?cell_line_name)) as ?url)
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cell_lines_with_names_that_could_also_be_URLs_(Internet_of_Cell_Lines).
  title: Cell lines with names that could also be URLs (Internet of Cell Lines).
  description: ''
- name: List of pharmaceutical drugs with picture
  namespace: examples
  domain: wikidata.org
  query_id: list-of-pharmaceutical-drugs-with-picture--examples@wikidata.org
  sparql: |2


    SELECT ?moleculeLabel ?formule ?picture ?molecule
    WHERE
    {
      ?molecule wdt:P31 wd:Q12140 ;
                wdt:P274 ?formule ;
                wdt:P117 ?picture
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en, de" . }
    }
    ORDER BY ?moleculeLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_pharmaceutical_drugs_with_picture
  title: List of pharmaceutical drugs with picture
  description: ''
- name: Organisms that are located in the female urogential tract and that have a
    gene with product indole
  namespace: examples
  domain: wikidata.org
  query_id: organisms-that-are-located-in-the-female-urogential-tract-and-that-have-a-gene-with-product-indole--examples@wikidata.org
  sparql: |2


    SELECT ?organism_name
    WHERE
    {
      ?organism_item wdt:P2974 wd:Q5880 ;
                     rdfs:label ?organism_name .
      ?gene wdt:P703 ?organism_item ;
            wdt:P1056 wd:Q319541 .
      FILTER (LANG(?organism_name) = "en") .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Organisms_that_are_located_in_the_female_urogential_tract_and_that_have_a_gene_with_product_indole
  title: Organisms that are located in the female urogential tract and that have a
    gene with product indole
  description: "* Items used:  \n* Properties used:"
- name: Threatened Species of Animals as per IUCN Classification
  namespace: examples
  domain: wikidata.org
  query_id: threatened-species-of-animals-as-per-iucn-classification--examples@wikidata.org
  sparql: |2

    # ----
    #title: <translate><!--T:778--> Animals which are Threatened as per IUCN Red List</translate>
    # https://en.wikipedia.org/wiki/Conservation_status
    # Common names are in English only

    SELECT ?animal ?scientific_names ?common_names ?statusLabel where
    {
      # hint:Prior hint:runLast true
       {
          SELECT DISTINCT ?animal (GROUP_CONCAT(DISTINCT ?scientific_name; separator=", ") as ?scientific_names) (GROUP_CONCAT(DISTINCT ?common_name; separator=", ") as ?common_names) WHERE
          {

            ?animal wdt:P141 ?status;
                    wdt:P225 ?scientific_name;
                    wdt:P1843 ?common_name.
            filter( ?status
               IN (
                   wd:Q11394,  #Endangered
                   wd:Q219127, #critcally endangered
                   wd:Q278113  #vulnurable
                  )
              ).

            # Only return common names in English
            FILTER(LANGMATCHES(LANG(?common_name), "en"))
          }
          GROUP BY ?animal
       }.
       ?animal wdt:P141 ?status.
       SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Threatened_Species_of_Animals_as_per_IUCN_Classification
  title: Threatened Species of Animals as per IUCN Classification
  description: ''
- name: List of computer files formats
  namespace: examples
  domain: wikidata.org
  query_id: list-of-computer-files-formats--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?idExtension ?extension ?mediaType ?idExtensionLabel
    WHERE
    {
      ?idExtension wdt:P31 wd:Q235557 ;
                   wdt:P1195 ?extension .
      OPTIONAL { ?idExtension wdt:P1163 ?mediaType }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY ?extension ?mediaType
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_computer_files_formats
  title: List of computer files formats
  description: ''
- name: List of W3C standards
  namespace: examples
  domain: wikidata.org
  query_id: list-of-w3c-standards--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?standard ?standardLabel ?website
    WHERE
    {
            ?standard wdt:P1462 wd:Q37033 .
            OPTIONAL{ ?standard wdt:P856 ?website }
            SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY ?standardLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_W3C_standards
  title: List of W3C standards
  description: ''
- name: Oldest software
  namespace: examples
  domain: wikidata.org
  query_id: oldest-software--examples@wikidata.org
  sparql: |
    SELECT ?software ?softwareLabel ?date (ROUND((NOW() - ?date)/365.2425) AS ?age)
    {
      ?software wdt:P31/wdt:P139* wd:Q7397.
      OPTIONAL { ?software wdt:P571 ?date. }
      OPTIONAL { ?software p:P348/pq:P577 ?date. }
      FILTER(BOUND(?date)).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY ?date
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Oldest_software
  title: Oldest software
  description: ''
- name: Software written in Go programming language
  namespace: examples
  domain: wikidata.org
  query_id: software-written-in-go-programming-language--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?instance_of ?instance_ofDescription ?instance_ofLabel ?official_website WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      ?instance_of (wdt:P31/(wdt:P279*)) wd:Q341.
      OPTIONAL { ?instance_of wdt:P856 ?official_website. }
      ?instance_of wdt:P277 wd:Q37227.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Software_written_in_Go_programming_language
  title: Software written in Go programming language
  description: ''
- name: Free and open-source software written in Go programming language
  namespace: examples
  domain: wikidata.org
  query_id: free-and-open-source-software-written-in-go-programming-language--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?instance_of ?instance_ofDescription ?instance_ofLabel ?official_website WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      ?instance_of wdt:P31/wdt:P279* wd:Q341
      OPTIONAL { ?instance_of wdt:P856 ?official_website. }
      ?instance_of wdt:P277 wd:Q37227.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Free_and_open-source_software_written_in_Go_programming_language
  title: Free and open-source software written in Go programming language
  description: ''
- name: Freeware games for Windows, ordered from recent date
  namespace: examples
  domain: wikidata.org
  query_id: freeware-games-for-windows-ordered-from-recent-date--examples@wikidata.org
  sparql: "\nSELECT DISTINCT\n    ?game\n    ?gameLabel\n    (MIN(?publication_date)\
    \ as ?publicationDateMin)\n    (GROUP_CONCAT(?genre_label; SEPARATOR=\", \") as\
    \ ?genres) \n    ?gameDescription\n    ?steamLink\n    ?official_website\nWHERE\
    \ {\n    ?game wdt:P31 wd:Q7889.\n    ?game wdt:P400 wd:Q1406.\n    ?game wdt:P275\
    \ wd:Q178285.\n    OPTIONAL { ?game wdt:P577 ?publication_date }\n    OPTIONAL\
    \ {\n        ?game wdt:P136 ?genre .\n        ?genre rdfs:label ?genre_label FILTER\
    \ (lang(?genre_label) = \"en\").\n    }\n    OPTIONAL { ?game wdt:P1733 ?steam\
    \ }\n    BIND(URI(CONCAT(\"https://store.steampowered.com/app/\", ?steam)) as\
    \ ?steamLink)\n    OPTIONAL { ?game wdt:P856 ?official_website }\n    SERVICE\
    \ wikibase:label {\n        bd:serviceParam wikibase:language \"en\".\n    }\n\
    }\nGROUP BY ?game ?gameLabel ?publicationDateMin ?genres ?gameDescription ?steamLink\
    \ ?official_website\nORDER BY DESC(?publicationDateMin)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Freeware_games_for_Windows,_ordered_from_recent_date
  title: Freeware games for Windows, ordered from recent date
  description: ''
- name: Universities of main programming language authors
  namespace: examples
  domain: wikidata.org
  query_id: universities-of-main-programming-language-authors--examples@wikidata.org
  sparql: |
    SELECT ?lang ?langLabel ?human ?humanLabel ?educatedat ?educatedatLabel ?coords
    {
      ?lang wdt:P31/wdt:P279* wd:Q9143 .
      ?human wdt:P31 wd:Q5 .
      { ?lang wdt:P287 ?human } UNION { ?lang wdt:P170 ?human } UNION { ?lang wdt:P943 ?human } UNION { ?lang wdt:P178 ?human } .

      ?human wdt:P69 ?educatedat .
      ?educatedat wdt:P625 ?coords .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en,fr" }
    }
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Universities_of_main_programming_language_authors
  title: Universities of main programming language authors
  description: ''
- name: Websites with OpenAPI endpoints
  namespace: examples
  domain: wikidata.org
  query_id: websites-with-openapi-endpoints--examples@wikidata.org
  sparql: |2

    SELECT ?database ?databaseLabel ?license ?licenseLabel ?value WHERE {
      ?database ?p ?wds .
      OPTIONAL { ?database wdt:P275 ?license }
      ?wds ?v ?value.
      ?wdP wikibase:statementProperty ?v.
      ?wdP wikibase:claim ?p.
      ?wds pq:P31 wd:Q27075870.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    } ORDER BY ASC(?databaseLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Websites_with_OpenAPI_endpoints
  title: Websites with OpenAPI endpoints
  description: ''
- name: E-Readers that support the mobipocket file format
  namespace: examples
  domain: wikidata.org
  query_id: e-readers-that-support-the-mobipocket-file-format--examples@wikidata.org
  sparql: |2

    SELECT ?ereader ?ereaderLabel

    WHERE {
      ?ereader wdt:P31 wd:Q726235 .
      ?ereader wdt:P1072 wd:Q1941622 .

      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .

    }
      }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#E-Readers_that_support_the_mobipocket_file_format
  title: E-Readers that support the mobipocket file format
  description: ''
- name: Software applications ranked in descending order by the number of writable
    file formats
  namespace: examples
  domain: wikidata.org
  query_id: software-applications-ranked-in-descending-order-by-the-number-of-writable-file-formats--examples@wikidata.org
  sparql: |2

    # ----
    #defaultView:BubbleChart
    #title:<translate><!--T:779--> Software applications ranked in descending order by the number of writable file formats</translate>
    SELECT ?app ?appLabel (COUNT(?format) AS ?count)
    WHERE {
      ?app (p:P31/ps:P31/wdt:P279) wd:Q7397 .
      ?app wdt:P1072 ?format .

      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .

    }
      }

    GROUP BY ?app ?appLabel
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Software_applications_ranked_in_descending_order_by_the_number_of_writable_file_formats
  title: Software applications ranked in descending order by the number of writable
    file formats
  description: ''
- name: Return a bubble chart of mediatypes by count of file formats
  namespace: examples
  domain: wikidata.org
  query_id: return-a-bubble-chart-of-mediatypes-by-count-of-file-formats--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?mediaType (COUNT (?ff) as ?count)
    WHERE
    {
      ?ff wdt:P31/wdt:P279* wd:Q235557.
      ?ff wdt:P1163 ?mediaType.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }

    GROUP BY ?mediaType
    ORDER BY DESC (?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Return_a_bubble_chart_of_mediatypes_by_count_of_file_formats
  title: Return a bubble chart of mediatypes by count of file formats
  description: ''
- name: Erdos Numbers and images of people who have oral histories in the Computer
    History Museum's collection
  namespace: examples
  domain: wikidata.org
  query_id: erdos-numbers-and-images-of-people-who-have-oral-histories-in-the-computer-history-museum-s-collection--examples@wikidata.org
  sparql: |2

    #defaultView:ImageGrid
    SELECT ?personLabel ?image ?Erdos

    WHERE {
      ?person wdt:P485 wd:Q964035 .
      ?person wdt:P18 ?image .
      ?person wdt:P2021 ?Erdos
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .

    }
      }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Erdos_Numbers_and_images_of_people_who_have_oral_histories_in_the_Computer_History_Museum's_collection
  title: Erdos Numbers and images of people who have oral histories in the Computer
    History Museum's collection
  description: ''
- name: Chemical elements and their properties
  namespace: examples
  domain: wikidata.org
  query_id: chemical-elements-and-their-properties--examples@wikidata.org
  sparql: |2


    SELECT ?elementLabel ?_boiling_point ?_melting_point ?_electronegativity ?_density ?_mass
    WHERE
    {
      ?element wdt:P31 wd:Q11344.
      ?element wdt:P2102 ?_boiling_point.
      ?element wdt:P2101 ?_melting_point.
      ?element wdt:P1108 ?_electronegativity.
      ?element wdt:P2054 ?_density.
      ?element wdt:P2067 ?_mass.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Chemical_elements_and_their_properties
  title: Chemical elements and their properties
  description: ''
- name: Chemical elements and their isotopes by number of neutrons (min/max)
  namespace: examples
  domain: wikidata.org
  query_id: chemical-elements-and-their-isotopes-by-number-of-neutrons-min-max--examples@wikidata.org
  sparql: |2


    SELECT ?element (SAMPLE(?symbol) AS ?symbol) (SAMPLE(?protons) AS ?protons) (MIN(?neutrons) AS ?minNeutrons) (MAX(?neutrons) AS ?maxNeutrons)
    WHERE
    {
      ?element wdt:P31 wd:Q11344;
               wdt:P1086 ?protons;
               wdt:P246 ?symbol.
      ?isotope wdt:P279 ?element;
               wdt:P1148 ?neutrons.
    }
    GROUP BY ?element
    ORDER BY ?protons
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Chemical_elements_and_their_isotopes_by_number_of_neutrons_(min/max)
  title: Chemical elements and their isotopes by number of neutrons (min/max)
  description: ''
- name: Colors of chemical compounds
  namespace: examples
  domain: wikidata.org
  query_id: colors-of-chemical-compounds--examples@wikidata.org
  sparql: |2


    #defaultView:BubbleChart
    SELECT ?rgb ?colorLabel (COUNT(?compound) AS ?count)
    WHERE
    {
      ?compound wdt:P31 wd:Q11173;
                wdt:P462 ?color.
      OPTIONAL { ?color wdt:P465 ?rgb. }
      BIND(IF(BOUND(?rgb),?rgb,"CCCCCC") AS ?rgb).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    GROUP BY ?rgb ?colorLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Colors_of_chemical_compounds
  title: Colors of chemical compounds
  description: ''
- name: All pKa data in Wikidata and the source titles
  namespace: examples
  domain: wikidata.org
  query_id: all-pka-data-in-wikidata-and-the-source-titles--examples@wikidata.org
  sparql: |2


    SELECT ?compound ?compoundLabel ?pKa ?source ?sourceLabel ?doi
    WHERE
    {
      ?compound wdt:P31 wd:Q11173 ; p:P1117 ?statement .
      ?statement rdf:type wikibase:BestRank ;
        ps:P1117 ?pKa .
      OPTIONAL {
        ?statement prov:wasDerivedFrom/pr:P248 ?source .
        OPTIONAL { ?source wdt:P356 ?doi . }
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". } #English label
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_pKa_data_in_Wikidata_and_the_source_titles
  title: All pKa data in Wikidata and the source titles
  description: ''
- name: All CAS registry numbers in Wikidata
  namespace: examples
  domain: wikidata.org
  query_id: all-cas-registry-numbers-in-wikidata--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?compound ?compoundLabel ?cas
    WHERE
    {
      ?compound wdt:P231 ?cas .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_CAS_registry_numbers_in_Wikidata
  title: All CAS registry numbers in Wikidata
  description: ''
- name: Chemical compounds in Wikidata sharing the same CAS registry number
  namespace: examples
  domain: wikidata.org
  query_id: chemical-compounds-in-wikidata-sharing-the-same-cas-registry-number--examples@wikidata.org
  sparql: |2

    # ----
    #two chemical compounds with the same CAS registry number
    SELECT DISTINCT ?cas ?compound1 ?compound1Label ?compound2 ?compound2Label WHERE {
      ?compound1 wdt:P231 ?cas .
      ?compound2 wdt:P231 ?cas .
      FILTER (?compound1 != ?compound2)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Chemical_compounds_in_Wikidata_sharing_the_same_CAS_registry_number
  title: Chemical compounds in Wikidata sharing the same CAS registry number
  description: ''
- name: The number of Chemical compounds in Wikidata sharing the same CAS registry
    number
  namespace: examples
  domain: wikidata.org
  query_id: the-number-of-chemical-compounds-in-wikidata-sharing-the-same-cas-registry-number--examples@wikidata.org
  sparql: |2

    # ----
    #The number of times a cas registry number is shared by distinct Wikidata items
    SELECT ?cas ?items
    WHERE
    {
      {SELECT DISTINCT ?cas (count(?compound) as ?items) WHERE {
          ?compound wdt:P231 ?cas .
      }
        GROUP BY ?cas }
        FILTER (?items >1)
    }
    ORDER BY desc(?items)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_number_of_Chemical_compounds_in_Wikidata_sharing_the_same_CAS_registry_number
  title: The number of Chemical compounds in Wikidata sharing the same CAS registry
    number
  description: ''
- name: Awarded Chemistry Nobel Prizes
  namespace: examples
  domain: wikidata.org
  query_id: awarded-chemistry-nobel-prizes--examples@wikidata.org
  sparql: |2


    #defaultView:Timeline
    SELECT DISTINCT ?item ?itemLabel ?when (YEAR(?when) as ?date) ?pic
    WHERE {
      ?item p:P166 ?awardStat . # … with an awarded(P166) statement
      ?awardStat ps:P166 wd:Q44585 . # … that has the value Nobel Prize in Chemistry (Q44585)
      ?awardStat pq:P585 ?when . # when did they receive the Nobel prize

    SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    OPTIONAL { ?item wdt:P18 ?pic }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Awarded_Chemistry_Nobel_Prizes
  title: Awarded Chemistry Nobel Prizes
  description: ''
- name: Images of organic acids
  namespace: examples
  domain: wikidata.org
  query_id: images-of-organic-acids--examples@wikidata.org
  sparql: |2-

    #defaultView:ImageGrid
    SELECT ?compound ?compoundLabel ?image WHERE {
      ?compound wdt:P279+
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Images_of_organic_acids
  title: Images of organic acids
  description: ''
- name: Boiling points of alkanes
  namespace: examples
  domain: wikidata.org
  query_id: boiling-points-of-alkanes--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?comp ?compLabel ?formula ?bp ?bpUnit ?bpUnitLabel WHERE {
      ?comp wdt:P31/wdt:P279* wd:Q41581 ;
            wdt:P274 ?formula ;
            p:P2102 [
              ps:P2102 ?bp ;
              psv:P2102/wikibase:quantityUnit ?bpUnit
            ] .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY DESC(?bpUnit) ASC(?bp)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Boiling_points_of_alkanes
  title: Boiling points of alkanes
  description: ''
- name: Solubilities of chemicals
  namespace: examples
  domain: wikidata.org
  query_id: solubilities-of-chemicals--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?chemical ?chemicalLabel ?value ?units ?unitsLabel ?solvent ?solventLabel ?temperature ?temperatureUnit ?temperatureUnitLabel ?source ?sourceLabel ?doi
    WITH {
      SELECT DISTINCT ?chemical ?value ?units ?source ?doi ?solvent ?temperature ?temperatureUnit WHERE {
        ?chemical ?propp ?statement .
        ?statement a wikibase:BestRank ;
          ?proppsv [
            wikibase:quantityAmount ?value ;
            wikibase:quantityUnit ?units
          ] .
        OPTIONAL {
          ?statement prov:wasDerivedFrom/pr:P248 ?source .
          OPTIONAL { ?source wdt:P356 ?doi . }
        }
        ?property wikibase:claim ?propp ;
                wikibase:statementValue ?proppsv ;
                wdt:P1629 wd:Q170731 ;
                wdt:P31 wd:Q21077852 .
        OPTIONAL {
          ?statement pqv:P2076 ?temperatureNode .
          ?temperatureNode wikibase:quantityAmount ?temperature ;
                           wikibase:quantityUnit ?temperatureUnit .
        }
        OPTIONAL {
          wd:P2178 wikibase:qualifier ?qualifierS .
          ?qualifierS a owl:ObjectProperty .
          ?statement ?qualifierS ?solvent .
        }
      } GROUP BY ?chemical ?value ?units ?temperature ?temperatureUnit ?solvent ?source ?doi
    } AS %result
    WHERE {
      INCLUDE %result
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ASC(?propEntityLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Solubilities_of_chemicals
  title: Solubilities of chemicals
  description: ''
- name: Who discovered the most asteroids?
  namespace: examples
  domain: wikidata.org
  query_id: who-discovered-the-most-asteroids--examples@wikidata.org
  sparql: |2

    SELECT ?discoverer ?discovererLabel ?count
    WITH {
      SELECT ?discoverer (COUNT(?asteroid) AS ?count)
      WHERE {
        ?asteroid wdt:P31 wd:Q3863;
          wdt:P61 ?discoverer .
      }
      GROUP BY ?discoverer
      ORDER BY DESC(?count)
      LIMIT 20
    } AS %i
    WHERE {
      INCLUDE %i
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Who_discovered_the_most_asteroids?
  title: Who discovered the most asteroids?
  description: ''
- name: Who discovered the most planets? (with list)
  namespace: examples
  domain: wikidata.org
  query_id: who-discovered-the-most-planets-with-list--examples@wikidata.org
  sparql: |2

    SELECT
      ?discoverer ?discovererLabel
      (COUNT(DISTINCT ?planet) as ?count)
      (GROUP_CONCAT(DISTINCT(?planetLabel); separator=", ") as ?planets)
    WHERE
    {
      ?ppart wdt:P279* wd:Q634 .
      ?planet wdt:P31 ?ppart .
      ?planet wdt:P61 ?discoverer .
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
        ?discoverer rdfs:label ?discovererLabel .
        ?planet rdfs:label ?planetLabel
      }
    }
    GROUP BY ?discoverer ?discovererLabel
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Who_discovered_the_most_planets?_(with_list)
  title: Who discovered the most planets? (with list)
  description: ''
- name: List of space probes with pictures
  namespace: examples
  domain: wikidata.org
  query_id: list-of-space-probes-with-pictures--examples@wikidata.org
  sparql: |2


    #defaultView:ImageGrid
    SELECT ?spaceProbeLabel ?date ?picture
    WHERE
    {
      ?spaceProbe wdt:P31 wd:Q26529;
            wdt:P18 ?picture;
            wdt:P619 ?date . #mandatory
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "fr,en" .
      }
    }
    ORDER BY ?date
    LIMIT 88
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_space_probes_with_pictures
  title: List of space probes with pictures
  description: ''
- name: Birthplaces of astronauts
  namespace: examples
  domain: wikidata.org
  query_id: birthplaces-of-astronauts--examples@wikidata.org
  sparql: |2

    # ----
    # select all astronauts with name, image, birthdate, birthplace and coordinates of the birthplace

    SELECT ?astronaut ?astronautLabel ?image ?birthdate ?birthplace ?coord WHERE {
      ?astronaut ?x1 wd:Q11631;
      wdt:P18 ?image;
      wdt:P569 ?birthdate;
      wdt:P19 ?birthplace.

      ?birthplace wdt:P625 ?coord
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC(?birthdate)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Birthplaces_of_astronauts
  title: Birthplaces of astronauts
  description: ''
- name: (Artist) Images of Exoplanets
  namespace: examples
  domain: wikidata.org
  query_id: artist-images-of-exoplanets--examples@wikidata.org
  sparql: |2

    #defaultView:ImageGrid
    SELECT ?exoplanet ?exoplanetLabel ?image WHERE {
      ?exoplanet wdt:P31 wd:Q44559 ;
                 wdt:P18 ?image .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#(Artist)_Images_of_Exoplanets
  title: (Artist) Images of Exoplanets
  description: ''
- name: Number of scientists per gender
  namespace: examples
  domain: wikidata.org
  query_id: number-of-scientists-per-gender--examples@wikidata.org
  sparql: |2

    SELECT ?gender (count(DISTINCT ?human) as ?number)
    WHERE
    {
      ?human wdt:P31 wd:Q5 ;
             wdt:P21 ?gender ;
             wdt:P106/wdt:P279* wd:Q901 .
    }
    GROUP BY ?gender
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_scientists_per_gender
  title: Number of scientists per gender
  description: "* Items used: , \n* Properties used: , , ,"
- name: Most eponymous mathematicians
  namespace: examples
  domain: wikidata.org
  query_id: most-eponymous-mathematicians--examples@wikidata.org
  sparql: |2


    SELECT ?eponym ?eponymLabel ?count ?sample ?sampleLabel
    WHERE
    {
      {
      SELECT ?eponym (COUNT(?item) as ?count) (SAMPLE(?item) AS ?sample)
      WHERE
      {
        ?item wdt:P138 ?eponym.
        ?eponym wdt:P106 wd:Q170790.
      }
      GROUP BY ?eponym
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_eponymous_mathematicians
  title: Most eponymous mathematicians
  description: "* Items used: \n* Properties used:"
- name: Authors of scientific articles by occupation
  namespace: examples
  domain: wikidata.org
  query_id: authors-of-scientific-articles-by-occupation--examples@wikidata.org
  sparql: "\n# ----\n# NOTE - This scholarly query is written for use in WDQS-Main\
    \ UI only  (query.wikidata.org).\n\n#defaultView:BubbleChart\nSELECT ?occupationLabel\
    \ (count(DISTINCT ?author) as ?count)\nWHERE\n{\n hint:Query hint:optimizer \"\
    None\" .      \n SERVICE wdsubgraph:scholarly_articles {\n         SELECT ?author\
    \ \n           WHERE {\n              ?object wdt:P31 wd:Q13442814 ;\n       \
    \               wdt:P50 ?author .     }\n           LIMIT 1000 \n  }\n  ?author\
    \ wdt:P106 ?occupation .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language\
    \ \"en,fr\" }\n}\nGROUP BY ?occupationLabel\nORDER BY DESC(?count)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authors_of_scientific_articles_by_occupation
  title: Authors of scientific articles by occupation
  description: ''
- name: Authors of scientific articles who received a Nobel prize
  namespace: examples
  domain: wikidata.org
  query_id: authors-of-scientific-articles-who-received-a-nobel-prize--examples@wikidata.org
  sparql: "\n# ----\n# NOTE - This scholarly query is written for use in WDQS-Main\
    \ UI only  (query.wikidata.org).\n\n#added in 2016-10; updated 2025-08\n\n#Authors\
    \ of scientific articles who received a Nobel prize\nSELECT ?article ?articleLabel\
    \ ?person ?personLabel ?_image ?award ?awardLabel\nWHERE {\n  hint:Query hint:optimizer\
    \ \"None\" .      \n  ?award wdt:P279* wd:Q7191 . #award is a Nobel Prize\n  ?person\
    \ wdt:P166 ?award ; #person received an award\n          wdt:P31 wd:Q5 . #person\
    \ is instance of human\n  SERVICE wdsubgraph:scholarly_articles {\n    SELECT\
    \ ?article ?articleLabel ?person \n    WHERE  {\n    ?article wdt:P50 ?person\
    \ ; #person is an author of article\n        wdt:P31 wd:Q13442814 . #article is\
    \ a scientific article\n    SERVICE wikibase:label { bd:serviceParam wikibase:language\
    \ \"en\". }\n    }\n    LIMIT 100\n  }\n  SERVICE wikibase:label { bd:serviceParam\
    \ wikibase:language \"en\". }\n OPTIONAL { ?person wdt:P18 ?_image. } #Wikimedia\
    \ Commons has an image of person\n}\nLIMIT 100\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authors_of_scientific_articles_who_received_a_Nobel_prize
  title: Authors of scientific articles who received a Nobel prize
  description: ''
- name: Using VALUES for extracting scientific articles of specific authors
  namespace: examples
  domain: wikidata.org
  query_id: using-values-for-extracting-scientific-articles-of-specific-authors--examples@wikidata.org
  sparql: |2

    # ----
    # NOTE - This scholarly query is written for use in WDQS-Main UI only  (query.wikidata.org).

     SELECT ?entity ?entityLabel ?authorLabel WHERE {
      hint:Query hint:optimizer "None".
      SERVICE wdsubgraph:scholarly_articles {
        SELECT ?entity ?entityLabel ?author WHERE {
          VALUES ?author {wd:Q18016466} #initialize "?author with the Wikidata item "Lydia Pintscher"
          ?entity wdt:P31 wd:Q13442814. #filter by scientific articles
          ?entity wdt:P50 ?author.
          SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
        }
       }
     SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Using_VALUES_for_extracting_scientific_articles_of_specific_authors
  title: Using VALUES for extracting scientific articles of specific authors
  description: "* Items used:  \n* Properties used:"
- name: Female scientists with most number of sitelinks (but not English Wikipedia)
  namespace: examples
  domain: wikidata.org
  query_id: female-scientists-with-most-number-of-sitelinks-but-not-english-wikipedia--examples@wikidata.org
  sparql: |2

    # ----
    #Female scientists with most number of sitelinks (but not English Wikipedia)
    #PREFIX schema: <http://schema.org/>

    SELECT ?item ?itemLabel ?linkcount WHERE {
        ?item wdt:P31 wd:Q5 .
        ?item wdt:P21 wd:Q6581072 .
        ?item wdt:P106 wd:Q901 .
        ?item wikibase:sitelinks ?linkcount .
      FILTER (?linkcount >= 1) . # only include items with 1 or more sitelinks
      FILTER NOT EXISTS {
        ?article schema:about ?item .
        ?article schema:inLanguage "en" .
        ?article schema:isPartOf <https://en.wikipedia.org/>
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,es,ar,fr" }
    }
    GROUP BY ?item ?itemLabel ?linkcount
    ORDER BY DESC(?linkcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Female_scientists_with_most_number_of_sitelinks_(but_not_English_Wikipedia)
  title: Female scientists with most number of sitelinks (but not English Wikipedia)
  description: ''
- name: Inventors killed by their own invention
  namespace: examples
  domain: wikidata.org
  query_id: inventors-killed-by-their-own-invention--examples@wikidata.org
  sparql: |2

    SELECT ?inventor ?inventorLabel ?gadget ?gadgetLabel WHERE {
      ?inventor wdt:P157 ?gadget.
      ?gadget wdt:P61 ?inventor.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Inventors_killed_by_their_own_invention
  title: Inventors killed by their own invention
  description: ''
- name: Most cited female authors
  namespace: examples
  domain: wikidata.org
  query_id: most-cited-female-authors--examples@wikidata.org
  sparql: |-
    #added 2016-12
    ##defaultView:BubbleChart
    SELECT ?author ?authorLabel (COUNT(?publication) AS ?count)
    WHERE
    {
        ?item wdt:P2860 ?publication . #citations
        ?publication wdt:P50 ?author . #authors
        ?author wdt:P21 wd:Q6581072. #females
        SERVICE wikibase:label {
            bd:serviceParam wikibase:language "en" .
        }
    }
    GROUP BY ?author ?authorLabel
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_cited_female_authors
  title: Most cited female authors
  description: ''
- name: Scientists who have worked together but whose Erdos numbers don’t reflect
    that
  namespace: examples
  domain: wikidata.org
  query_id: scientists-who-have-worked-together-but-whose-erdos-numbers-don-t-reflect-that--examples@wikidata.org
  sparql: |2

     # ----
    # Finds authors who have published scientific articles together,
    # but whose Erdos numbers are more than one apart.
    # These would appear to violate the definition of the Erdos number.

    # note on 2025-08-10: This query currently does not work on WDQS due to the graph split, and federated versions of it lead to timeouts. A version of it can be run on Qlever (https://qlever.cs.uni-freiburg.de/wikidata/BzLc22) without LIMIT or timeouts.

    SELECT
      # Q#s
      ?paper
      ?author1
      ?author2
      # title (either from title statement or label)
      (IF(BOUND(?title), ?title, ?paperLabel) AS ?title)
      # author labels (should be names) and their Erdos numbers
      ?author1Label
      ?erdos1
      ?author2Label
      ?erdos2
      # distance between Erdos numbers
      ?distance
    WHERE
    {
      # paper, instance of or subclass of scientific article; also has two authors
      ?paper wdt:P31/wdt:P279* wd:Q13442814;
               wdt:P50 ?author1, ?author2.
      # if it has a title, we’ll grab that as well, but it’s also okay if there’s no title
      OPTIONAL { ?paper wdt:P1476 ?title. }
      # grab Erdos numbers of the two authors
      ?author1 wdt:P2021 ?erdos1.
      ?author2 wdt:P2021 ?erdos2.
      # introduce a new variable for the difference of the Erdos numbers
      BIND(?erdos2 - ?erdos1 AS ?distance).
      # select those cases where the distance is > 1
      # (note: by *not* taking the absolute value of the distance, we avoid getting duplicated cases because the author variables might be swapped)
      FILTER(?distance > 1).
      # get *Label variables automagically
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    # sort by distance first (descending), then by first author, then by second author
    ORDER BY DESC(?distance) ?author1Label ?author2Label
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Scientists_who_have_worked_together_but_whose_Erdos_numbers_don’t_reflect_that
  title: Scientists who have worked together but whose Erdos numbers don’t reflect
    that
  description: '*  As of August 2025, this query does not work on WDQS due to graph
    split, and federated versions of it lead to timeouts. A version of it can be [<tvar
    name="1">https://qlever.cs.uni-freiburg.de/wikidata/BzLc22</tvar> run on Qlever]
    without LIMIT or timeouts.'
- name: Map of institutions where Canadian citizens got their PhD
  namespace: examples
  domain: wikidata.org
  query_id: map-of-institutions-where-canadian-citizens-got-their-phd--examples@wikidata.org
  sparql: |2

    #defaultView:Map
    SELECT DISTINCT ?institution ?institutionLabel ?academics ?academicsLabel ?degree ?degreeLabel ?geoloc ?image WHERE {
      ?academics wdt:P31 wd:Q5 ; # instances (P31) of humans (Q5)
                 wdt:P27 wd:Q16 ; # country of citizenship
                 p:P69 ?statement . # check for an "educated at" (P69) statement
      OPTIONAL { ?academics wdt:P18 ?image }. #image

      ?statement ps:P69 ?institution . # get value of the "educated at" statement, i.e. the institution
      ?institution wdt:P625 ?geoloc . # get the geolocation of the institution

      ?statement pq:P512 ?degree . # get qualifier "academic degree" (P512)
      ?degree wdt:P31/wdt:P279* wd:Q849697 . # filter for doctoral degrees

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }

    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_institutions_where_Canadian_citizens_got_their_PhD
  title: Map of institutions where Canadian citizens got their PhD
  description: ''
- name: PMID-DOI mappings
  namespace: examples
  domain: wikidata.org
  query_id: pmid-doi-mappings--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?pmid ?doi
    WHERE
    {
      ?item wdt:P698 ?pmid ;
                  wdt:P356 ?doi .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#PMID-DOI_mappings
  title: PMID-DOI mappings
  description: ''
- name: The number of statements by DOI
  namespace: examples
  domain: wikidata.org
  query_id: the-number-of-statements-by-doi--examples@wikidata.org
  sparql: |
    SELECT ?doi (COUNT (?entry) as ?entries)
    {
      ?entry ?p ?statement .
      ?statement prov:wasDerivedFrom/pr:P248/wdt:P356 ?doi .
    }
    GROUP BY ?doi
    ORDER BY DESC(?entries)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_number_of_statements_by_DOI
  title: The number of statements by DOI
  description: ''
- name: number of statements backed by a reference with a DOI
  namespace: examples
  domain: wikidata.org
  query_id: number-of-statements-backed-by-a-reference-with-a-doi--examples@wikidata.org
  sparql: |2


    SELECT (COUNT (?statement) as ?statements)
    WHERE
    {
      ?entry ?p ?statement .
      ?statement prov:wasDerivedFrom/
           <http://www.wikidata.org/prop/reference/P248>/
           wdt:P356 ?doi .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#number_of_statements_backed_by_a_reference_with_a_DOI
  title: number of statements backed by a reference with a DOI
  description: ''
- name: Statements originating from a specific DOI
  namespace: examples
  domain: wikidata.org
  query_id: statements-originating-from-a-specific-doi--examples@wikidata.org
  sparql: |2


    SELECT ?entryRes ?entry ?statement
    WHERE
    {
      ?entryRes ?p ?statement ;
        rdfs:label ?entry .
      ?statement prov:wasDerivedFrom/
           <http://www.wikidata.org/prop/reference/P248>/
           wdt:P356 "10.1021/JA01577A030" .
       FILTER(lang(?entry) = "en")
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Statements_originating_from_a_specific_DOI
  title: Statements originating from a specific DOI
  description: ''
- name: Translations of the Disease Ontology term DOID:399 (Tuberculosis)
  namespace: examples
  domain: wikidata.org
  query_id: translations-of-the-disease-ontology-term-doid-399-tuberculosis--examples@wikidata.org
  sparql: |
    SELECT ?English ?language ?label WHERE {
      ?disease wdt:P699 "DOID:399";
                 rdfs:label ?English;
                 rdfs:label ?label .
      BIND(LANG(?label) as ?languageCode)
      ?wdLanguage wdt:P424 ?languageCode;
                  rdfs:label ?language .
        FILTER EXISTS {?wdLanguage wdt:P31?/wdt:P279+ wd:Q17376908}
      FILTER (LANG(?English)="en")
      FILTER (LANG(?language)="en")
    } ORDER BY ?language
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Translations_of_the_Disease_Ontology_term_DOID:399_(Tuberculosis)
  title: Translations of the Disease Ontology term DOID:399 (Tuberculosis)
  description: '* Properties used:'
- name: Library and Information Science journals
  namespace: examples
  domain: wikidata.org
  query_id: library-and-information-science-journals--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?journal ?name WHERE {
        ?journal wdt:P31 wd:Q5633421 . # is scientific journal
        {
                { ?journal wdt:P921 wd:Q199655 }   # with topic library science
          UNION { ?journal wdt:P921 wd:Q16387 }    # and/or topic information science
          UNION { ?journal wdt:P921 wd:Q13420675 } # and/or topic library and information science
        }
        SERVICE wikibase:label {
            bd:serviceParam wikibase:language "en" .
            ?journal rdfs:label ?name .
        }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Library_and_Information_Science_journals
  title: Library and Information Science journals
  description: "* items used:     \n* Properties used:"
- name: Most popular subjects of scientific articles
  namespace: examples
  domain: wikidata.org
  query_id: most-popular-subjects-of-scientific-articles--examples@wikidata.org
  sparql: |2

    # ----
    # NOTE - This scholarly query is written for use in WDQS-Main UI only  (query.wikidata.org).

    #title:<translate><!--T:780--> Most popular subjects of scientific articles</translate>
    SELECT (COUNT(?work) AS ?count) ?subject ?subjectLabel WHERE {
      hint:Query hint:optimizer "None".
      SERVICE wdsubgraph:scholarly_articles {
        SELECT ?work ?subject WHERE {
          ?work wdt:P31 wd:Q13442814;
            wdt:P921 ?subject.
        }
        LIMIT 10000
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    GROUP BY ?subject ?subjectLabel
    ORDER BY DESC (?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_popular_subjects_of_scientific_articles
  title: Most popular subjects of scientific articles
  description: ''
- name: Galaxies ordered by the ones that are most linked from scientific articles
  namespace: examples
  domain: wikidata.org
  query_id: galaxies-ordered-by-the-ones-that-are-most-linked-from-scientific-articles--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:781--> Galaxies ordered by the ones that are most linked from scientific articles</translate>
    #author: So9q
    #date:2021-10-27
    #note:I deliberately choose a smaller subgraph (galaxies) to prevent a timeout on WDQS
    SELECT ?main_subject ?main_subjectLabel (count(?item) as ?c)
    WHERE
    {
      ?item wdt:P31 wd:Q13442814;
            wdt:P921 ?main_subject.
      ?main_subject wdt:P31/wdt:P279* wd:Q318.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?main_subject ?main_subjectLabel
    ORDER BY DESC(?c)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Galaxies_ordered_by_the_ones_that_are_most_linked_from_scientific_articles
  title: Galaxies ordered by the ones that are most linked from scientific articles
  description: ''
- name: Scientific journals with editors on Twitter
  namespace: examples
  domain: wikidata.org
  query_id: scientific-journals-with-editors-on-twitter--examples@wikidata.org
  sparql: |2-

    SELECT ?journal ?journalLabel ?editor ?editorLabel ?twitter ?ex_publisher ?ex_publisherLabel
    WITH {
      SELECT ?journal ?editor ?twitter (SAMPLE(?publisher) AS ?ex_publisher) WHERE {
        ?journal wdt:P31 wd:Q5633421 ; wdt:P98 ?editor .
        OPTIONAL { ?journal wdt:P123 ?publisher }
        ?editor wdt:P2002 ?twitter .
      } GROUP BY ?journal ?editor ?twitter
    } AS %result
    WHERE {
      INCLUDE %result
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY ?journalLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Scientific_journals_with_editors_on_Twitter
  title: Scientific journals with editors on Twitter
  description: ''
- name: Mathematical proofs
  namespace: examples
  domain: wikidata.org
  query_id: mathematical-proofs--examples@wikidata.org
  sparql: |2


    SELECT ?proof ?proofLabel
    WHERE
    {
      ?proof wdt:P31 wd:Q11538.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mathematical_proofs
  title: Mathematical proofs
  description: ''
- name: Timeline of death of mathematicans and their theorems
  namespace: examples
  domain: wikidata.org
  query_id: timeline-of-death-of-mathematicans-and-their-theorems--examples@wikidata.org
  sparql: |2

    #defaultView:Timeline
    SELECT
      ?genderLabel
      ?theorem
      ?theoremLabel
      ?nameLabel
      ?death
      ?formula
    WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,de,pl". }
      ?theorem wdt:P31 wd:Q65943;
               wdt:P138 ?name.
      ?name    wdt:P570 ?death;
               wdt:P21 ?gender .
      # OPTIONAL{
      ?theorem wdt:P2534 ?formula.
      # }
    }
    ORDER BY DESC(?death)
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Timeline_of_death_of_mathematicans_and_their_theorems
  title: Timeline of death of mathematicans and their theorems
  description: ''
- name: Databases listed in Wikidata and if available applicable licenses
  namespace: examples
  domain: wikidata.org
  query_id: databases-listed-in-wikidata-and-if-available-applicable-licenses--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT *
    WHERE
    {
      ?item wdt:P31 wd:Q8513 ;
        rdfs:label ?name .
      OPTIONAL { ?item wdt:P275 ?licenseItem .
          ?licenseItem rdfs:label ?license .
          FILTER (LANG(?license) = "en")}
      FILTER (LANG(?name) = "en")
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Databases_listed_in_Wikidata_and_if_available_applicable_licenses
  title: Databases listed in Wikidata and if available applicable licenses
  description: ''
- name: Fictional universes with most fictional planets
  namespace: examples
  domain: wikidata.org
  query_id: fictional-universes-with-most-fictional-planets--examples@wikidata.org
  sparql: |2


    SELECT ?universe (SAMPLE(?label) AS ?label) (COUNT(?planet) AS ?count)
    WHERE
    {
      ?planet wdt:P31 wd:Q2775969;
              wdt:P1080 ?universe.
      ?universe rdfs:label ?label.
      FILTER(LANG(?label) = "en").
    }
    GROUP BY ?universe
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Fictional_universes_with_most_fictional_planets
  title: Fictional universes with most fictional planets
  description: ''
- name: Objects with most mass
  namespace: examples
  domain: wikidata.org
  query_id: objects-with-most-mass--examples@wikidata.org
  sparql: |2

    SELECT ?object ?objectLabel ?mass WHERE {
      {
        SELECT ?object (MAX(?mass) AS ?mass) WHERE {
          ?object p:P2067/psn:P2067/wikibase:quantityAmount ?mass.
          MINUS { ?object wdt:P31 wd:Q3647172. }
        }
        GROUP BY ?object
        ORDER BY DESC(?mass)
        LIMIT 100
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC(?mass)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Objects_with_most_mass
  title: Objects with most mass
  description: ''
- name: Biological databases listed in Wikidata and, if available, applicable licenses
  namespace: examples
  domain: wikidata.org
  query_id: biological-databases-listed-in-wikidata-and-if-available-applicable-licenses--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?url ?licence ?licenceLabel
    WHERE {
      ?item wdt:P31 wd:Q4117139.
      OPTIONAL { ?item wdt:P856 ?url }
      OPTIONAL { ?item wdt:P275 ?licence }
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    } ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Biological_databases_listed_in_Wikidata_and,_if_available,_applicable_licenses
  title: Biological databases listed in Wikidata and, if available, applicable licenses
  description: ''
- name: American universities founded before the states they reside in were created
  namespace: examples
  domain: wikidata.org
  query_id: american-universities-founded-before-the-states-they-reside-in-were-created--examples@wikidata.org
  sparql: |
    SELECT ?uLabel ?founded ?stateLabel ?stateStart
    WHERE {
      ?u wdt:P31/wdt:P279* wd:Q3918 ;
           wdt:P131+ ?state ;
           wdt:P571 ?founded .
      ?state wdt:P31 wd:Q35657 ;
               wdt:P571 ?stateStart .
      FILTER (?founded < ?stateStart) .
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
      }
    }
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#American_universities_founded_before_the_states_they_reside_in_were_created
  title: American universities founded before the states they reside in were created
  description: "* Items used: . \n* Properties used: , , , ,"
- name: Universities ranked by PageRank on English Wikipedia (federated query)
  namespace: examples
  domain: wikidata.org
  query_id: universities-ranked-by-pagerank-on-english-wikipedia-federated-query--examples@wikidata.org
  sparql: |
    PREFIX vrank:<http://purl.org/voc/vrank#>

    SELECT DISTINCT ?uni ?uniLabel ?pr WHERE {
      ?uni wdt:P31/wdt:P279* wd:Q3918.
      SERVICE <http://dbpedia.org/sparql> {
        ?uni vrank:hasRank/vrank:rankValue ?pr
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
      }
    } ORDER BY DESC(?pr) LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Universities_ranked_by_PageRank_on_English_Wikipedia_(federated_query)
  title: Universities ranked by PageRank on English Wikipedia (federated query)
  description: "*  Items used: \n*  Properties used: ,"
- name: Education
  namespace: examples
  domain: wikidata.org
  query_id: education--examples@wikidata.org
  sparql: "\n# ----\n#ranking(s) of university(es)\n#defaultView:LineChart\nSELECT\
    \ ?year ?ranking ?universityLabel ?deternimationMethodLabel ?university WHERE\
    \ {\n  VALUES ?university {\n    wd:Q832355\n    wd:Q1108197\n    wd:Q16952\n\
    \    wd:Q16955\n    \n    \n# <translate><!--T:883--> to add another university\
    \ :</translate>\n    # <translate><!--T:890--> 1,uncomment the last commented\
    \ line below,</translate>\n    # <translate><!--T:891--> 2,optionnally replace\
    \ « Zhejiang_University » by another university name,</translate>\n    # <translate><!--T:892-->\
    \ 3,put cursor to the end of line,</translate>\n    # <translate><!--T:893-->\
    \ 4,and press CTRL+SPACE，or CTRL+ALT+SPACE, or ALT+ENTER,</translate>\n    # <translate><!--T:894-->\
    \ 5,and select first item, and press CTRL+ENTER to execute</translate>\n    #wd:Zhejiang_University\n\
    \    \n    }\n  \n  VALUES ?deternimationMethod {\n    wd:Q478743\n    \n    \n\
    # <translate><!--T:884--> to add another deternimationMethod :</translate>\n \
    \   # <translate><!--T:895--> 1,uncomment the last commented line below,</translate>\n\
    \    # <translate><!--T:896--> 2,optionnally replace the « Shanghai .. » method\
    \ by another one,</translate>\n    # <translate><!--T:897--> 3,put cursor to the\
    \ end of line,</translate>\n    # <translate><!--T:898--> 4,and press CTRL+SPACE，or\
    \ CTRL+ALT+SPACE, or ALT+ENTER,</translate>\n    # <translate><!--T:899--> 5,and\
    \ select first item, and press CTRL+ENTER to execute</translate>\n    #wd:Shanghai_Academic_Ranking_of_World_Universities\n\
    \    \n    }\n                              \n  \n  ?university p:P1352 ?statement.\n\
    \  ?statement ps:P1352 ?ranking.\n  ?statement pq:P459 ?deternimationMethod.\n\
    \  ?statement pq:P585 ?date.\n  \n  BIND (STR(YEAR(?date)) as ?year)\n  SERVICE\
    \ wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"\
    . }\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Education
  title: Education
  description: ''
- name: Ranking(s) of university(es)
  namespace: examples
  domain: wikidata.org
  query_id: ranking-s-of-university-es--examples@wikidata.org
  sparql: "\n# ----\n#ranking(s) of university(es)\n#defaultView:LineChart\nSELECT\
    \ ?year ?ranking ?universityLabel ?deternimationMethodLabel ?university WHERE\
    \ {\n  VALUES ?university {\n    wd:Q832355\n    wd:Q1108197\n    wd:Q16952\n\
    \    wd:Q16955\n    \n    \n# <translate><!--T:883--> to add another university\
    \ :</translate>\n    # <translate><!--T:890--> 1,uncomment the last commented\
    \ line below,</translate>\n    # <translate><!--T:891--> 2,optionnally replace\
    \ « Zhejiang_University » by another university name,</translate>\n    # <translate><!--T:892-->\
    \ 3,put cursor to the end of line,</translate>\n    # <translate><!--T:893-->\
    \ 4,and press CTRL+SPACE，or CTRL+ALT+SPACE, or ALT+ENTER,</translate>\n    # <translate><!--T:894-->\
    \ 5,and select first item, and press CTRL+ENTER to execute</translate>\n    #wd:Zhejiang_University\n\
    \    \n    }\n  \n  VALUES ?deternimationMethod {\n    wd:Q478743\n    \n    \n\
    # <translate><!--T:884--> to add another deternimationMethod :</translate>\n \
    \   # <translate><!--T:895--> 1,uncomment the last commented line below,</translate>\n\
    \    # <translate><!--T:896--> 2,optionnally replace the « Shanghai .. » method\
    \ by another one,</translate>\n    # <translate><!--T:897--> 3,put cursor to the\
    \ end of line,</translate>\n    # <translate><!--T:898--> 4,and press CTRL+SPACE，or\
    \ CTRL+ALT+SPACE, or ALT+ENTER,</translate>\n    # <translate><!--T:899--> 5,and\
    \ select first item, and press CTRL+ENTER to execute</translate>\n    #wd:Shanghai_Academic_Ranking_of_World_Universities\n\
    \    \n    }\n                              \n  \n  ?university p:P1352 ?statement.\n\
    \  ?statement ps:P1352 ?ranking.\n  ?statement pq:P459 ?deternimationMethod.\n\
    \  ?statement pq:P585 ?date.\n  \n  BIND (STR(YEAR(?date)) as ?year)\n  SERVICE\
    \ wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"\
    . }\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Ranking(s)_of_university(es)
  title: Ranking(s) of university(es)
  description: ''
- name: List of presidents with causes of death
  namespace: examples
  domain: wikidata.org
  query_id: list-of-presidents-with-causes-of-death--examples@wikidata.org
  sparql: |
    SELECT ?h ?hLabel ?cause ?causeLabel (YEAR(?date) AS ?year) WHERE {
    ?h wdt:P39 wd:Q11696;
       wdt:P509 ?cause;
       wdt:P570 ?date
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY ?year
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_presidents_with_causes_of_death
  title: List of presidents with causes of death
  description: "*  Items used: \n*  Properties used: ,"
- name: Presidents and their causes of death ranking
  namespace: examples
  domain: wikidata.org
  query_id: presidents-and-their-causes-of-death-ranking--examples@wikidata.org
  sparql: |2


    #defaultView:BubbleChart
    SELECT ?cid ?cause (count(*) as ?count)
    WHERE
    {
      ?pid wdt:P39 wd:Q11696 .
      ?pid wdt:P509 ?cid .
      OPTIONAL {
        ?cid rdfs:label ?cause FILTER (lang(?cause) = "en") .
      }
    }
    GROUP BY ?cid ?cause
    ORDER BY DESC(?count) ASC(?cause)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Presidents_and_their_causes_of_death_ranking
  title: Presidents and their causes of death ranking
  description: ''
- name: Politicians who died of cancer (of any type)
  namespace: examples
  domain: wikidata.org
  query_id: politicians-who-died-of-cancer-of-any-type--examples@wikidata.org
  sparql: |2

    SELECT ?politician ?cause ?politician_label ?cause_of_death_label
    WHERE
    {
      ?politician wdt:P106 wd:Q82955 .    # find items that have "occupation (P106): politician (Q82955)"
      ?politician wdt:P509 ?cause .       # with a P509 (cause of death) claim
      ?cause wdt:P279* wd:Q12078 .        # ... where the cause is a subclass of (P279*) cancer (Q12078)
      # ?politician wdt:P39 wd:Q11696 .   # Uncomment this line to include only U.S. Presidents

      OPTIONAL {?politician rdfs:label ?politician_label FILTER (lang(?politician_label) = "en") .}
      OPTIONAL {?cause rdfs:label ?cause_of_death_label FILTER (lang(?cause_of_death_label) = "en").}
    }
    ORDER BY ASC (?politician)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Politicians_who_died_of_cancer_(of_any_type)
  title: Politicians who died of cancer (of any type)
  description: "* Items used: , \n* Properties used: , ,"
- name: List of popes
  namespace: examples
  domain: wikidata.org
  query_id: list-of-popes--examples@wikidata.org
  sparql: |2


    SELECT ?link ?linkLabel ?picture ?age
    WHERE
    {
      ?link wdt:P31 wd:Q5 ;
              p:P39 [ ps:P39 wd:Q19546 ; pq:P580 ?startTime ] .
      OPTIONAL { ?link wdt:P569 ?dateOfBirth }
      OPTIONAL { ?link wdt:P18 ?picture }
      OPTIONAL { ?link wdt:P570 ?dateOfDeath }
      BIND(YEAR(?dateOfDeath) - YEAR(?dateOfBirth) as ?age)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr,en" }
    }
    ORDER BY DESC(?startTime)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_popes
  title: List of popes
  description: "*  Items used:  \n*  Properties used:"
- name: Years with 3 popes
  namespace: examples
  domain: wikidata.org
  query_id: years-with-3-popes--examples@wikidata.org
  sparql: |2


    SELECT ?year ?pope1Label ?pope2Label ?pope3Label
    WHERE
    {
      ?pope2 p:P39 [
               ps:P39 wd:Q19546;
               pq:P580 ?p2s;
               pq:P582 ?p2e;
               pq:P1365 ?pope1;
               pq:P1366 ?pope3
             ].
      ?pope1 p:P39 [
               ps:P39 wd:Q19546;
               pq:P582 ?p1e
             ].
      ?pope3 p:P39 [
               ps:P39 wd:Q19546;
               pq:P580 ?p3s
             ].
      BIND(YEAR(?p2s) AS ?year).
      FILTER(YEAR(?p2e) = ?year && YEAR(?p1e) = ?year && YEAR(?p3s) = ?year).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ?year
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Years_with_3_popes
  title: Years with 3 popes
  description: "*  Items used: \n*  Properties used:"
- name: Popes with children
  namespace: examples
  domain: wikidata.org
  query_id: popes-with-children--examples@wikidata.org
  sparql: |2


    # All popes with number of children
    SELECT (SAMPLE(?father) as ?father) ?fatherLabel (SAMPLE(?picture) as ?picture) (COUNT(?father) as ?children)
    WHERE
    {
      ?subj wdt:P22 ?father .
        ?father wdt:P31 wd:Q5 .
        ?father wdt:P39 wd:Q19546 .
      OPTIONAL {
        ?father wdt:P18 ?picture .
        }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    GROUP BY ?fatherLabel
    ORDER BY DESC(?children)
    LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Popes_with_children
  title: Popes with children
  description: "*  Items used:  \n*  Properties used:"
- name: French heads of government by length of service
  namespace: examples
  domain: wikidata.org
  query_id: french-heads-of-government-by-length-of-service--examples@wikidata.org
  sparql: |-
    SELECT DISTINCT ?item ?itemLabel ?positionLabel ?picture ?start ?end ?days WHERE
    {
      ?item wdt:P31 wd:Q5 ;
            p:P39 ?position_statement .
      ?position_statement ps:P39 ?position ;
                          pq:P580 ?start FILTER (?start >= "1815-01-01T00:00:00Z"^^xsd:dateTime) .
      ?position wdt:P31
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#French_heads_of_government_by_length_of_service
  title: French heads of government by length of service
  description: "*  Items used:  \n*  Properties used:"
- name: List of countries in 1754
  namespace: examples
  domain: wikidata.org
  query_id: list-of-countries-in-1754--examples@wikidata.org
  sparql: |
    #updated 2020-12-06
    SELECT DISTINCT ?h ?hLabel ?inception ?dissolved ?coor
    WHERE
    {
      VALUES ?countryclass { wd:Q3024240 wd:Q6256 wd:Q3624078 }
      ?h p:P31/ps:P31 ?countryclass .
      ?h wdt:P571 ?inception .
      OPTIONAL { ?h wdt:P576 ?dissolved } .
      FILTER (?inception < "1755-01-01T00:00:00Z"^^xsd:dateTime)
      FILTER (?dissolved >= "1755-01-01T00:00:00Z"^^xsd:dateTime {{!}}{{!}} !Bound(?dissolved) )
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
      OPTIONAL { ?h wdt:P625 ?coor } .
    }
    ORDER BY ?inception
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_countries_in_1754
  title: List of countries in 1754
  description: |-
    *  Items used:  ( country),  ( former country),  ( sovereign state)
    *  Properties used: , ,
- name: Population in Europe after 1960
  namespace: examples
  domain: wikidata.org
  query_id: population-in-europe-after-1960--examples@wikidata.org
  sparql: |2


    SELECT ?objectLabel (YEAR(?date) as ?year)
           ?population (?objectLabel as ?Location)
    WHERE
    {
            wd:Q458 wdt:P150 ?object . # European Union contains administrative territorial entity
            ?object p:P1082 ?populationStatement .
            ?populationStatement ps:P1082 ?population ;
                                 pq:P585 ?date .
            SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
      FILTER (YEAR(?date) >= 1960)
    }
    ORDER BY ?objectLabel ?year
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Population_in_Europe_after_1960
  title: Population in Europe after 1960
  description: "*  Items used: \n*  Properties used:"
- name: Locations of air accidents
  namespace: examples
  domain: wikidata.org
  query_id: locations-of-air-accidents--examples@wikidata.org
  sparql: |2

     SELECT ?label ?coord ?place
    WHERE
    {
       ?subj wdt:P31 wd:Q744913 .
       ?subj wdt:P625 ?coord .
       ?subj rdfs:label ?label FILTER (lang(?label) = "en")
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Locations_of_air_accidents
  title: Locations of air accidents
  description: "*  Items used: \n*  Properties used:"
- name: Most prolific fathers
  namespace: examples
  domain: wikidata.org
  query_id: most-prolific-fathers--examples@wikidata.org
  sparql: |2

    SELECT ?father ?fatherLabel (SAMPLE(?picture_) AS ?picture) ?children
    WITH
    {
      SELECT ?father (COUNT(?father) AS ?children)
      WHERE
      {
        ?subj wdt:P22 ?father .
      }
      GROUP BY ?father
      ORDER BY DESC(?children)
      LIMIT 50
    }
    AS %get_fathers
    WHERE
    {
      INCLUDE %get_fathers
      OPTIONAL { ?father wdt:P18 ?picture_ . }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    GROUP BY ?father ?fatherLabel ?children
    ORDER BY DESC(?children)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_prolific_fathers
  title: Most prolific fathers
  description: '*  Properties used:'
- name: List of suicide attacks
  namespace: examples
  domain: wikidata.org
  query_id: list-of-suicide-attacks--examples@wikidata.org
  sparql: |2


    SELECT ?h ?hLabel ?location (CONCAT("injured: ",str(?injured)) as ?injuredl) (concat("dead: ",str(?dead)) as ?deadl) ?date ?image
    WHERE
    {
            ?h wdt:P31 ?attack.
        values (?attack) {
          (wd:Q18493502)
          (wd:Q217327)

        }
        OPTIONAL { ?h wdt:P1339 ?injured . }
        OPTIONAL { ?h wdt:P1120 ?dead. }
        OPTIONAL { ?h wdt:P276?/wdt:P625 ?location }
        OPTIONAL { ?h wdt:P585 ?date }
        OPTIONAL { ?h wdt:P18 ?image }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_suicide_attacks
  title: List of suicide attacks
  description: ''
- name: People who died by burning - on a timeline
  namespace: examples
  domain: wikidata.org
  query_id: people-who-died-by-burning-on-a-timeline--examples@wikidata.org
  sparql: |2


    #defaultView:Timeline
    SELECT ?person ?personLabel ?date
    WHERE
    {
      ?person wdt:P31 wd:Q5;
              wdt:P509 wd:Q468455;
              wdt:P570 ?date.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ?date
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_who_died_by_burning_-_on_a_timeline
  title: People who died by burning - on a timeline
  description: ''
- name: People who lived in the same period as another person
  namespace: examples
  domain: wikidata.org
  query_id: people-who-lived-in-the-same-period-as-another-person--examples@wikidata.org
  sparql: |2


    SELECT ?person ?personLabel ?personDescription ?birth ?death ?age
    {
      ?person wdt:P31 wd:Q5. # instance of human
      ?person wdt:P569 ?birth . # birth date
      ?person wdt:P570 ?death . # death date
      hint:Prior hint:rangeSafe true . # tell the optimizer that fields doesn’t mix dates, strings, integers or other data types, which simplifies the range comparison
      FILTER (?birth > "1452-04-15"^^xsd:dateTime && ?death < "1519-05-02"^^xsd:dateTime) # And between these two dates
      bind( year(?death)-year(?birth) as ?age ) # Make a new variable called ?age that we can use
      FILTER (?age > 10 && ?age < 100) # Only find people with realistic ages
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". } # used to display a label
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_who_lived_in_the_same_period_as_another_person
  title: People who lived in the same period as another person
  description: ''
- name: People elevated in the public domain in 2020 "life+50 years"
  namespace: examples
  domain: wikidata.org
  query_id: people-elevated-in-the-public-domain-in-2020-life-50-years--examples@wikidata.org
  sparql: |2

    # ----
    #added before 2019-02

    #Shows people raised in the public domain "life + 50 years".
    SELECT ?item ?itemLabel ?genderLabel (GROUP_CONCAT(DISTINCT ?occupationLabel; SEPARATOR=", ") AS ?occupations) (GROUP_CONCAT(DISTINCT ?countryLabel; SEPARATOR=", ") AS ?countries) ?death ?articles {
      VALUES ?target_country { wd:Q16 wd:Q142 wd:Q39 wd:Q31 wd:Q30 } . #countries: Canada, France, Switzerland, Belgium, USA. Removing this line to get worldwide may cause a query timeout.
      VALUES ?occ { wd:Q2500638 wd:Q20826540 wd:Q215627 } . #occupation: creator, erudite, person. These 3 occupations will also look for subclasses. Example: Alan Turing is a cryptographer, a subclass of cryptologist, a subclass of mathematician, a subclass of scientist, a subclass of erudite.
       ?item wdt:P31 wd:Q5;
                   wdt:P21 ?gender;
                   wdt:P27 ?target_country;
                   wdt:P27 ?country;
                   wdt:P106/wdt:P279* ?occ ;
                   wdt:P106 ?occupation;
                   wikibase:sitelinks ?articles . #Service to count the number of articles in Wikipedia language versions. The higher the number, the greater the chances that the person is very notorious.
       ?item wdt:P570 ?death . hint:Prior hint:rangeSafe true .
       FILTER( ?death >= "1969-01-01T00:00:00"^^xsd:dateTime && ?death < "1970-01-01T00:00:00"^^xsd:dateTime ) #death: public domain "life+50 years". Change both years to get a list in different legislation. Example for USA: life+70 years
       SERVICE wikibase:label {
           bd:serviceParam wikibase:language "fr,en" . #Service to retrieve the labels of items, in order of language. Example: if the label does not exist in French, the service will take the English label
           ?item rdfs:label ?itemLabel .
           ?gender rdfs:label ?genderLabel .
           ?occupation rdfs:label ?occupationLabel .
           ?country rdfs:label ?countryLabel .
       } .
    } GROUP BY ?item ?itemLabel ?genderLabel ?death ?articles ORDER BY DESC (?articles) #Order by the number of articles in Wikipedia language versions. The most notorious people will be at the top of the list.
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_elevated_in_the_public_domain_in_2020_"life+50_years"
  title: People elevated in the public domain in 2020 "life+50 years"
  description: ''
- name: List of torture devices
  namespace: examples
  domain: wikidata.org
  query_id: list-of-torture-devices--examples@wikidata.org
  sparql: |2

     SELECT ?thing ?thingLabel ?image
    WHERE
    {
      ?thing wdt:P366 wd:Q132781 .
      ?thing wdt:P18 ?image .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_torture_devices
  title: List of torture devices
  description: ''
- name: Animals that were executed
  namespace: examples
  domain: wikidata.org
  query_id: animals-that-were-executed--examples@wikidata.org
  sparql: |2


    SELECT ?animal ?animalLabel ?died ?mannerOfDeathLabel ?image
    WHERE
    {
      ?animal wdt:P31/wdt:P31 wd:Q16521; # instance of some taxon (does not include human)
              wdt:P509 ?mannerOfDeath.
      ?mannerOfDeath wdt:P279* wd:Q8454. # some subclass of capital punishment
      OPTIONAL { ?animal wdt:P570 ?died. }
      OPTIONAL { ?animal wdt:P18 ?image. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY ?died
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Animals_that_were_executed
  title: Animals that were executed
  description: ''
- name: People who were stateless for some time
  namespace: examples
  domain: wikidata.org
  query_id: people-who-were-stateless-for-some-time--examples@wikidata.org
  sparql: |2

    # ----
    # persons who were stateless (country of citizenship: no value) for some time (start time and end time qualifiers)
    SELECT ?person ?personLabel ?start ?end WHERE {
      ?person wdt:P31 wd:Q5;
              p:P27 [
                rdf:type wdno:P27;
                pq:P580 ?start;
                pq:P582 ?end
              ].
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY DESC(?end - ?start)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_who_were_stateless_for_some_time
  title: People who were stateless for some time
  description: ''
- name: Poets who were through An Lushan Rebellion
  namespace: examples
  domain: wikidata.org
  query_id: poets-who-were-through-an-lushan-rebellion--examples@wikidata.org
  sparql: |2

    # ----
    # Poets who were through An Lushan Rebellion
    SELECT ?poet ?poetLabel WHERE {
     SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],zh-hant,zh". }
     wd:Q253774 wdt:P580 ?battleStartTime.
     wd:Q253774 wdt:P582 ?battleEndTime.
     ?poet wdt:P106 wd:Q49757.
     ?poet wdt:P497 ?cbdbId.
     ?poet wdt:P569 ?birthDate.
      FILTER(?birthDate < ?battleStartTime).
      ?poet wdt:P570 ?deathDate.
      FILTER(?deathDate > ?battleEndTime).
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Poets_who_were_through_An_Lushan_Rebellion
  title: Poets who were through An Lushan Rebellion
  description: ''
- name: Periods of Japanese history and what they were named after
  namespace: examples
  domain: wikidata.org
  query_id: periods-of-japanese-history-and-what-they-were-named-after--examples@wikidata.org
  sparql: |
    SELECT ?era ?eraLabel (YEAR(?start_time) AS ?start) (YEAR(?end_time) AS ?end) ?namedLabel ?namedDescription WHERE {
    ?era wdt:P31 wd:Q11514315; wdt:P361 wd:Q130436; # eras of the history of Japan
      wdt:P580 ?start_time.
    MINUS { ?era wdt:P2348/wdt:P361 wd:Q130436 } # exclude sub-eras
    OPTIONAL { ?era wdt:P582 ?end_time } # optional end-time to make sure we include the current era
    OPTIONAL { ?era wdt:P138 ?named }
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    } ORDER BY ?start DESC(?end)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Periods_of_Japanese_history_and_what_they_were_named_after
  title: Periods of Japanese history and what they were named after
  description: ''
- name: Ancestors of Willem-Alexander of the Netherlands
  namespace: examples
  domain: wikidata.org
  query_id: ancestors-of-willem-alexander-of-the-netherlands--examples@wikidata.org
  sparql: |-
    SELECT DISTINCT ?item ?itemLabel ?dateofbirth WHERE {
      wd:Q154952 (wdt:P22
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Ancestors_of_Willem-Alexander_of_the_Netherlands
  title: Ancestors of Willem-Alexander of the Netherlands
  description: ''
- name: All events that occured on 2001/09/11
  namespace: examples
  domain: wikidata.org
  query_id: all-events-that-occured-on-2001-09-11--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel
    WHERE
    {
      ?item p:P585/ps:P585 "2001-09-11T00:00:00Z"^^xsd:dateTime
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_events_that_occured_on_2001/09/11
  title: All events that occured on 2001/09/11
  description: ''
- name: Everything with a time property on a given date
  namespace: examples
  domain: wikidata.org
  query_id: everything-with-a-time-property-on-a-given-date--examples@wikidata.org
  sparql: |
    SELECT (GROUP_CONCAT(?classLabel; SEPARATOR = "; ") AS ?classes) ?propLabel ?i ?iLabel ?iDescription WHERE {
      {
        SELECT ?i ?prop ?class WHERE {
          {
            SELECT ?prop ?p WHERE {
              ?prop wikibase:directClaim ?p;
                (wdt:P31/(wdt:P279*)) wd:Q18636219.
            }
          }
          ?i ?p "2001-5-11"^^xsd:dateTime.
          OPTIONAL { ?i wdt:P31 ?class }
        }
        LIMIT 1000
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
        ?class rdfs:label ?classLabel.
        ?prop rdfs:label ?propLabel.
        ?i rdfs:label ?iLabel;
          schema:description ?iDescription.
      }
    }
    GROUP BY ?propLabel ?i ?iLabel ?iDescription
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Everything_with_a_time_property_on_a_given_date
  title: Everything with a time property on a given date
  description: ''
- name: Cathedrals in Paris
  namespace: examples
  domain: wikidata.org
  query_id: cathedrals-in-paris--examples@wikidata.org
  sparql: |2


    SELECT ?item ?itemLabel ?placeLabel ?coords ?image
    WHERE
    {
      ?item wdt:P31 wd:Q2977 .
      ?item wdt:P131 ?place .
      ?place wdt:P131 wd:Q90 .
      OPTIONAL { ?item wdt:P625 ?coords . }
      OPTIONAL { ?item wdt:P18 ?image . }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr" . }
    } ORDER BY ?placeLabel ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Cathedrals_in_Paris
  title: Cathedrals in Paris
  description: "* Items used:  \n* Properties used:"
- name: Churches in church district Wittenberg
  namespace: examples
  domain: wikidata.org
  query_id: churches-in-church-district-wittenberg--examples@wikidata.org
  sparql: |2

    #defaultView:Map{"layer": "?pbLabel"}
    SELECT ?item ?itemLabel ?pbLabel (SAMPLE(?cat) AS ?cat) (SAMPLE(?coord) AS ?coord) (SAMPLE(?img) AS ?img)
    WHERE {
      wd:Q75849591 wdt:P527 [ wdt:P527 ?item; wdt:P361 ?pb ].
      ?pb wdt:P31 wd:Q76598130.
      ?item wdt:P625 ?coord.
      OPTIONAL { ?item wdt:P373 ?cat. }
      OPTIONAL { ?item wdt:P18 ?img. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "de". }
    } GROUP BY ?item ?itemLabel ?pbLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Churches_in_church_district_Wittenberg
  title: Churches in church district Wittenberg
  description: ''
- name: Special church type "Spitalkirche" in Germany
  namespace: examples
  domain: wikidata.org
  query_id: special-church-type-spitalkirche-in-germany--examples@wikidata.org
  sparql: |2

    #defaultView:Map
    SELECT ?pid ?name ?coord ?ort ?ortLabel
    WHERE
    {
      ?pid wdt:P31 wd:Q16970.
      ?pid rdfs:label ?name
      FILTER((LANG(?name)) = "de")
      FILTER(REGEX(STR(?name), "[Ss]pitalkirche")).
      ?pid wdt:P131 ?ort.
      ?pid wdt:P17 wd:Q183.
      ?pid wdt:P625 ?coord.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "de,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Special_church_type_"Spitalkirche"_in_Germany
  title: Special church type "Spitalkirche" in Germany
  description: ''
- name: Museums in Brittany
  namespace: examples
  domain: wikidata.org
  query_id: museums-in-brittany--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?museumLabel ?museumDescription ?villeId ?villeIdLabel (?villeIdLabel AS ?ville) ?coord ?lat ?lon
    WHERE
    {
      ?museum wdt:P539 ?museofile. # french museofile Id
      ?museum wdt:P131* wd:Q12130. # in Brittany
      ?museum wdt:P131 ?villeId. #city of the museum
      # ?object wdt:P166 wd:Q2275045 # that have french label "musées de France"
      OPTIONAL {?museum wdt:P856 ?link.} # official website
      OPTIONAL {?museum wdt:P625 ?coord .} # geographic coord
      OPTIONAL {
        ?museum p:P625 ?statement.
        ?statement psv:P625 ?node.
        ?node wikibase:geoLatitude ?lat.
        ?node wikibase:geoLongitude ?lon.
       }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr". } #french label
    }
    ORDER BY ?villeIdLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Museums_in_Brittany
  title: Museums in Brittany
  description: ''
- name: All museums in Barcelona with coordinates
  namespace: examples
  domain: wikidata.org
  query_id: all-museums-in-barcelona-with-coordinates--examples@wikidata.org
  sparql: |2

    # ----
    #All museums (including subclass of museum) in Barcelona with coordinates
    SELECT DISTINCT ?item ?name ?coord ?lat ?lon
    WHERE
    {
     hint:Query hint:optimizer "None" .
     ?item wdt:P131* wd:Q1492 .
     ?item wdt:P31/wdt:P279* wd:Q33506 .
     ?item wdt:P625 ?coord .
     ?item p:P625 ?coordinate .
     ?coordinate psv:P625 ?coordinate_node .
     ?coordinate_node wikibase:geoLatitude ?lat .
     ?coordinate_node wikibase:geoLongitude ?lon .
     SERVICE wikibase:label {
     bd:serviceParam wikibase:language "ca" .
     ?item rdfs:label ?name
     }
    }
    ORDER BY ASC (?name)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_museums_in_Barcelona_with_coordinates
  title: All museums in Barcelona with coordinates
  description: ''
- name: Museums in Antwerp
  namespace: examples
  domain: wikidata.org
  query_id: museums-in-antwerp--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?item ?itemLabel ?coordinates
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q33506 ;
            wdt:P131 wd:Q12892 ;
            wdt:P625 ?coordinates .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "nl, en" }
      }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Museums_in_Antwerp
  title: Museums in Antwerp
  description: ''
- name: Louvre artworks in display cases
  namespace: examples
  domain: wikidata.org
  query_id: louvre-artworks-in-display-cases--examples@wikidata.org
  sparql: |2

    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?itemDescription ?image WHERE {
      #part1: objects in cases
      {
      ?item wdt:P276 ?case .
      ?case wdt:P31 wd:Q3561331 .

      ?case wdt:P276 ?room .
      ?room wdt:P31/wdt:P279* wd:Q180516 . # wd:Q15206795

      ?room wdt:P466 ?dep .
      ?dep wdt:P361+ wd:Q19675
      }

      OPTIONAL { ?item wdt:P18 ?image } # Optionally with an image

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,fr" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Louvre_artworks_in_display_cases
  title: Louvre artworks in display cases
  description: ''
- name: Characters portrayed by most actors
  namespace: examples
  domain: wikidata.org
  query_id: characters-portrayed-by-most-actors--examples@wikidata.org
  sparql: |2


    SELECT ?character ?characterLabel (COUNT(?actor) AS ?count)
    WHERE
    {
      {
        SELECT DISTINCT ?character ?actor
        WHERE {
          ?film p:P161 [
            ps:P161 ?actor;
            pq:P453 ?character
          ].
          #?character wdt:P31 wd:Q5. # uncomment to filter for real people
        }
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    GROUP BY ?character ?characterLabel
    ORDER BY DESC(?count)
    LIMIT 25
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Characters_portrayed_by_most_actors
  title: Characters portrayed by most actors
  description: ''
- name: List of actors with pictures with year of birth and/or death
  namespace: examples
  domain: wikidata.org
  query_id: list-of-actors-with-pictures-with-year-of-birth-and-or-death--examples@wikidata.org
  sparql: |2


    #defaultView:ImageGrid
    SELECT ?human ?humanLabel ?yob ?yod ?picture
    WHERE
    {
      ?human wdt:P31 wd:Q5 ;
             wdt:P106 wd:Q33999 .
      ?human wdt:P18 ?picture .
      OPTIONAL { ?human wdt:P569 ?dob . ?human wdt:P570 ?dod }.
      BIND(YEAR(?dob) as ?yob) . #if available: year
      BIND(YEAR(?dod) as ?yod) .
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
      }
    }
    LIMIT 88
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_actors_with_pictures_with_year_of_birth_and/or_death
  title: List of actors with pictures with year of birth and/or death
  description: ''
- name: Actors who played the same role more than 40 years apart
  namespace: examples
  domain: wikidata.org
  query_id: actors-who-played-the-same-role-more-than-40-years-apart--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?actor ?actorLabel ?characterLabel ?movie1Label ?movie2Label WHERE {
      ?movie1 p:P161 [
                ps:P161 ?actor;
                pq:P453 ?character
              ];
              wdt:P577 ?movie1Publication.
      ?movie2 p:P161 [
                ps:P161 ?actor;
                pq:P453 ?character
              ];
              wdt:P577 ?movie2Publication.
      MINUS{?movie1 wdt:P31/wdt:P279? wd:Q24856} # Not a series
      MINUS{?movie2 wdt:P31/wdt:P279? wd:Q24856} # Not a series
      FILTER(?character != wd:Q18086706). # Not as "themselves"
      FILTER(?movie1Publication + "P40Y"^^xsd:duration < ?movie2Publication) # 40 years between them
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Actors_who_played_the_same_role_more_than_40_years_apart
  title: Actors who played the same role more than 40 years apart
  description: ''
- name: Films of directors by their English Wikipedia name
  namespace: examples
  domain: wikidata.org
  query_id: films-of-directors-by-their-english-wikipedia-name--examples@wikidata.org
  sparql: |2

    SELECT ?film ?filmLabel ?genere ?d WHERE {
      {
        SELECT ?film (GROUP_CONCAT(DISTINCT ?gL; SEPARATOR = ", ") AS ?genere) (MIN(YEAR(?date)) AS ?d) WHERE {
          ?sitelink schema:about ?director;
            schema:isPartOf <https://en.wikipedia.org/>;
            schema:name "Steven Spielberg"@en. # Edit this with different director's name to see their films. Use the English Wikipedia title only.
          ?film wdt:P31 wd:Q11424;
            wdt:P136 ?g, ?g.
          ?g rdfs:label ?gL.
          ?film wdt:P57 ?director;
            wdt:P577 ?date.
          FILTER((LANG(?gL)) = "en")
          SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
        }
        GROUP BY ?film
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC (?d)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Films_of_directors_by_their_English_Wikipedia_name
  title: Films of directors by their English Wikipedia name
  description: ''
- name: Winner of the Academy Awards by Award and Time
  namespace: examples
  domain: wikidata.org
  query_id: winner-of-the-academy-awards-by-award-and-time--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?item ?itemLabel ?awardLabel ?time
    {
        ?item wdt:P106/wdt:P279* wd:Q3455803 ; # Items with the Occupation(P106) of Director(Q3455803) or a subclass(P279)
              p:P166 ?awardStat .              # ... with an awarded(P166) statement
        ?awardStat pq:P805 ?award ;            # Get the award (which is "subject of" XXth Academy Awards)
                   ps:P166 wd:Q103360 .        # ... that has the value Academy Award for Best Director(Q103360)
        ?award wdt:P585 ?time .                # the "point of time" of the Academy Award
        SERVICE wikibase:label {               # ... include the labels
            bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en"
        }
    }
    ORDER BY DESC(?time)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Winner_of_the_Academy_Awards_by_Award_and_Time
  title: Winner of the Academy Awards by Award and Time
  description: ''
- name: Academy award data
  namespace: examples
  domain: wikidata.org
  query_id: academy-award-data--examples@wikidata.org
  sparql: |2+

  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Academy_award_data
  title: Academy award data
  description: "* items used:  \n* Properties used:"
- name: People that received both Academy Award and Nobel Prize
  namespace: examples
  domain: wikidata.org
  query_id: people-that-received-both-academy-award-and-nobel-prize--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?Person ?PersonLabel ?NobelPrizeLabel ?AcademyAwardLabel WHERE {
      ?NobelPrize wdt:P279?/wdt:P31? wd:Q7191 .    # <- subtypes of nobel prize
      ?AcademyAward wdt:P279?/wdt:P31? wd:Q19020 . # <- subtypes of academy award
      ?Person wdt:P166? ?NobelPrize .              # <- people awarded a nobel prize
      ?Person wdt:P166? ?AcademyAward .            # <- people awarded an academy award
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#People_that_received_both_Academy_Award_and_Nobel_Prize
  title: People that received both Academy Award and Nobel Prize
  description: ''
- name: Number of handed out Academy Awards per award type
  namespace: examples
  domain: wikidata.org
  query_id: number-of-handed-out-academy-awards-per-award-type--examples@wikidata.org
  sparql: |2


    SELECT ?awardCount ?award ?awardLabel WHERE {
      {
        SELECT (COUNT(?award) AS ?awardCount) ?award
        WHERE
        {
          {
            SELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {
              ?award wdt:P31 wd:Q19020 .      # All items that are instance of(P31) of Academy awards (Q19020)
              {
                ?human p:P166 ?awardStat .              # Humans with an awarded(P166) statement
                ?awardStat ps:P166 ?award .        # ... that has any of the values of ?award
                ?awardStat pq:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
                ?awardStat pq:P1686 ?awardWork . # The work they have been awarded for
                ?human wdt:P31 wd:Q5 .        # Humans
              } UNION {
                ?awardWork wdt:P31 wd:Q11424 . # Films
                ?awardWork p:P166 ?awardStat . # ... with an awarded(P166) statement
                ?awardStat ps:P166 ?award .        # ... that has any of the values of ?award
                ?awardStat pq:P805 ?awardEdition . # Get the award edition (which is "subject of" XXth Academy Awards)
              }
              OPTIONAL {
                ?awardEdition wdt:P585 ?time . # the "point of time" of the Academy Award
                ?awardWork wdt:P57 ?director .
              }
            }
            GROUP BY ?awardWork ?award # We only want every movie once for a category (a 'random' person is selected)
          }
        } GROUP BY ?award
        ORDER BY ASC(?awardCount)
      }
      SERVICE wikibase:label { # ... include the labels
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Number_of_handed_out_Academy_Awards_per_award_type
  title: Number of handed out Academy Awards per award type
  description: "* Items used: , \n* Properties used: , , , ,"
- name: Film directors ranked by number of sitelinks multiplied by their number of
    films
  namespace: examples
  domain: wikidata.org
  query_id: film-directors-ranked-by-number-of-sitelinks-multiplied-by-their-number-of-films--examples@wikidata.org
  sparql: |
    SELECT ?director ?director_label ?films ?sitelinks ((?films * ?sitelinks) as ?rank)
    WHERE {
      {SELECT ?director (count(DISTINCT ?film) as ?films) (count(DISTINCT ?sitelink) as ?sitelinks)
         WHERE {
           ?director wdt:P106 wd:Q2526255 .         # has "film director" as occupation
         ?film wdt:P57 ?director .            # get all films directed by the director
           ?sitelink schema:about ?director .       # get all the sitelinks about the director
           } GROUP BY ?director }
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". # Get label if it exists
    ?director rdfs:label ?director_label }
    } ORDER BY DESC(?rank)
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Film_directors_ranked_by_number_of_sitelinks_multiplied_by_their_number_of_films
  title: Film directors ranked by number of sitelinks multiplied by their number of
    films
  description: ''
- name: The Simpsons television series episodes list by season
  namespace: examples
  domain: wikidata.org
  query_id: the-simpsons-television-series-episodes-list-by-season--examples@wikidata.org
  sparql: |
    SELECT ?show ?showLabel ?seasonNumber ?episode ?episodeLabel
    WHERE {
      BIND(wd:Q886 as ?show) .
      ?season wdt:P179 ?show;
              wdt:P527 ?episode;
              p:P179 [pq:P1545 ?seasonNumber] .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY xsd:integer(?seasonNumber)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_Simpsons_television_series_episodes_list_by_season
  title: The Simpsons television series episodes list by season
  description: "* Items used: \n* Properties used: ,"
- name: Law & Order episodes
  namespace: examples
  domain: wikidata.org
  query_id: law-order-episodes--examples@wikidata.org
  sparql: |2

    # ----
    # All Law & Order episodes on Wikidata.
    # According to enwp, “[a] total of 456 original episodes… aired before cancellation” (https://en.wikipedia.org/wiki/List_of_Law_%26_Order_episodes).
    # As of this writing, the query returns 451 results, so some episodes are missing (either without item or lacking the necessary statements to match this query).

    SELECT (SAMPLE(?seasonNumber) AS ?seasonNumber) (SAMPLE(?episodeNumber) AS ?episodeNumber) (SAMPLE(?title) AS ?title) (MIN(?pubDate) AS ?pubDate) ?episode
    {
      # All episodes should be instance of episode with series Law & Order.
      ?episode wdt:P31 wd:Q21191270;
               wdt:P179 wd:Q321423.
      # Many of them also have the season as series, so we can get episode and season number from qualifiers there.
      OPTIONAL {
        ?episode p:P179 [
          # the season also has series Law & Order
          ps:P179/p:P179 [
            ps:P179 wd:Q321423;
                    pq:P1545 ?seasonNumber
          ] ;
          pq:P1545 ?episodeNumber
        ]
      }
      OPTIONAL { ?episode wdt:P1476 ?title. }
      OPTIONAL { ?episode wdt:P577 ?pubDate. }
    }
    GROUP BY ?episode # make sure we return each episode only once – a few have multiple publication dates, for example
    ORDER BY IF(BOUND(?seasonNumber), xsd:integer(?seasonNumber), 1000) xsd:integer(?episodeNumber) ?title
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Law_&_Order_episodes
  title: Law & Order episodes
  description: "* Items used:  \n* Properties used:"
- name: Main subjects of West Wing episodes
  namespace: examples
  domain: wikidata.org
  query_id: main-subjects-of-west-wing-episodes--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?episode ?ordinal ?episodeLabel ?subject ?subjectLabel
    WHERE {
      ?episode wdt:P31/wdt:P279* wd:Q1983062;
               p:P179 ?statement.
      OPTIONAL{ ?episode wdt:P921 ?subject }
      ?statement ps:P179 wd:Q3577037;
                 pq:P1545 ?ordinal
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    } ORDER BY xsd:integer(?ordinal)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Main_subjects_of_West_Wing_episodes
  title: Main subjects of West Wing episodes
  description: ''
- name: Movies with Bud Spencer
  namespace: examples
  domain: wikidata.org
  query_id: movies-with-bud-spencer--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel (MIN(?date) AS ?firstReleased) ?_image
    WHERE {
      ?item wdt:P161 wd:Q221074;
            wdt:P577 ?date
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      OPTIONAL { ?item wdt:P18 ?_image. }
    } GROUP BY ?item ?itemLabel ?_image
    ORDER BY (?date)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Movies_with_Bud_Spencer
  title: Movies with Bud Spencer
  description: ''
- name: Contemporary Indian actresses
  namespace: examples
  domain: wikidata.org
  query_id: contemporary-indian-actresses--examples@wikidata.org
  sparql: |
    SELECT ?item ?itemLabel ?itemDescription (SAMPLE(?img) AS ?image) (SAMPLE(?dob) AS ?dob) ?sl
    WHERE {
      ?item wdt:P106 wd:Q33999 ;
              wdt:P27 wd:Q668 ;
              wdt:P21 wd:Q6581072 .
      MINUS { ?item wdt:P570 [] }
      OPTIONAL { ?item wdt:P18 ?img }
      OPTIONAL { ?item wdt:P569 ?dob }
      OPTIONAL { ?item wikibase:sitelinks ?sl }
        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en"}
    } GROUP BY ?item ?itemLabel ?itemDescription ?sl
    ORDER BY DESC(?sl)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Contemporary_Indian_actresses
  title: Contemporary Indian actresses
  description: ''
- name: Articles on Punjabi (Gurmukhi) Wikipedia about Pakistani actresses
  namespace: examples
  domain: wikidata.org
  query_id: articles-on-punjabi-gurmukhi-wikipedia-about-pakistani-actresses--examples@wikidata.org
  sparql: |
    #added 2017-03-25 (46 results)
    SELECT ?sitelink
    WHERE
    {
      # gender = female
        ?item wdt:P21 wd:Q6581072 .

        # country = Pakistan (Q25)
        { ?item wdt:P27 wd:Q843 }

            # occupation = actress (Q33999)
        { ?item wdt:P106 wd:Q33999 }

      # look for articles (sitelinks) in Punjabi ("pa")
        { ?sitelink schema:about ?item . ?sitelink schema:inLanguage "pa" }

      # humans only
        ?item wdt:P31 wd:Q5 .
    }
    #Listeria can only handle up to 5000
    LIMIT 5000
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Articles_on_Punjabi_(Gurmukhi)_Wikipedia_about_Pakistani_actresses
  title: Articles on Punjabi (Gurmukhi) Wikipedia about Pakistani actresses
  description: ''
- name: All Dr. Who performers
  namespace: examples
  domain: wikidata.org
  query_id: all-dr-who-performers--examples@wikidata.org
  sparql: |
    #added 2017-07-16, updated 2020-07-08
    SELECT ?doctor ?doctorLabel ?ordinal ?performer ?performerLabel
    WHERE {
      ?doctor wdt:P31 wd:Q47543030 .
      OPTIONAL { ?doctor wdt:P1545 ?ordinal }
      OPTIONAL { ?doctor p:P175 / ps:P175 ?performer }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY ASC(xsd:integer(?ordinal) )
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_Dr._Who_performers
  title: All Dr. Who performers
  description: ''
- name: Movies and their narrative location on a map
  namespace: examples
  domain: wikidata.org
  query_id: movies-and-their-narrative-location-on-a-map--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT ?movie ?movieLabel ?narrative_location ?narrative_locationLabel ?coordinates WHERE {
       ?movie wdt:P840 ?narrative_location ;
              wdt:P31 wd:Q11424 .
       ?narrative_location wdt:P625 ?coordinates .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Movies_and_their_narrative_location_on_a_map
  title: Movies and their narrative location on a map
  description: ''
- name: Movies released in 2017
  namespace: examples
  domain: wikidata.org
  query_id: movies-released-in-2017--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?item ?itemLabel WHERE {
      ?item wdt:P31 wd:Q11424.
      ?item wdt:P577 ?pubdate.
      FILTER((?pubdate >= "2017-01-01T00:00:00Z"^^xsd:dateTime) && (?pubdate <= "2017-12-31T00:00:00Z"^^xsd:dateTime))
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Movies_released_in_2017
  title: Movies released in 2017
  description: ''
- name: Authors, writers and poets ranked by sitelink and also includes "country of
    citizenship"
  namespace: examples
  domain: wikidata.org
  query_id: authors-writers-and-poets-ranked-by-sitelink-and-also-includes-country-of-citizenship--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?writer ?place ?linkcount
    WHERE
    {
      {?s wdt:P106 wd:Q36180 .} UNION { ?s wdt:P106 wd:Q482980 . } UNION { ?s wdt:P106 wd:Q49757 . }
      ?s wdt:P27 ?pl .
      ?s wikibase:sitelinks ?linkcount .
      OPTIONAL {
         ?s rdfs:label ?writer FILTER (lang(?writer) = "en").
       }
        OPTIONAL {
         ?pl rdfs:label ?place FILTER (lang(?place) = "en").
       }
    } GROUP BY ?place ?writer ?linkcount HAVING (?linkcount > 10) ORDER BY DESC(?linkcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authors,_writers_and_poets_ranked_by_sitelink_and_also_includes_"country_of_citizenship"
  title: Authors, writers and poets ranked by sitelink and also includes "country
    of citizenship"
  description: "* Items used:   \n* Properties used:"
- name: 'Occupation: writer, language: Belarussian, died more than 50 years (so his
    books now in public domain)'
  namespace: examples
  domain: wikidata.org
  query_id: occupation-writer-language-belarussian-died-more-than-50-years-so-his-books-now-in-public-domain--examples@wikidata.org
  sparql: "\nSELECT ?writer ?writerLabel ?dateOfDeath WHERE {\n  ?writer wdt:P31 wd:Q5;\
    \          # Instance of human\n          wdt:P106 wd:Q36180;     # Occupation\
    \ writer\n          wdt:P1412 wd:Q9091;     # Languages spoken or written Belarusian\n\
    \          wdt:P570 ?dateOfDeath .\n\n  FILTER((YEAR(?dateOfDeath)) <= (YEAR(NOW())\
    \ - 50)) . # More than 50 years ago\n  \n  SERVICE wikibase:label { bd:serviceParam\
    \ wikibase:language \"[AUTO_LANGUAGE],be\". }\n}\nORDER BY DESC(?dateOfDeath)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Occupation:_writer,_language:_Belarussian,_died_more_than_50_years_(so_his_books_now_in_public_domain)
  title: 'Occupation: writer, language: Belarussian, died more than 50 years (so his
    books now in public domain)'
  description: ''
- name: Birth places of German poets
  namespace: examples
  domain: wikidata.org
  query_id: birth-places-of-german-poets--examples@wikidata.org
  sparql: |
    #defaultView:Map{"hide": ["?coord"]}
    SELECT ?subj ?subjLabel ?place ?placeLabel ?coord ?birthyear
    WHERE {
       ?subj wdt:P106 wd:Q49757 .
       ?subj wdt:P19 ?place .
       ?place wdt:P17 wd:Q183 .
       ?place wdt:P625 ?coord .
       OPTIONAL { ?subj wdt:P569 ?dob }
       BIND(YEAR(?dob) as ?birthyear)
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Birth_places_of_German_poets
  title: Birth places of German poets
  description: ''
- name: Books or literary works published before 1830 with place of publication or
    narrative location coordinates
  namespace: examples
  domain: wikidata.org
  query_id: books-or-literary-works-published-before-1830-with-place-of-publication-or-narrative-location-coordinates--examples@wikidata.org
  sparql: |2-

    SELECT ?item ?label ?coord ?place
    WHERE
    {
      VALUES ?type {wd:Q571 wd:Q7725634} # book or literary work
      ?item wdt:P31 ?type .
      ?item wdt:P577 ?date FILTER (?date < "1830-01-01T00:00:00Z"^^xsd:dateTime) .
      ?item rdfs:label ?label FILTER (lang(?label) = "en")

      OPTIONAL {
        ?item (wdt:P291
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Books_or_literary_works_published_before_1830_with_place_of_publication_or_narrative_location_coordinates
  title: Books or literary works published before 1830 with place of publication or
    narrative location coordinates
  description: "*  Items used:  \n*  Properties used:"
- name: Books by a given Author including genres and date of first publication
  namespace: examples
  domain: wikidata.org
  query_id: books-by-a-given-author-including-genres-and-date-of-first-publication--examples@wikidata.org
  sparql: |2

     SELECT DISTINCT ?book ?bookLabel ?authorLabel (GROUP_CONCAT(?genre_label) as ?genres) (MIN(?publicationDate) as ?firstPublication)
    WHERE
    {
      ?author rdfs:label "Ernest Hemingway"@en .
      ?book wdt:P50 ?author .
      OPTIONAL {
        ?book wdt:P136 ?genre .
        ?genre rdfs:label ?genre_label FILTER (lang(?genre_label) = "en").
      }

      OPTIONAL {
        ?book wdt:P577 ?publicationDate .
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
      }
    } GROUP BY ?book ?bookLabel ?authorLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Books_by_a_given_Author_including_genres_and_date_of_first_publication
  title: Books by a given Author including genres and date of first publication
  description: ''
- name: Text by author containing case-insensitive title with optional cover image
  namespace: examples
  domain: wikidata.org
  query_id: text-by-author-containing-case-insensitive-title-with-optional-cover-image--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?item ?authorLabel ?itemLabel ?image where {
      ?item wdt:P31/wdt:P279* wd:Q234460.

      ?author ?label 'Bram Stoker'.
      ?item wdt:P50 ?author.

      ?item rdfs:label ?itemLabel.
      FILTER contains(lcase(?itemLabel), 'dracula').

      OPTIONAL {?item wdt:P18 ?image.}

      SERVICE wikibase:label {bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".}
    } LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Text_by_author_containing_case-insensitive_title_with_optional_cover_image
  title: Text by author containing case-insensitive title with optional cover image
  description: ''
- name: Literary works by label count
  namespace: examples
  domain: wikidata.org
  query_id: literary-works-by-label-count--examples@wikidata.org
  sparql: |2

    SELECT ?s ?desc (COUNT(DISTINCT ?label) as ?labelcount)
    WHERE
    {
      ?s wdt:P31 wd:Q7725634 .
      ?s rdfs:label ?label .
      OPTIONAL {
         ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
       }
     } GROUP BY ?s ?desc ORDER BY DESC(?labelcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Literary_works_by_label_count
  title: Literary works by label count
  description: "*  Items used: \n*  Properties used:"
- name: All subclasses of "Literary Work"
  namespace: examples
  domain: wikidata.org
  query_id: all-subclasses-of-literary-work--examples@wikidata.org
  sparql: |2

    SELECT ?s ?desc
    WHERE
    {
      ?s wdt:P279 wd:Q7725634 .
      OPTIONAL {
         ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
       }
     }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_subclasses_of_"Literary_Work"
  title: All subclasses of "Literary Work"
  description: ''
- name: Epic poems by label count
  namespace: examples
  domain: wikidata.org
  query_id: epic-poems-by-label-count--examples@wikidata.org
  sparql: |2

    SELECT ?s ?desc (COUNT(DISTINCT ?label) as ?labelcount)
    WHERE
    {
      ?s wdt:P31 wd:Q37484 .
      ?s rdfs:label ?label .
      OPTIONAL {
         ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
       }
     } GROUP BY ?s ?desc ORDER BY DESC(?labelcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Epic_poems_by_label_count
  title: Epic poems by label count
  description: ''
- name: Epic poems by sitelink count
  namespace: examples
  domain: wikidata.org
  query_id: epic-poems-by-sitelink-count--examples@wikidata.org
  sparql: |2

    # ----
    #old method for sitelink count
    SELECT ?s ?desc ?linkcount
    WHERE
    {
      ?s wdt:P31 wd:Q37484 .
      ?s wikibase:sitelinks ?linkcount .
      OPTIONAL {
         ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
       }
     } GROUP BY ?s ?desc ?linkcount ORDER BY DESC(?linkcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Epic_poems_by_sitelink_count
  title: Epic poems by sitelink count
  description: ''
- name: Instance of Book by sitelink count
  namespace: examples
  domain: wikidata.org
  query_id: instance-of-book-by-sitelink-count--examples@wikidata.org
  sparql: |2

    #old method for sitelink count
    SELECT ?s ?desc ?linkcount
    WHERE
    {
      ?s wdt:P31 wd:Q571 .
      ?s wikibase:sitelinks ?linkcount .
      OPTIONAL {
         ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
       }
     } GROUP BY ?s ?desc ?linkcount ORDER BY DESC(?linkcount)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Instance_of_Book_by_sitelink_count
  title: Instance of Book by sitelink count
  description: ''
- name: Poets and monarchs
  namespace: examples
  domain: wikidata.org
  query_id: poets-and-monarchs--examples@wikidata.org
  sparql: |2


    SELECT ?poetLabel ?image ?yob ?yod ?start ?end ?monarchLabel
    WHERE
    {
      ?poet p:P39 ?positionStat.
      ?positionStat ps:P39 wd:Q877838;
                    pq:P580 ?start;
                    pq:P748 ?monarch.
      OPTIONAL {
        ?positionStat pq:P582 ?end.
      }
      OPTIONAL {
        ?poet wdt:P18 ?image;
              wdt:P569 ?dob;
              wdt:P570 ?dod.
      }
      BIND(YEAR(?dob) AS ?yob).
      BIND(YEAR(?dod) AS ?yod).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY ?start DESC(?monarchLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Poets_and_monarchs
  title: Poets and monarchs
  description: "*  Items used: \n*  Properties used:"
- name: List of digital libraries in the world
  namespace: examples
  domain: wikidata.org
  query_id: list-of-digital-libraries-in-the-world--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?item ?itemLabel ?website
    WHERE {
    ?item wdt:P31/wdt:P279* wd:Q212805 # digital libraries or subtypes
    OPTIONAL { ?item wdt:P856 ?website } # Official URL if known
    MINUS { ?item wdt:P576 [] } # Exclude those that have shut down
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en, es, ca, fr, de, pl, uk, ru, he" }
    }
    ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_digital_libraries_in_the_world
  title: List of digital libraries in the world
  description: ''
- name: Map of Libraries in Canada
  namespace: examples
  domain: wikidata.org
  query_id: map-of-libraries-in-canada--examples@wikidata.org
  sparql: |2

    # ----
    # Canadian libraries on a map (must have coordinates!)
    #defaultView:Map
    SELECT ?library ?libraryLabel ?coords WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,fr". }
      ?library (wdt:P31/wdt:P279*) wd:Q7075.
      ?library wdt:P17 wd:Q16.
      ?library wdt:P625 ?coords.
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_Libraries_in_Canada
  title: Map of Libraries in Canada
  description: ''
- name: List of authors unsuccessfully nominated for Nobel prize in literature
  namespace: examples
  domain: wikidata.org
  query_id: list-of-authors-unsuccessfully-nominated-for-nobel-prize-in-literature--examples@wikidata.org
  sparql: |2

    SELECT ?nominee ?nomineeLabel (SAMPLE(?citizenshipLabel) AS ?country) (COUNT(DISTINCT ?year) as ?timesNominated)
    WHERE
    {
        BIND( wd:Q37922 as ?prize )
        ?nominee p:P1411 [ ps:P1411 ?prize; pq:P585 ?time ]
        BIND( year(?time) as ?year )
        OPTIONAL {
          ?nominee wdt:P27 [ rdfs:label ?citizenshipLabel ] FILTER (lang(?citizenshipLabel) = "en") .
        }
        FILTER NOT EXISTS { ?nominee wdt:P166 ?prize . }
        SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    GROUP BY ?nominee ?nomineeLabel
    ORDER BY DESC(?timesNominated) ?nomineeLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_authors_unsuccessfully_nominated_for_Nobel_prize_in_literature
  title: List of authors unsuccessfully nominated for Nobel prize in literature
  description: "*  Items used: \n*  Properties used: ,"
- name: Authors whose works enter the public domain in 2017 (died in 1946)
  namespace: examples
  domain: wikidata.org
  query_id: authors-whose-works-enter-the-public-domain-in-2017-died-in-1946--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?item WHERE
    {
      ?item wdt:P31 wd:Q5 ;
      wdt:P106/wdt:P279* wd:Q482980 .
      ?item wdt:P570 ?time0 .
      FILTER((?time0 >= "1945-01-01T00:00:00Z"^^xsd:dateTime) && (?time0 <= "1946-01-01T00:00:00Z"^^xsd:dateTime))
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authors_whose_works_enter_the_public_domain_in_2017_(died_in_1946)
  title: Authors whose works enter the public domain in 2017 (died in 1946)
  description: ''
- name: Authors with United States citizenship without a Goodreads identifier
  namespace: examples
  domain: wikidata.org
  query_id: authors-with-united-states-citizenship-without-a-goodreads-identifier--examples@wikidata.org
  sparql: |2

    # ----
    #authors with country of citizenship United States who do not have a Goodreads identifier
    SELECT ?item ?itemLabel
    WHERE
    {
      ?item wdt:P31 wd:Q5. #instance of (P31) human (Q5)
      ?item wdt:P27 wd:Q30. #country of citizenship (P27) is United States (Q30)
      ?item wdt:P106 wd:Q36180. #occupation (P106) is writer (Q36180)
      MINUS { ?item wdt:P2963 [] } .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authors_with_United_States_citizenship_without_a_Goodreads_identifier
  title: Authors with United States citizenship without a Goodreads identifier
  description: ''
- name: Works by women that were born between 1800 and 1900, are in the WomenWriters
    database and are translated
  namespace: examples
  domain: wikidata.org
  query_id: works-by-women-that-were-born-between-1800-and-1900-are-in-the-womenwriters-database-and-are-translated--examples@wikidata.org
  sparql: |
    SELECT ?translator ?translatorLabel ?work ?workLabel ?author ?authorLabel {
        ?work wdt:P655 ?translator;
              wdt:P50 ?author.

        ?author wdt:P2533 ?wid;
                wdt:P21 wd:Q6581072;
                wdt:P569 ?birth;

        FILTER (?birth > "1800-01-01"^^xsd:dateTime && ?birth < "1900-01-01"^^xsd:dateTime)

        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,nl,fr". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Works_by_women_that_were_born_between_1800_and_1900,_are_in_the_WomenWriters_database_and_are_translated
  title: Works by women that were born between 1800 and 1900, are in the WomenWriters
    database and are translated
  description: ''
- name: Pokémon!
  namespace: examples
  domain: wikidata.org
  query_id: pokémon--examples@wikidata.org
  sparql: |
    # Updated 2020-06-17

    # Gotta catch 'em all
    SELECT DISTINCT ?pokemon ?pokemonLabel ?pokedexNumber
    WHERE
    {
        ?pokemon wdt:P31/wdt:P279* wd:Q3966183 .
        ?pokemon p:P1685 ?statement.
        ?statement ps:P1685 ?pokedexNumber;
                  pq:P972 wd:Q20005020.
        FILTER (! wikibase:isSomeValue(?pokedexNumber) )
        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
    ORDER BY (?pokedexNumber)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Pokémon!
  title: Pokémon!
  description: "* Items used: \n* Properties used:"
- name: Fictional subjects of the Marvel Universe
  namespace: examples
  domain: wikidata.org
  query_id: fictional-subjects-of-the-marvel-universe--examples@wikidata.org
  sparql: |
    SELECT ?char ?charName (GROUP_CONCAT(DISTINCT ?typeLabel;separator=", ") AS ?types) (GROUP_CONCAT(DISTINCT ?universeLabel;separator=", ") AS ?universes)
    WHERE {
      ?char wdt:P1080 wd:Q931597;
              wdt:P31 ?type ;
              wdt:P1080 ?universe .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".
                             ?char rdfs:label ?charName .
                             ?universe rdfs:label ?universeLabel .
                             ?type rdfs:label ?typeLabel .}
    } GROUP BY ?char ?charName
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Fictional_subjects_of_the_Marvel_Universe
  title: Fictional subjects of the Marvel Universe
  description: ''
- name: Fictional characters whose birth/death date is in the current decade
  namespace: examples
  domain: wikidata.org
  query_id: fictional-characters-whose-birth-death-date-is-in-the-current-decade--examples@wikidata.org
  sparql: |
    SELECT DISTINCT ?character ?characterLabel ?birth ?death WITH {
      # Fictional character subclasses as a named subquery
      SELECT ?fictiontype WHERE {
        ?fictiontype wdt:P279* wd:Q95074. hint:Prior hint:gearing "forward".
      }
    } AS %i
    WHERE
    {
      INCLUDE %i
      ?character wdt:P31 ?fictiontype.
      # Date of birth
      { ?character wdt:P569 ?birth . }
      # Date of death
      UNION { ?character wdt:P570 ?death . }
      # Get actual decade to compare with dates
      BIND(xsd:integer(YEAR(NOW())/10) as ?actual_decade).
      # Only show characters born or deceased in the current decade
      FILTER(xsd:integer(YEAR(?birth)/10) = ?actual_decade {{!}}{{!}} xsd:integer(YEAR(?death)/10) = ?actual_decade)
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
      }
    } ORDER BY ?birth ?death
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Fictional_characters_whose_birth/death_date_is_in_the_current_decade
  title: Fictional characters whose birth/death date is in the current decade
  description: ''
- name: List tracks of an album, with links to Yandex, Apple, Spotify, Amazon
  namespace: examples
  domain: wikidata.org
  query_id: list-tracks-of-an-album-with-links-to-yandex-apple-spotify-amazon--examples@wikidata.org
  sparql: |2

    SELECT ?order ?item ?itemLabel ?yandex ?apple
    (URI(CONCAT("https://open.spotify.com/track/", ?spotify_id)) AS ?spotify)
    (URI(CONCAT("https://www.amazon.com/dp/", ?amazon_id)) AS ?amazon)
    {
      wd:Q105834355 p:P658 ?st . ?st ps:P658 ?item .

      OPTIONAL { ?st pq:P1545 ?order } .
      OPTIONAL {?item p:P750 [ps:P750 wd:Q4537983 ; pq:P2699 ?yandex ] .}
      OPTIONAL {?item p:P750 [ps:P750 wd:Q20056642 ; pq:P2699 ?apple ] .}
      OPTIONAL {?item wdt:P5749 ?amazon_id .}
      OPTIONAL {?item wdt:P2207 ?spotify_id .}

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],ru,en" }
    } ORDER BY xsd:integer(?order)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_tracks_of_an_album,_with_links_to_Yandex,_Apple,_Spotify,_Amazon
  title: List tracks of an album, with links to Yandex, Apple, Spotify, Amazon
  description: ''
- name: 'List tracks of an album, with links to Yandex, Apple, Spotify, Amazon: URLs
    without triangles - as text'
  namespace: examples
  domain: wikidata.org
  query_id: list-tracks-of-an-album-with-links-to-yandex-apple-spotify-amazon-urls-without-triangles-as-text--examples@wikidata.org
  sparql: "\nSELECT ?order ?item ?itemLabel\n(STR(?_yandex) AS ?yandex)\n(STR(?_apple)\
    \ AS ?apple)\n(CONCAT(\"https://open.spotify.com/track/\", ?spotify_id) AS ?spotify)\n\
    (CONCAT(\"https://www.amazon.com/dp/\", ?amazon_id) AS ?amazon)\n{\n  wd:Q105834355\
    \ p:P658 ?st . ?st ps:P658 ?item .\n\n  OPTIONAL { ?st pq:P1545 ?order } . \n\
    \  OPTIONAL {?item p:P750 [ps:P750 wd:Q4537983 ; pq:P2699 ?_yandex ] .}\n  OPTIONAL\
    \ {?item p:P750 [ps:P750 wd:Q20056642 ; pq:P2699 ?_apple ] .}\n  OPTIONAL {?item\
    \ wdt:P5749 ?amazon_id .}\n  OPTIONAL {?item wdt:P2207 ?spotify_id .}\n\n  SERVICE\
    \ wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],ru,en\"\
    \ }\n} ORDER BY xsd:integer(?order)\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_tracks_of_an_album,_with_links_to_Yandex,_Apple,_Spotify,_Amazon:_URLs_without_triangles_-_as_text
  title: 'List tracks of an album, with links to Yandex, Apple, Spotify, Amazon: URLs
    without triangles - as text'
  description: ''
- name: Timeline of albums by Manu Chao and Mano Negra
  namespace: examples
  domain: wikidata.org
  query_id: timeline-of-albums-by-manu-chao-and-mano-negra--examples@wikidata.org
  sparql: |2

    #defaultView:Timeline
    SELECT ?album ?performerLabel ?albumLabel ?publication_date WHERE {
      VALUES ?performer {
          wd:Q936474
          wd:Q207898
        }
       ?album wdt:P175 ?performer ;
          wdt:P577 ?publication_date .
       SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Timeline_of_albums_by_Manu_Chao_and_Mano_Negra
  title: Timeline of albums by Manu Chao and Mano Negra
  description: ''
- name: Most popular tonality
  namespace: examples
  domain: wikidata.org
  query_id: most-popular-tonality--examples@wikidata.org
  sparql: |2

     SELECT ?tonalityLabel (COUNT(?tonalityLabel) as ?count)
    WHERE
    {
      ?work wdt:P826 ?tonality.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    GROUP BY ?tonalityLabel
    ORDER BY DESC(?count)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Most_popular_tonality
  title: Most popular tonality
  description: ''
- name: Music composers by birth place
  namespace: examples
  domain: wikidata.org
  query_id: music-composers-by-birth-place--examples@wikidata.org
  sparql: |
    #defaultView:Map
    SELECT ?item ?itemLabel ?_coordinates ?_image WHERE {
      ?item wdt:P106 wd:Q36834; # occupation: composer
            wdt:P18 ?_image; # with an image depicting them
            wdt:P19/wdt:P625 ?_coordinates # their birthplace, specifically the coordinates of their birthplace
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" } # labels in English
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Music_composers_by_birth_place
  title: Music composers by birth place
  description: ''
- name: Composers and their most-used tonality
  namespace: examples
  domain: wikidata.org
  query_id: composers-and-their-most-used-tonality--examples@wikidata.org
  sparql: |2

    # ----
    # Each composer’s most used tonality, with number of works in that tonality.
    # (If this is ambiguous – multiple tonalities with the same number – there are multiple results for one composer.)
    #
    # The SPARQL for this is an evil perversion of three subqueries (one of them nested in another).
    # To understand it, you have to go inside out… follow the numbers.

    SELECT ?composerLabel ?tonalityLabel ?count
    WHERE
    {
      {
        # 4. Group again, this time just by the composer.
        #    We also select the highest count of a tonality.
        #    Notice that we don’t know what tonality this count is associated with – we’ll get to that.
        #    So now we have each composer, along with how often they used whatever tonality they used most.
        SELECT ?composer (MAX(?count) AS ?count_)
        WHERE
        {
          {
            # 2. Group by composer and tonality, so that for each composer and tonality, we get a count of how often the composer used this tonality.
            SELECT ?composer ?tonality (COUNT(?composition) AS ?count)
            WHERE
            {
              # 1. Extremely straightforward: the ?composition has the composer ?composer and the tonality ?tonality.
              #    (I’m not bothering with any “instance of” because the presence of these two properties is a sufficient indicator of ?composition being a composition.)
              ?composition wdt:P86 ?composer;
                           wdt:P826 ?tonality.
            }
            GROUP BY ?composer ?tonality
            HAVING(?count > 1) # 3. Limit that to counts > 1, because using a tonality once is hardly “most used”.
          }
        }
        GROUP BY ?composer
      }
      {
        # 6. Identical to 2.
        SELECT ?composer ?tonality (COUNT(?composition) AS ?count)
        WHERE
        {
          # 5. Identical to 1.
          ?composition wdt:P86 ?composer;
                       wdt:P826 ?tonality.
        }
        GROUP BY ?composer ?tonality
        HAVING(?count > 1) # 7. Identical to 3.
      }
      # 8. That’s it. Wait, what?
      #    From 4, we now have ?composer, any composer, and ?count, the count of how often they used whatever tonality they used most.
      #    From 6, we also have a ?composer, as well as a ?tonality, and the count of how often they used that particular tonality.
      #    The trick is that ?composer and ?count are the same variable in each subquery, and so now, when the two subqueries are joined,
      #    we select only that ?tonality from 6 where the ?composer and the ?count are identical to those from 4 –
      #    that is, where this tonality was used as often as the composer’s most-used tonality.
      #    In other words, this must *be* the composer’s most-used tonality (except when there are multiple tonalities with the same count).
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC(?count) # 9. Order by count (highest first), because the result isn’t very meaningful for low counts (many compositions aren’t on Wikidata or don’t have a tonality statement).
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Composers_and_their_most-used_tonality
  title: Composers and their most-used tonality
  description: '*  Properties used:'
- name: Songs with longest melody
  namespace: examples
  domain: wikidata.org
  query_id: songs-with-longest-melody--examples@wikidata.org
  sparql: |2


    SELECT ?song ?songLabel ?code
    WHERE
    {
      ?song wdt:P1236 ?code.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
    ORDER BY DESC(STRLEN(REPLACE(?code, " ", "")))
    LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Songs_with_longest_melody
  title: Songs with longest melody
  description: '*  Properties used:'
- name: How many musicians died at a certain age
  namespace: examples
  domain: wikidata.org
  query_id: how-many-musicians-died-at-a-certain-age--examples@wikidata.org
  sparql: |2

    # ----
       # Query to find all musicians who have already died
       # calculate their age (full years) at death
       # count how many of them died at each age

        SELECT ?age (COUNT (DISTINCT ?a) AS ?count) WHERE {
            ?a wdt:P31 wd:Q5 . #instance of human
            ?a wdt:P106/wdt:P279 wd:Q639669 . #occupation a subclass of musician
            ?a p:P569/psv:P569 ?birth_date_node .
            ?a p:P570/psv:P570 ?death_date_node .
            ?birth_date_node wikibase:timeValue ?birth_date .
            ?death_date_node wikibase:timeValue ?death_date .
            FILTER(?age > 10 && ?age < 100) . #ignore outlyers, several of which are probably errors
            BIND( year(?death_date) - year(?birth_date) - if(month(?death_date)<month(?birth_date) {{!}}{{!}} (month(?death_date)=month(?birth_date) && day(?death_date)<day(?birth_date)),1,0) as ?age )
            # calculate the age, precisely to the day (times and timezones ignored)
        }
      GROUP BY ?age
        ORDER BY ?age
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#How_many_musicians_died_at_a_certain_age
  title: How many musicians died at a certain age
  description: "*  Items used:  \n*  Properties used:"
- name: Musicians born in Rotterdam (the Netherlands)
  namespace: examples
  domain: wikidata.org
  query_id: musicians-born-in-rotterdam-the-netherlands--examples@wikidata.org
  sparql: |2

        SELECT DISTINCT ?item ?itemLabel ?itemDescription where {
        ?item wdt:P106/wdt:P279* wd:Q639669 .
        ?item wdt:P19/wdt:P131* wd:Q34370 .
        SERVICE wikibase:label { bd:serviceParam wikibase:language "en,nl" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Musicians_born_in_Rotterdam_(the_Netherlands)
  title: Musicians born in Rotterdam (the Netherlands)
  description: "*  Items used:  \n*  Properties used:"
- name: Paintings depicting woodwind instruments
  namespace: examples
  domain: wikidata.org
  query_id: paintings-depicting-woodwind-instruments--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:782--> Paintings depicting woodwind instruments</translate>
    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?object ?objectLabel ?artistLabel ?image
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q3305213 . # Painting or sub-type of painting
      ?item wdt:P180 ?object .
      ?object wdt:P279* wd:Q181247 .
      OPTIONAL {?item wdt:P170 ?artistitem}
      BIND (IF(wikibase:isSomeValue(?artistitem), "Artist unknown", ?artistitem) AS ?artist).
      ?item wdt:P18 ?image .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Paintings_depicting_woodwind_instruments
  title: Paintings depicting woodwind instruments
  description: "*  Items used:  \n*  Properties used:"
- name: Paintings depicting musical instruments with some connection to Hamburg
  namespace: examples
  domain: wikidata.org
  query_id: paintings-depicting-musical-instruments-with-some-connection-to-hamburg--examples@wikidata.org
  sparql: |
    #defaultView:ImageGrid
    SELECT DISTINCT ?item ?image {
      hint:Query hint:optimizer "None" .
      ?object wdt:P279* wd:Q34379 .
      ?item wdt:P180 ?object .
      ?item wdt:P31/wdt:P279* wd:Q3305213 .
      ?item wdt:P18 ?image .
      ?item ?prop ?hhlink .
      ?hhlink ?prop2 wd:Q1055 .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Paintings_depicting_musical_instruments_with_some_connection_to_Hamburg
  title: Paintings depicting musical instruments with some connection to Hamburg
  description: "*  Items used:   \n*  Properties used:"
- name: Rock bands that start with "M"
  namespace: examples
  domain: wikidata.org
  query_id: rock-bands-that-start-with-m--examples@wikidata.org
  sparql: |2

    SELECT ?band ?bandLabel
    WHERE
    {
      ?band wdt:P31 wd:Q5741069 .
            ?band rdfs:label ?bandLabel .
      FILTER(LANG(?bandLabel) = "en") .
      FILTER(STRSTARTS(?bandLabel, 'M')) .
    } ORDER BY LCASE(?bandLabel)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Rock_bands_that_start_with_"M"
  title: Rock bands that start with "M"
  description: ''
- name: Musicians or singers that have a genre containing 'rock'
  namespace: examples
  domain: wikidata.org
  query_id: musicians-or-singers-that-have-a-genre-containing-rock--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?human ?humanLabel
    WHERE
    {
        VALUES ?professions {wd:Q177220 wd:Q639669}
        ?human wdt:P31 wd:Q5 .
        ?human wdt:P106 ?professions .
        ?human wdt:P136 ?genre .
        ?human wikibase:statements ?statementcount .
        ?genre rdfs:label ?genreLabel .
        FILTER CONTAINS(?genreLabel, "rock") .
        FILTER (?statementcount > 50 ) .
        SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    ORDER BY ?humanLabel
    LIMIT 50
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Musicians_or_singers_that_have_a_genre_containing_'rock'
  title: Musicians or singers that have a genre containing 'rock'
  description: "* items used:   \n* Properties used:"
- name: '''27 club'' - musicians who died at age 27'
  namespace: examples
  domain: wikidata.org
  query_id: 27-club-musicians-who-died-at-age-27--examples@wikidata.org
  sparql: |
    SELECT ?person ?personLabel ?dob ?dod (xsd:integer(?age_) as ?age)
    where {
      # We do a subquery here, because otherwise we'll get a timeout because of the labels
      {
        SELECT DISTINCT ?person ?dob ?dod ?age_
        where {
          ?person wdt:P31 wd:Q5; # Get all humans
            wdt:P106/wdt:P279* wd:Q639669; # That have a musician or a subclass thereof as their occupation
            p:P569 ?dob_st; # death of birth
            p:P570 ?dod_st. # death of death

          ?dob_st psv:P569 [ wikibase:timeValue ?dob; wikibase:timePrecision ?dob_prec ];
            a wikibase:BestRank.
          ?dod_st psv:P570 [ wikibase:timeValue ?dod; wikibase:timePrecision ?dod_prec ];
            a wikibase:BestRank.

          # Only accept date precisions of a day or better, so it doesn't cause problems when we calculate age
          FILTER(?dob_prec >= 11 && ?dod_prec >= 11)

          # Time differences in Blazegraph are counted as days and is xsd:double
          # We then calculate the age by dividing this by the orbital period of Earth
          # This is a shortcut, but it is accurate enough for our purposes
          bind((?dod - ?dob) / 365.2564 as ?age_)

          # And filter by age
          filter(?age_ >= 27.0 && ?age_ < 28.0)
        }
        #LIMIT 1000
      }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY desc(?dod) # Order by most recent
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#'27_club'_-_musicians_who_died_at_age_27
  title: '''27 club'' - musicians who died at age 27'
  description: ''
- name: Public sculptures in Paris
  namespace: examples
  domain: wikidata.org
  query_id: public-sculptures-in-paris--examples@wikidata.org
  sparql: |2

     SELECT DISTINCT ?item ?Titre ?createur (year(?date) as ?AnneeCreation) ?image ?coord
    WHERE
    {
       ?item wdt:P31/wdt:P279* wd:Q860861.                    # sculpture
       ?item wdt:P136 wd:Q557141 .                            # genre : art public
       {?item wdt:P131 wd:Q90.}                               # ... située dans Paris
       UNION
       {?item wdt:P131 ?arr.                                  # ... ou dans un arrondissement de Paris
       ?arr wdt:P131 wd:Q90. }
       ?item rdfs:label ?Titre FILTER (lang(?Titre) = "fr").  # Titre

       OPTIONAL {?item wdt:P170 ?Qcreateur.                   # créateur/créatrice (option)
       ?Qcreateur rdfs:label ?createur FILTER (lang(?createur) = "fr") .}
       OPTIONAL {?item wdt:P571 ?date.}                       # date de création (option)
       OPTIONAL {?item wdt:P18  ?image.}                      # image (option)
       OPTIONAL {?item wdt:P625 ?coord.}                      # coordonnées géographiques (option)
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Public_sculptures_in_Paris
  title: Public sculptures in Paris
  description: ''
- name: Locations of Pablo Picasso works
  namespace: examples
  domain: wikidata.org
  query_id: locations-of-pablo-picasso-works--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?label ?coord ?subj
    WHERE
    {
       ?subj wdt:P170 wd:Q5593 .
      OPTIONAL {?subj wdt:P276 ?loc .
        ?loc wdt:P625 ?coord } .
       ?subj rdfs:label ?label FILTER (lang(?label) = "en")
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Locations_of_Pablo_Picasso_works
  title: Locations of Pablo Picasso works
  description: ''
- name: Eiffel Tower in art
  namespace: examples
  domain: wikidata.org
  query_id: eiffel-tower-in-art--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?item ?itemLabel ?instanceLabel ?creatorLabel (YEAR(?date) as ?year) ?image
    WHERE
    {
      ?item wdt:P180 wd:Q243 .
      ?item wdt:P31 ?instance .
      OPTIONAL { ?item wdt:P170 ?creator }
      OPTIONAL { ?item wdt:P571 ?date }
      OPTIONAL { ?item wdt:P18 ?image }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr,en" . }
    }
    ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Eiffel_Tower_in_art
  title: Eiffel Tower in art
  description: ''
- name: Paintings by Gustav Klimt
  namespace: examples
  domain: wikidata.org
  query_id: paintings-by-gustav-klimt--examples@wikidata.org
  sparql: |2

    #defaultView:ImageGrid
    SELECT *
    WHERE
    {
      ?item wdt:P31 wd:Q3305213 .
      ?item wdt:P170 wd:Q34661 .
      ?item wdt:P18 ?pic .
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Paintings_by_Gustav_Klimt
  title: Paintings by Gustav Klimt
  description: ''
- name: Map of all the paintings for which we know a location with the count per location
  namespace: examples
  domain: wikidata.org
  query_id: map-of-all-the-paintings-for-which-we-know-a-location-with-the-count-per-location--examples@wikidata.org
  sparql: |2

    # ----
    #Map of all the paintings for which we know a location with the count per location
    #defaultView:Map
    SELECT ?locationLabel ?coord (count(*) as ?count)
    WHERE
    {
        ?painting wdt:P31 wd:Q3305213 .
        ?painting wdt:P276 ?location .
      ?location wdt:P625 ?coord
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
    GROUP BY ?locationLabel ?coord
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_all_the_paintings_for_which_we_know_a_location_with_the_count_per_location
  title: Map of all the paintings for which we know a location with the count per
    location
  description: ''
- name: Versions of The Scream
  namespace: examples
  domain: wikidata.org
  query_id: versions-of-the-scream--examples@wikidata.org
  sparql: |2


    SELECT
      ?item
      (SAMPLE (?itemL) AS ?title)
      (SAMPLE (?y) AS ?year)
      (SAMPLE (?typeL) AS ?type)
      (group_concat(DISTINCT ?materialL ; separator = ", ") as ?materials)
      (SAMPLE (?collectionL) AS ?collection)
      (SAMPLE (?img) AS ?image)
    {
      SELECT ?item ?itemL (YEAR(?date) AS ?y) ?typeL ?collectionL ?img ?materialL
      WHERE {
        ?item wdt:P179 wd:Q471379 .
        ?item wdt:P18 ?img .
        ?item wdt:P571 ?date .
        ?item wdt:P31 ?instance .
        ?item rdfs:label ?itemL FILTER (lang(?itemL) = "en").
        ?instance rdfs:label ?typeL FILTER (lang(?typeL) = "en").
        OPTIONAL {
          ?item wdt:P195 ?collection .
          ?collection rdfs:label ?collectionL FILTER (lang(?collectionL) = "en").
        }
        OPTIONAL {
          ?item wdt:P186 ?material .
          ?material rdfs:label ?materialL FILTER (lang(?materialL) = "en").
        }
      }
    }
    GROUP BY ?item
    ORDER BY ?year ?item ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Versions_of_The_Scream
  title: Versions of The Scream
  description: ''
- name: Depicted objects in art work
  namespace: examples
  domain: wikidata.org
  query_id: depicted-objects-in-art-work--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?depicts (SAMPLE(?dL) AS ?depictsLabel) (COUNT(DISTINCT ?item) AS ?count)
    WHERE
    {
      ?item wdt:P180 ?depicts .
      OPTIONAL { ?depicts rdfs:label ?dL FILTER (lang(?dL) = "en") }
    }
    GROUP BY ?depicts
    ORDER BY DESC(?count) ?depictsLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Depicted_objects_in_art_work
  title: Depicted objects in art work
  description: ''
- name: Monuments historiques in Loire-Atlantique
  namespace: examples
  domain: wikidata.org
  query_id: monuments-historiques-in-loire-atlantique--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT
      ?item
      ?itemLabel
      ?communeLabel
      (group_concat(DISTINCT ?merimee ; separator = ", ") as ?merimee)
      ?coords
      ?image
    WHERE
    {
      {
        SELECT DISTINCT ?item ?merimee WHERE {
          ?item wdt:P1435/wdt:P279* wd:Q916475 .
          ?item p:P1435 ?heritage_statement .
          FILTER NOT EXISTS { ?heritage_statement pq:P582 ?end . }
          ?item wdt:P380 ?merimee.
        }
        ORDER BY ?merimee
      }
      ?item wdt:P131/wdt:P131* wd:Q3068 .
      ?item wdt:P131 ?commune .
      OPTIONAL { ?item wdt:P625 ?coords . }
      OPTIONAL { ?item wdt:P18 ?image . }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr" . }
    }
    GROUP BY ?item ?itemLabel ?communeLabel ?coords ?image
    ORDER BY ?communeLabel ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Monuments_historiques_in_Loire-Atlantique
  title: Monuments historiques in Loire-Atlantique
  description: ''
- name: Sculptures by Max Bill
  namespace: examples
  domain: wikidata.org
  query_id: sculptures-by-max-bill--examples@wikidata.org
  sparql: |2


    SELECT DISTINCT ?item ?itemLabel ?countryLabel ?placeLabel (YEAR(?date) as ?year) ?coords ?image
    WHERE
    {
      ?item wdt:P31/wdt:P279* wd:Q860861 .
      ?item wdt:P170 wd:Q123454 .
      OPTIONAL { ?item wdt:P17 ?country . }
      OPTIONAL { ?item wdt:P131 ?place . }
      OPTIONAL { ?item wdt:P571 ?date . }
      OPTIONAL { ?item wdt:P625 ?coords . }
      OPTIONAL { ?item wdt:P18 ?image . }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
    }
    ORDER BY ?itemLabel ?placeLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Sculptures_by_Max_Bill
  title: Sculptures by Max Bill
  description: ''
- name: Paintings by Rembrandt in the Louvre or the Rijkmuseum
  namespace: examples
  domain: wikidata.org
  query_id: paintings-by-rembrandt-in-the-louvre-or-the-rijkmuseum--examples@wikidata.org
  sparql: |
    #defaultView:ImageGrid

    SELECT DISTINCT ?item ?itemLabel ?itemDescription ?image ?collection WHERE {
        ?item wdt:P31 wd:Q3305213 ; # Get items that are instances of painting
              wdt:P170 wd:Q5598 ; # By creator Rembrandt
              wdt:P195/wdt:P361* ?collection . # That are in some collection

        # Only return results where 'collection' is either Rijkmuseum or Louvre
        FILTER ( ?collection = wd:Q190804 {{!}}{{!}} ?collection = wd:Q19675 )

        OPTIONAL { ?item wdt:P18 ?image } # Optionally with an image

        SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Paintings_by_Rembrandt_in_the_Louvre_or_the_Rijkmuseum
  title: Paintings by Rembrandt in the Louvre or the Rijkmuseum
  description: ''
- name: Authority control properties usage for paintings
  namespace: examples
  domain: wikidata.org
  query_id: authority-control-properties-usage-for-paintings--examples@wikidata.org
  sparql: |2

    # ----
    # Make a list of the most used authority control properties for works for paintings
    SELECT ?propertyLabel ?propertyDescription (COUNT(?propertyclaim) AS ?count) WHERE {
      ?item wdt:P31 wd:Q3305213 .

      ?property wikibase:propertyType wikibase:ExternalId .
      ?property wdt:P31 wd:Q44847669 .
      ?property wikibase:claim ?propertyclaim .

      ?item ?propertyclaim [] .
      SERVICE wikibase:label { # ... include the labels
        bd:serviceParam wikibase:language "en" .
      }
    } GROUP BY ?propertyLabel ?propertyDescription ORDER BY DESC (?count)
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authority_control_properties_usage_for_paintings
  title: Authority control properties usage for paintings
  description: ''
- name: Authority control properties usage for painters
  namespace: examples
  domain: wikidata.org
  query_id: authority-control-properties-usage-for-painters--examples@wikidata.org
  sparql: |2

    # ----
    # Make a list of the most used authority control properties for people for painters
    SELECT ?propertyLabel ?propertyDescription ?count WHERE {
      {
        SELECT ?propertyclaim (COUNT(*) AS ?count) where {
          ?item wdt:P106 wd:Q1028181 .
          ?item wdt:P31 wd:Q5 .
          ?item ?propertyclaim [] .
        } GROUP BY ?propertyclaim
      }
      ?property wikibase:propertyType wikibase:ExternalId .
      ?property wdt:P31 wd:Q19595382 .
      ?property wikibase:claim ?propertyclaim .
      SERVICE wikibase:label { # ... include the labels
        bd:serviceParam wikibase:language "en" .
      }
    } ORDER BY DESC (?count)
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Authority_control_properties_usage_for_painters
  title: Authority control properties usage for painters
  description: ''
- name: Ten random painting images
  namespace: examples
  domain: wikidata.org
  query_id: ten-random-painting-images--examples@wikidata.org
  sparql: |2

    # ----
    # This returns 10 random painting images
    # RAND() returns one random number (cached like every query).
    # The string representation of the item and the random number are hashed.
    # This will give a complete different ordering every time you have a different random number.
    # You can change the LIMIT or OFFSET if you want to trigger a new random number
    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?image (MD5(CONCAT(str(?item),str(RAND()))) as ?random) WHERE {
      ?item wdt:P31 wd:Q3305213.
      ?item wdt:P18 ?image.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en"}
    } ORDER BY ?random
    LIMIT 10
    OFFSET 0
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Ten_random_painting_images
  title: Ten random painting images
  description: |-
    In cases where you want to re-run the query without getting a cached result, see the following blog post:

    * https://byabbe.se/2018/01/26/cache-busting-wikidata-sparql-queries
- name: Painters related to anonymous works
  namespace: examples
  domain: wikidata.org
  query_id: painters-related-to-anonymous-works--examples@wikidata.org
  sparql: |2

    # ----
    #Painters related in some way to any anonymous painting
    #defaultView:BubbleChart
    SELECT ?creatorqualifierLabel (COUNT(?creatorqualifier) AS ?count) WHERE {
      ?item wdt:P31 wd:Q3305213 .
      ?item wdt:P170 ?creatorvalue .
      FILTER wikibase:isSomeValue(?creatorvalue)
      OPTIONAL { ?item p:P170 ?creatorstatement .
                 ?creatorstatement rdf:type wikibase:BestRank .
                 ?creatorstatement ?qualifier ?creatorqualifier .
                 ?qualifierproperty wikibase:qualifier ?qualifier }
      FILTER(?qualifierproperty != wd:P3831)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
      } GROUP BY ?creatorqualifierLabel
    HAVING (?count > 2)
    LIMIT 20000
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Painters_related_to_anonymous_works
  title: Painters related to anonymous works
  description: ''
- name: Painters type of relations with anonymous works
  namespace: examples
  domain: wikidata.org
  query_id: painters-type-of-relations-with-anonymous-works--examples@wikidata.org
  sparql: |2

    # ----
    #Types of relationships that link painters to anonymous paintings
    #defaultView:BubbleChart
    SELECT ?qualifierpropertyLabel (COUNT(?creatorqualifier) AS ?count) WHERE {
      ?item wdt:P31 wd:Q3305213 .
      ?item wdt:P170 ?creatorvalue .
      FILTER wikibase:isSomeValue(?creatorvalue)
      OPTIONAL { ?item p:P170 ?creatorstatement .
                 ?creatorstatement rdf:type wikibase:BestRank .
                 ?creatorstatement ?qualifier ?creatorqualifier .
                 ?qualifierproperty wikibase:qualifier ?qualifier }
      FILTER(?qualifierproperty != wd:P3831)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
      } GROUP BY ?qualifierpropertyLabel
    HAVING (?count > 2)
    LIMIT 2000
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Painters_type_of_relations_with_anonymous_works
  title: Painters type of relations with anonymous works
  description: ''
- name: Using formatter url to construct links in SPARQL
  namespace: examples
  domain: wikidata.org
  query_id: using-formatter-url-to-construct-links-in-sparql--examples@wikidata.org
  sparql: |2

    # ----
    # Get 10 paintings that have a link to RKDimages (P350)
    # Use the formatter URL (P1630) to construct the links to RKDimages
    #defaultView:ImageGrid
    SELECT ?item ?image ?rkdurl WHERE {
      wd:P350 wdt:P1630 ?formatterurl .
      ?item wdt:P31 wd:Q3305213 .
      ?item wdt:P18 ?image .
      ?item wdt:P350 ?rkdid .
      BIND(IRI(REPLACE(?rkdid, '^(.+)$', ?formatterurl)) AS ?rkdurl).
      } LIMIT 10
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Using_formatter_url_to_construct_links_in_SPARQL
  title: Using formatter url to construct links in SPARQL
  description: ''
- name: Map of the locations of all paintings by Johannes Vermeer with an image
  namespace: examples
  domain: wikidata.org
  query_id: map-of-the-locations-of-all-paintings-by-johannes-vermeer-with-an-image--examples@wikidata.org
  sparql: |2

    #defaultView:Map
    SELECT ?painting ?paintingLabel ?location ?image where {
      ?painting wdt:P276 ?collection;
                wdt:P170 wd:Q41264;
                wdt:P18 ?image.
      ?collection wdt:P625 ?location.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Map_of_the_locations_of_all_paintings_by_Johannes_Vermeer_with_an_image
  title: Map of the locations of all paintings by Johannes Vermeer with an image
  description: ''
- name: All impressionist painters that have been in an exhibition, together with
    the amount of exhibitions they have been in
  namespace: examples
  domain: wikidata.org
  query_id: all-impressionist-painters-that-have-been-in-an-exhibition-together-with-the-amount-of-exhibitions-they-have-been-in--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:783--> Impressionist painters by number of exhibitions</translate>
    SELECT DISTINCT ?painter ?painterLabel (count (DISTINCT ?exhibition) as ?exhibition_count)
    (group_concat(DISTINCT ?exhibitionLabel; separator=", ") as ?exhibitions)
    WHERE {
      ?painter wdt:P106 wd:Q1028181 . #give me all people with occupation (P106) painter (Q1028181)
      ?painter wdt:P135 wd:Q40415 . #who belonged to the impressionist (Q40415) movement (P135)
      ?painting wdt:P170 ?painter . #the paintings created by (P170) the painter
      ?painting wdt:P608 ?exhibition . #have an exhibition history (P608) at an exhibition
      ?exhibition rdfs:label ?exhibitionLabel . #give me the english Labels of these exhibitions, if possible
      FILTER (lang(?exhibitionLabel) = "en")

      SERVICE wikibase:label {bd:serviceParam wikibase:language "en".}
    } GROUP BY ?painter ?painterLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#All_impressionist_painters_that_have_been_in_an_exhibition,_together_with_the_amount_of_exhibitions_they_have_been_in
  title: All impressionist painters that have been in an exhibition, together with
    the amount of exhibitions they have been in
  description: ''
- name: List of theatre plays
  namespace: examples
  domain: wikidata.org
  query_id: list-of-theatre-plays--examples@wikidata.org
  sparql: |2


    SELECT ?play ?playLabel
    WHERE
    {
      ?play wdt:P31 wd:Q25379 .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en,es,pt,de,fr,ja,zh" . }
    }
    LIMIT 1000
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#List_of_theatre_plays
  title: List of theatre plays
  description: ''
- name: Public art in Paris
  namespace: examples
  domain: wikidata.org
  query_id: public-art-in-paris--examples@wikidata.org
  sparql: |2

    SELECT
      ?item
      (SAMPLE (?titleL) AS ?title)
      (group_concat(DISTINCT ?creatorL ; separator = ", ") as ?creator)
      (group_concat(DISTINCT ?genreL ; separator = ", ") as ?genre)
      (group_concat(DISTINCT ?placeL ; separator = ", ") as ?place)
      (group_concat(DISTINCT ?arr ; separator = ", ") as ?arrondissement)
      (SAMPLE (?img) AS ?image)
      (SAMPLE (?coord) AS ?coordinates) {

        {
          SELECT DISTINCT ?item { {
            ?item wdt:P136 wd:Q557141 ;     # genre: public art
                  wdt:P131 wd:Q90           # located in: Paris
          } UNION { # or
            ?item wdt:P136 wd:Q557141 ;     # genre: public art
                  wdt:P131/wdt:P131* wd:Q90 # located in an arrondissement of Paris
          } }
        }

        # title
        OPTIONAL { ?item rdfs:label ?titleL FILTER (lang(?titleL) = "fr") }

        # creators
        OPTIONAL { ?item wdt:P170 [rdfs:label ?creatorL] FILTER (lang(?creatorL) = "fr") }

        #genre
        OPTIONAL {
          {
            ?item wdt:P136 ?g FILTER (STR(?g) != 'http://www.wikidata.org/entity/Q557141')
          } UNION {
            ?item wdt:P31 ?g .
          }
          ?g rdfs:label ?genreL FILTER (lang(?genreL) = "fr") .
        }

        # place
        OPTIONAL {
          ?item wdt:P276 [rdfs:label ?placeL] FILTER (lang(?placeL) = "fr") .
        }

        # arrondissement
        OPTIONAL {
          ?item wdt:P131 [wdt:P131 wd:Q90 ; rdfs:label ?arrL] FILTER (lang(?arrL) = "fr").
          BIND(REPLACE(?arrL, '^([0-9]+).*$', "$1", "si") AS ?arr)
        }

        # image
        OPTIONAL { ?item wdt:P18 ?img }

        # coordinates
        OPTIONAL { ?item wdt:P625 ?coord }

    } GROUP BY ?item
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Public_art_in_Paris
  title: Public art in Paris
  description: ''
- name: Works of art where the name might be a rhyme
  namespace: examples
  domain: wikidata.org
  query_id: works-of-art-where-the-name-might-be-a-rhyme--examples@wikidata.org
  sparql: |2

    SELECT ?work ?title
    WHERE
    {
      ?work wdt:P31/wdt:P279* wd:Q838948;
            wdt:P1476 ?title.
      FILTER(REGEX(?title, "^\\w*(\\w{3})(\\W+\\w*\\1)+$", "i") && !REGEX(?title, "^(\\w+)(\\W+\\1)+$", "i")).
    }
    ORDER BY STR(?title)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Works_of_art_where_the_name_might_be_a_rhyme
  title: Works of art where the name might be a rhyme
  description: ''
- name: Works of art where the title is an alliteration
  namespace: examples
  domain: wikidata.org
  query_id: works-of-art-where-the-title-is-an-alliteration--examples@wikidata.org
  sparql: |2

    SELECT ?work ?title
    WHERE
    {
      ?work wdt:P31/wdt:P279* wd:Q838948;
            wdt:P1476 ?title.
      FILTER(REGEX(STR(?title), "^(\\p{L})\\w+(?:\\W+\\1\\w+){2,}$", "i")).
    }
    ORDER BY STR(?title)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Works_of_art_where_the_title_is_an_alliteration
  title: Works of art where the title is an alliteration
  description: ''
- name: Distribution of public art by place
  namespace: examples
  domain: wikidata.org
  query_id: distribution-of-public-art-by-place--examples@wikidata.org
  sparql: |2


    SELECT ?place ?placeLabel (COUNT(*) AS ?count) WHERE {
      ?item wdt:P136 wd:Q557141 .
      ?item wdt:P131 ?place .
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
    }
    GROUP BY ?place ?placeLabel
    ORDER BY DESC(?count) ?placeLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Distribution_of_public_art_by_place
  title: Distribution of public art by place
  description: ''
- name: Birthplaces of Europeana280 artists
  namespace: examples
  domain: wikidata.org
  query_id: birthplaces-of-europeana280-artists--examples@wikidata.org
  sparql: |2


    #defaultView:Map
    SELECT ?creator ?creatorLabel ?placebirthLabel ?geoloc where {
      ?item wdt:P31/wdt:P279* wd:Q838948 . # œuvre d’art et ss-classe
      ?item wdt:P608 wd:Q20980830 . # du projet Europeana 280
      ?item wdt:P170 ?creator . # créateur
      ?creator wdt:P19 ?placebirth . # lieu de naissance
      ?placebirth wdt:P625 ?geoloc . #coordonnées géo
      SERVICE wikibase:label {
           bd:serviceParam wikibase:language "fr,es,en" .
        }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Birthplaces_of_Europeana280_artists
  title: Birthplaces of Europeana280 artists
  description: ''
- name: Female artists
  namespace: examples
  domain: wikidata.org
  query_id: female-artists--examples@wikidata.org
  sparql: "\n\nSELECT DISTINCT ?women ?women_label ?women_description\nWHERE\n{\n\
    \       ?women wdt:P31 wd:Q5 .\n       ?women wdt:P21 wd:Q6581072 .\n       ?women\
    \ wdt:P106/wdt:P279* wd:Q483501 . # artists\n       OPTIONAL {?women rdfs:label\
    \ ?women_label FILTER (LANG(?women_label) = \"en\")}.\n\t   OPTIONAL {?women schema:description\
    \ ?women_description FILTER (LANG(?women_description) = \"en\")}.\n}\nLIMIT 500\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Female_artists
  title: Female artists
  description: ''
- name: Common phrases
  namespace: examples
  domain: wikidata.org
  query_id: common-phrases--examples@wikidata.org
  sparql: |2


    SELECT ?q ?qLabel
    WHERE
    {
      ?q wdt:P31 wd:Q15841920.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Common_phrases
  title: Common phrases
  description: "* Items used: \n* Properties used:"
- name: Padua University Rectors by dates
  namespace: examples
  domain: wikidata.org
  query_id: padua-university-rectors-by-dates--examples@wikidata.org
  sparql: |2

    SELECT ?rettore ?rettoreLabel ?starttime ?endtime WHERE {
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
      ?rettore p:P106 [
        ps:P106 wd:Q212071; # is a Rector
        pq:P642 wd:Q193510; # of Padua Univerity
        pq:P580 ?starttime;
        pq:P582 ?endtime
      ].
    }
    ORDER BY ?starttime
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Padua_University_Rectors_by_dates
  title: Padua University Rectors by dates
  description: ''
- name: Top 100 podcasts by number of statements
  namespace: examples
  domain: wikidata.org
  query_id: top-100-podcasts-by-number-of-statements--examples@wikidata.org
  sparql: |2

    # ----
    # Top 100 podcasts by number of statements
    SELECT ?item ?itemLabel ?statements WHERE
    {
      ?item wdt:P31 wd:Q24634210. # Instance of: podcast
      ?item wikibase:statements ?statements. # Number of statements
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
    ORDER BY desc(?statements) # Sort by number of statements
    LIMIT 100 # Only the top 100 items
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Top_100_podcasts_by_number_of_statements
  title: Top 100 podcasts by number of statements
  description: ''
- name: German breweries
  namespace: examples
  domain: wikidata.org
  query_id: german-breweries--examples@wikidata.org
  sparql: |2

    # ----
    #Locations of breweries in Germany
    #defaultView:Map
    SELECT ?breweryLabel ?breweryDescription ?coord
    WHERE
    {
      ?brewery wdt:P31/wdt:P279* wd:Q131734 ;
        wdt:P17 wd:Q183 ;
        wdt:P625 ?coord .
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en, de" .
      }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#German_breweries
  title: German breweries
  description: ''
- name: Sandwiches
  namespace: examples
  domain: wikidata.org
  query_id: sandwiches--examples@wikidata.org
  sparql: |2


    #defaultView:ImageGrid
    SELECT ?item ?itemLabel ?_image
    WHERE
    {
      ?item wdt:P279 wd:Q28803.
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    OPTIONAL { ?item wdt:P18 ?_image. }
    }
    LIMIT 100
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Sandwiches
  title: Sandwiches
  description: ''
- name: Sandwich ingredients
  namespace: examples
  domain: wikidata.org
  query_id: sandwich-ingredients--examples@wikidata.org
  sparql: |2

    SELECT ?sandwich ?ingredient ?sandwichLabel ?ingredientLabel
    WHERE
    {
      ?sandwich wdt:P31?/wdt:P279* wd:Q28803;
                wdt:P527 ?ingredient.
      MINUS { ?ingredient wdt:P279* wd:Q7802. }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en", "fr". }
    }
    ORDER BY UCASE(STR(?sandwichLabel))
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Sandwich_ingredients
  title: Sandwich ingredients
  description: ''
- name: Mushers with neither a ranking in a race nor a reason for not finishing it
  namespace: examples
  domain: wikidata.org
  query_id: mushers-with-neither-a-ranking-in-a-race-nor-a-reason-for-not-finishing-it--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?race ?raceLabel ?musherLabel
    WHERE
    {
      { ?race wdt:P31/wdt:P279* wd:Q1968664 . }
      UNION { ?race wdt:P31/wdt:P641* wd:Q1968664 . }
      ?race p:P710 ?musherS . #here we have a full statement, not a value
      ?musherS ps:P710 ?musher . #here we get the value
      FILTER NOT EXISTS { ?musherS pq:P1352 ?rank }
      FILTER NOT EXISTS { ?musherS pq:P793 ?event }
      SERVICE wikibase:label { bd:serviceParam wikibase:language "fr,en" }
    }
    ORDER BY ?raceLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Mushers_with_neither_a_ranking_in_a_race_nor_a_reason_for_not_finishing_it
  title: Mushers with neither a ranking in a race nor a reason for not finishing it
  description: "* Items used: \n* Properties used: , , , , ,"
- name: What is the relation between Terrell Buckley and Miami Dolphins?
  namespace: examples
  domain: wikidata.org
  query_id: what-is-the-relation-between-terrell-buckley-and-miami-dolphins--examples@wikidata.org
  sparql: |2

    SELECT ?l
    WHERE {
      wd:Q5571382 ?p wd:Q223243 .
      ?property ?ref ?p .
      ?property rdf:type wikibase:Property .
      ?property rdfs:label ?l FILTER (lang(?l) = "en")
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#What_is_the_relation_between_Terrell_Buckley_and_Miami_Dolphins?
  title: What is the relation between Terrell Buckley and Miami Dolphins?
  description: '*  Items used: ,'
- name: The awards received by Cristiano Ronaldo by year
  namespace: examples
  domain: wikidata.org
  query_id: the-awards-received-by-cristiano-ronaldo-by-year--examples@wikidata.org
  sparql: |
    SELECT ?entity ?desc (year(?date) as ?year) {
      wd:Q11571 p:P166 [ps:P166 ?entity ; pq:P585 ?date ]
      OPTIONAL { ?entity rdfs:label ?desc FILTER((LANG(?desc)) = "en") }
    } ORDER BY ?year
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#The_awards_received_by_Cristiano_Ronaldo_by_year
  title: The awards received by Cristiano Ronaldo by year
  description: Shows only the year instead of the default format (i.e. show all of
    the date information).
- name: Winners of the 2024 Summer Olympics
  namespace: examples
  domain: wikidata.org
  query_id: winners-of-the-2024-summer-olympics--examples@wikidata.org
  sparql: |-
    SELECT DISTINCT ?eventLabel ?goldCountryLabel ?silverCountryLabel ?bronzeCountryLabel WHERE {
      ?event wdt:P31 wd:Q18536594; # individual event
             wdt:P361+ wd:Q995653; # part of 2024 summer olympics
             (p:P710
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Winners_of_the_2024_Summer_Olympics
  title: Winners of the 2024 Summer Olympics
  description: Shows the country winning the gold, silver, and bronze medal for each
    event in the 2024 Summer Olympics.
- name: Wikidata statements with a reference to data in Figshare
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-statements-with-a-reference-to-data-in-figshare--examples@wikidata.org
  sparql: |2

    SELECT ?doi (count(?doi) as ?counts) WHERE {
       ?statement prov:wasDerivedFrom ?ref .
       ?ref pr:P356 ?doi .
       FILTER (CONTAINS(lcase(?doi), "figshare"))
    }
    GROUP BY ?doi
    ORDER BY DESC(?counts)
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_statements_with_a_reference_to_data_in_Figshare
  title: Wikidata statements with a reference to data in Figshare
  description: ''
- name: Wikidata statement with a reference to data in Figshare of which a Wikicite
    item exists
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-statement-with-a-reference-to-data-in-figshare-of-which-a-wikicite-item-exists--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?wikiciteLabel ?doi WHERE {
       ?wikicite p:P356/ps:P356 ?doi .
       ?statement prov:wasDerivedFrom ?ref .
       ?ref pr:P356 ?doi .
       FILTER (CONTAINS(lcase(?doi), "figshare"))
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_statement_with_a_reference_to_data_in_Figshare_of_which_a_Wikicite_item_exists
  title: Wikidata statement with a reference to data in Figshare of which a Wikicite
    item exists
  description: ''
- name: Wikidata statements with a reference to a Figshare DOI (<tvar name=1>[[Q28061352]]</tvar>)
  namespace: examples
  domain: wikidata.org
  query_id: wikidata-statements-with-a-reference-to-a-figshare-doi-tvar-name-1-q28061352-tvar--examples@wikidata.org
  sparql: |2

    SELECT DISTiNCT ?item ?itemLabel WHERE {
       ?item ?p ?statement.
       ?statement prov:wasDerivedFrom ?ref .
       ?ref ?prop wd:Q28061352 .
       SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Wikidata_statements_with_a_reference_to_a_Figshare_DOI_(<tvar_name=1>[[Q28061352]]</tvar>)
  title: Wikidata statements with a reference to a Figshare DOI (<tvar name=1>[[Q28061352]]</tvar>)
  description: ''
- name: Scholarly articles with "Zika" in the item label
  namespace: examples
  domain: wikidata.org
  query_id: scholarly-articles-with-zika-in-the-item-label--examples@wikidata.org
  sparql: |
    true
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Scholarly_articles_with_"Zika"_in_the_item_label
  title: Scholarly articles with "Zika" in the item label
  description: ''
- name: Scientific articles that have subject Zika virus or fever and that are used
    as a reference in another item
  namespace: examples
  domain: wikidata.org
  query_id: scientific-articles-that-have-subject-zika-virus-or-fever-and-that-are-used-as-a-reference-in-another-item--examples@wikidata.org
  sparql: "true\n "
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Scientific_articles_that_have_subject_Zika_virus_or_fever_and_that_are_used_as_a_reference_in_another_item
  title: Scientific articles that have subject Zika virus or fever and that are used
    as a reference in another item
  description: ''
- name: CIViC Corpus
  namespace: examples
  domain: wikidata.org
  query_id: civic-corpus--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?pmid ?citation ?citationLabel
    WHERE
    {
      VALUES ?predictor {p:P3354 p:P3355 p:P3356 p:P3357 p:P3358 p:P3359 }
        ?item p:P3329 ?civicId ;
              ?predictor ?predictor_value .
        ?civicId ps:P3329 ?id .
        ?predictor_value prov:wasDerivedFrom ?reference .
        ?reference pr:P248 ?citation .
        ?citation wdt:P698 ?pmid ;
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#CIViC_Corpus
  title: CIViC Corpus
  description: ''
- name: Get the CIViC citation corpus
  namespace: examples
  domain: wikidata.org
  query_id: get-the-civic-citation-corpus--examples@wikidata.org
  sparql: |2

    SELECT DISTINCT ?pmid ?citation ?citationLabel
    WHERE
    {
      VALUES ?predictor {p:P3354 p:P3355 p:P3356 p:P3357 p:P3358 p:P3359 }
        ?item p:P3329 ?civicId ;
              ?predictor ?predictor_value .
        ?civicId ps:P3329 ?id .
        ?predictor_value prov:wasDerivedFrom ?reference .
        ?reference pr:P248 ?citation .
        ?citation wdt:P698 ?pmid ;
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_the_CIViC_citation_corpus
  title: Get the CIViC citation corpus
  description: ''
- name: Get the Pathways citation corpus
  namespace: examples
  domain: wikidata.org
  query_id: get-the-pathways-citation-corpus--examples@wikidata.org
  sparql: |2

    SELECT ?id ?item ?itemLabel ?referenceLabel
    WHERE
    {
      {?item wdt:P3937 ?id } UNION
            {?item wdt:P2410 ?id }
            ?item wdt:P2860 ?reference .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_the_Pathways_citation_corpus
  title: Get the Pathways citation corpus
  description: ''
- name: Get the Wikipathways citation corpus
  namespace: examples
  domain: wikidata.org
  query_id: get-the-wikipathways-citation-corpus--examples@wikidata.org
  sparql: |2

    SELECT ?wpid ?item ?itemLabel ?referenceLabel
    WHERE
    {
      ?item wdt:P2410 ?wpid ;
              wdt:P2860 ?reference ;

      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_the_Wikipathways_citation_corpus
  title: Get the Wikipathways citation corpus
  description: ''
- name: Get the Reactome citation corups
  namespace: examples
  domain: wikidata.org
  query_id: get-the-reactome-citation-corups--examples@wikidata.org
  sparql: |2

    SELECT ?reactome_id ?item ?itemLabel ?referenceLabel
    WHERE
    {
      ?item wdt:P3937 ?reactome_id ;
            wdt:P2860 ?reference ;

      SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Get_the_Reactome_citation_corups
  title: Get the Reactome citation corups
  description: ''
- name: Suriname citation corpora
  namespace: examples
  domain: wikidata.org
  query_id: suriname-citation-corpora--examples@wikidata.org
  sparql: |2

    SELECT ?item ?itemLabel ?_PubMed_ID
    WHERE
    {
      ?item wdt:P31 wd:Q13442814 ;
            rdfs:label ?itemLabel .

      FILTER(CONTAINS(LCASE(?itemLabel), "suriname"))
      OPTIONAL { ?item wdt:P698 ?_PubMed_ID. }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Suriname_citation_corpora
  title: Suriname citation corpora
  description: ''
- name: English common names and information for animals given their scientific names
  namespace: examples
  domain: wikidata.org
  query_id: english-common-names-and-information-for-animals-given-their-scientific-names--examples@wikidata.org
  sparql: |2

    # ----
    # Given the scientific name for a list of animals, it will return all the
    # English common names, their length, life expectency, height, wing span and mass
    # Note: There is more information on these specific animals. Unfortunately, a lot of animals on WikiData have missing information (e.g. no life expectancy or mass)
    SELECT DISTINCT ?item ?scientific_name ?common_name ?length ?life_expectency ?height ?wing_span ?mass WHERE {
      ?item wdt:P225 ?scientific_name;
        wdt:P1843 ?common_name.
      OPTIONAL { ?item wdt:P2043 ?length. }
      OPTIONAL { ?item wdt:P2250 ?life_expectency. }
      OPTIONAL { ?item wdt:P2048 ?height. }
      OPTIONAL { ?item wdt:P2050 ?wing_span. }

      # Adult mass only. Excludes birth mass
      OPTIONAL {
        ?item p:P2067 ?mass_statement_node.
        ?mass_statement_node pq:P642 wd:Q78101716;
          ps:P2067 ?mass.
      }

      # Only return common names in English
      FILTER(LANGMATCHES(LANG(?common_name), "en"))

      # List of animals. All lowercase to avoid capitalisation issues
      FILTER(lcase(str(?scientific_name)) IN (
       "mustela erminea",
       "aquila adalberti",
       "vespula germanica",
       "accipiter nisus",
       "buteo buteo"
      ))
      SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#English_common_names_and_information_for_animals_given_their_scientific_names
  title: English common names and information for animals given their scientific names
  description: ''
- name: External identifiers
  namespace: examples
  domain: wikidata.org
  query_id: external-identifiers--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:784--> Swedish municipalities which changed their municipality identifier at some point</translate>
    #author:Salgo60 2021-09-11
    SELECT DISTINCT ?item ?itemLabel ?kkod1 ?kkod2 ?svwp_artikel WHERE
    {
      ?item wdt:P525 ?kkod1 .
      ?item wdt:P525 ?kkod2 .
      FILTER ( ?kkod1<?kkod2 )
      SERVICE wikibase:label { bd:serviceParam wikibase:language "sv". }
      OPTIONAL { ?svwp_artikel schema:about ?item; schema:isPartOf <https://sv.wikipedia.org/> }
    }
    ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#External_identifiers
  title: External identifiers
  description: This is an example of an identifier which is unstable over time and
    thus unsuitable to be used as an identifier in e.g. an IT-system.
- name: Swedish municipalities which changed their municipality identifier at some
    point
  namespace: examples
  domain: wikidata.org
  query_id: swedish-municipalities-which-changed-their-municipality-identifier-at-some-point--examples@wikidata.org
  sparql: |2

    # ----
    #title:<translate><!--T:784--> Swedish municipalities which changed their municipality identifier at some point</translate>
    #author:Salgo60 2021-09-11
    SELECT DISTINCT ?item ?itemLabel ?kkod1 ?kkod2 ?svwp_artikel WHERE
    {
      ?item wdt:P525 ?kkod1 .
      ?item wdt:P525 ?kkod2 .
      FILTER ( ?kkod1<?kkod2 )
      SERVICE wikibase:label { bd:serviceParam wikibase:language "sv". }
      OPTIONAL { ?svwp_artikel schema:about ?item; schema:isPartOf <https://sv.wikipedia.org/> }
    }
    ORDER BY ?itemLabel
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Swedish_municipalities_which_changed_their_municipality_identifier_at_some_point
  title: Swedish municipalities which changed their municipality identifier at some
    point
  description: This is an example of an identifier which is unstable over time and
    thus unsuitable to be used as an identifier in e.g. an IT-system.
- name: Works from the Biodiversity Heritage Library (combining results from both
    graphs with UNION)
  namespace: examples
  domain: wikidata.org
  query_id: works-from-the-biodiversity-heritage-library-combining-results-from-both-graphs-with-union--examples@wikidata.org
  sparql: |2

    # ----
    # NOTE - This scholarly query is written for use in WDQS-Main UI only  (query.wikidata.org).

    #topic: Internal Federation
    SELECT ?item ?bhl_id WHERE {
    {?item wdt:P4327 ?bhl_id .}
    UNION
    {SERVICE wdsubgraph:scholarly_articles {?item wdt:P4327 ?bhl_id . } }

    # Notice the change in number of results by removing the block after or the block before the UNION
    }
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Works_from_the_Biodiversity_Heritage_Library_(combining_results_from_both_graphs_with_UNION)
  title: Works from the Biodiversity Heritage Library (combining results from both
    graphs with UNION)
  description: ''
- name: 'Birthdays of authors in the "Wikidata: The Making Of" article (using hint
    to avoid timeout)'
  namespace: examples
  domain: wikidata.org
  query_id: birthdays-of-authors-in-the-wikidata-the-making-of-article-using-hint-to-avoid-timeout--examples@wikidata.org
  sparql: "\n# ----\n# NOTE - This scholarly query is written for use in WDQS-Main\
    \ UI only  (query.wikidata.org).\n\n#topic: Internal Federation\n#defaultView:Timeline\n\
    \nSELECT ?author ?authorLabel ?dateOfBirth WHERE {\n  \n   # Blazegraph gets confused\
    \ with federation and tries to first get all millions of dates of birth on Wikidata.\n\
    \   # We need to tell it that it should not try and \"optimize\" this query, and\
    \ just follow the order we set.\n   hint:Query hint:optimizer \"None\" .\n   \n\
    \  # First, get the 3 authors of the paper from the scholarly graph.\n  SERVICE\
    \ wdsubgraph:scholarly_articles {\n    wd:Q118130164 wdt:P50 ?author .\n  }\n\
    \  \n  # Then, get their dates of birth from the main graph.\n  ?author wdt:P569\
    \ ?dateOfBirth .\n  \n  # Finally, get the label for their names.\n  SERVICE wikibase:label\
    \ { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  \n  # Remove\
    \ the line for the hint and the query will timeout. \n  # Learn more in https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/WDQS_graph_split/Federation_Limits\n\
    }\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Birthdays_of_authors_in_the_"Wikidata:_The_Making_Of"_article_(using_hint_to_avoid_timeout)
  title: 'Birthdays of authors in the "Wikidata: The Making Of" article (using hint
    to avoid timeout)'
  description: ''
- name: Articles by Lydia Pintscher (using the wikibase:label SERVICE in both graphs)
  namespace: examples
  domain: wikidata.org
  query_id: articles-by-lydia-pintscher-using-the-wikibase-label-service-in-both-graphs--examples@wikidata.org
  sparql: "\n# ----\n# NOTE - This scholarly query is written for use in WDQS-Main\
    \ UI only  (query.wikidata.org).\n\n#topic: Internal Federation\nSELECT\n  ?work\
    \ ?workLabel\n  ?author ?authorLabel\nWHERE {\n    # Lydia Pintscher    \n   \
    \ VALUES ?author {wd:Q18016466} . \n\n   SERVICE wdsubgraph:scholarly_articles\
    \ {\n     # Note that you need to select ?workLabel here too\n     SELECT \n \
    \      ?work ?workLabel\n       ?author\n     WHERE {\n       ?work wdt:P50 ?author\
    \ .\n\n       # This call returns the labels for the ?work variable\n       SERVICE\
    \ wikibase:label { \n         bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\"\
    . \n       }\n     }\n  } \n\n  # This call returns the labels for the ?author\
    \ variable\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language\
    \ \"[AUTO_LANGUAGE],mul,en\".\n  }\n}\n"
  url: https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/queries/examples#Articles_by_Lydia_Pintscher_(using_the_wikibase:label_SERVICE_in_both_graphs)
  title: Articles by Lydia Pintscher (using the wikibase:label SERVICE in both graphs)
  description: ''
